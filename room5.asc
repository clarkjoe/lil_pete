bool crashing = false;
AudioChannel *rickRollChannel;

function turnOnInstrumentPanelPopup()
{
  oInstrumentPanelPopup.Visible = true;
  oToggle.Visible = true;
  oLever.Visible = true;
  oGas.Visible = true;
  oEngineSwitch.Visible = true;
  oDial.Visible = true;
  oGrnBtn1.Visible = true;
  oGrnBtn2.Visible = true;
  oGrnBtn3.Visible = true;
  oGrnBtn4.Visible = true;
  oBlkBtn1.Visible = true;
  oBlkBtn2.Visible = true;
  oBlkBtn3.Visible = true;
  oRedBtn1.Visible = true;
  oRedBtn2.Visible = true;
  oRedBtn3.Visible = true;
  oRedBtn4.Visible = true;
  oRdrBtn1.Visible = true;
  oRdrBtn2.Visible = true;
  oRdrBtn3.Visible = true;
  oRdrBtn4.Visible = true;
  oRadar.Visible = true;
  
  ViewFrame *radarBtn1On = Game.GetViewFrame(RADAR, 5, 4);
  if (oRdrBtn4.Graphic == radarBtn1On.Graphic)
  {
    if (rickRollChannel != null && !rickRollChannel.IsPlaying)
    {
      rickRollChannel = aRick_Roll_Sound_Effect.Play(eAudioPriorityHigh, eRepeat);
      a6001_Airplane_cropped.Stop();
      aAirplane_hum.Stop();
    }
  }
}

function turnOffInstrumentPanelPopup()
{
  oInstrumentPanelPopup.Visible = false;
  oToggle.Visible = false;
  oLever.Visible = false;
  oGas.Visible = false;
  oEngineSwitch.Visible = false;
  oDial.Visible = false;
  oGrnBtn1.Visible = false;
  oGrnBtn2.Visible = false;
  oGrnBtn3.Visible = false;
  oGrnBtn4.Visible = false;
  oBlkBtn1.Visible = false;
  oBlkBtn2.Visible = false;
  oBlkBtn3.Visible = false;
  oRedBtn1.Visible = false;
  oRedBtn2.Visible = false;
  oRedBtn3.Visible = false;
  oRedBtn4.Visible = false;
  oRdrBtn1.Visible = false;
  oRdrBtn2.Visible = false;
  oRdrBtn3.Visible = false;
  oRdrBtn4.Visible = false;
  oRadar.Visible = false;
  
  if (rickRollChannel != null && rickRollChannel.IsPlaying)
  {
    rickRollChannel.Stop();
    a6001_Airplane_cropped.Play(eAudioPriorityHigh, eRepeat);
    aAirplane_hum.Play(eAudioPriorityHigh, eRepeat);
  }
}

function oEngineSwitchSmall_Look(Object *theObject, CursorMode mode)
{
  Display("A sturdy switch labeled \"Engine\" stands out among the controls. Flipping it might hold power over the plane's fate, but Clara knows better than to meddle with it.");
}

function oEngineSwitchSmall_Interact(Object *theObject, CursorMode mode)
{
  cEgo.Walk(theObject.X + 5, theObject.Y + 30, eBlock, eWalkableAreas);
  oEngineSwitch.Visible = true;
  cEgo.Direction(eDirectionUp);
}

function doPlaneDeathSequence()
{
  ViewFrame *engineSwitchOff = Game.GetViewFrame(RANDOM, 1, 1);
  ViewFrame *engineSwitchOn = Game.GetViewFrame(RANDOM, 1, 0);
  
  Display("What a great idea!");
  
  oEngineSwitch.Graphic = engineSwitchOff.Graphic;
  Wait(GetGameSpeed()/2);
  turnOffInstrumentPanelPopup();
  Display("As Clara flips the engine switch, a sudden jolt rocks the plane. The engines sputter and whine before falling silent, leaving an eerie stillness. Panic spreads through the cabin as the aircraft begins to tilt downward, the unmistakable signs of an impending crash.");

  cCopilot.Say("She turned off the engines!");
  oEngineSwitch.Graphic = engineSwitchOn.Graphic;
  cEgo.ChangeRoom(301);
}

function doHyperspaceDeathSequene()
{
  turnOffInstrumentPanelPopup();
  cEgo.LockViewFrame(CLARAFALL_1004, 6, 0);
  cEgo.LockViewOffset(CLARAFALL_1004, 0, 40);
  cPilot.LockView(PILOTSEATED_1705);
  cCopilot.LockView(PILOTSEATED_1705);
  
  Wait(GetGameSpeed() / 2);
  cPilot.Animate(4, 4, eRepeat, eNoBlock, eForwards);
  cCopilot.Animate(4, 4, eRepeat, eNoBlock, eForwards, 4);
  cEgo.Animate(6, 9, eOnce, eNoBlock, eForwards);
  
  oStarField.Visible = true;
  oStarField.SetView(AIRPLANECLOUDS_5002, 3, 0);
  oStarField.Animate(3, 9, eOnce, eBlock);
  oStarField.Animate(4, 9, eOnce, eBlock);
  cEgo.ChangeRoom(306);
}

function oEngineSwitchPopout_Interact(Object *theObject, CursorMode mode)
{
  doPlaneDeathSequence();
}

function oEngineSwitchPopout_Look(Object *theObject, CursorMode mode)
{
  Display("A sturdy switch labeled \"Engine\" stands out among the controls. Flipping it might hold power over the plane's fate, but Clara knows better than to meddle with it.");
}

function hInstrumentPanel_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("The instrument panel is a maze of buttons, switches, and glowing gauges, each vital to the pilot's precise navigation.");
}

function oInsideFirstAidKit_Look(Object *theObject, CursorMode mode)
{
  if (cEgo.HasInventory(iClearTape))
  {
    Display("A compact first aid kit, its contents meticulously organized. Among the usual supplies, a spool of green thread stands out as particularly useful.");
    return;
  }
  
  Display("A compact first aid kit, its contents meticulously organized. Among the usual supplies, a spool of green thread and a roll of clear tape stand out as particularly useful.");
}

function oInsideFirstAidKit_Interact(Object *theObject, CursorMode mode)
{
  oInsideFirstAidKit_Look(theObject, mode);
}

function oGreenThread_Look(Object *theObject, CursorMode mode)
{
  Display("A spool of sturdy green thread, neatly wound and ready for stitching emergencies.");
}

function oGreenThread_Interact(Object *theObject, CursorMode mode)
{
  if (cEgo.HasInventory(iGreenThread))
  {
    Display("You already have green thread in your pocket.");
    return;
  }
  
  cEgo.UnlockView();
  cEgo.LockViewOffset(CLARAFIRSTAIDKIT_1005, 14, 0);
  oGreenThread.Visible = false;
  cEgo.Animate(3, 9, eOnce, eBlock, eForwards);
  Display("You tear off a piece of green thread and place it in you pocket.");
  cEgo.AddInventory(iGreenThread);
  oGreenThread.Visible = true;
}

function oClearTape_Look(Object *theObject, CursorMode mode)
{
  Display("A small roll of clear tape, tucked neatly among the other supplies.");
}

function oClearTape_Interact(Object *theObject, CursorMode mode)
{
  cEgo.UnlockView();
  cEgo.LockViewOffset(CLARAFIRSTAIDKIT_1005, -1, 0);
  cEgo.Animate(4, 9, eOnce, eBlock, eForwards);
  Display("You take the clear tape and place it in your pocket.");
  cEgo.AddInventory(iClearTape);
  oClearTape.Visible = false;  
}

function oFirstAidKit_Look(Object *theObject, CursorMode mode)
{
  Display("The first aid kit is neatly secured, its contents well organized behind a sturdy, red exterior. A sense of reassurance comes from its presence.");
}

function oFirstAidKit_Interact(Object *theObject, CursorMode mode)
{
  if (cEgo.x == (theObject.X + 10) && cEgo.y == theObject.Y)
  {
    cEgo.LockViewOffset(CLARAFIRSTAIDKIT_1005, 1, 0);
  }
  else
  {
    cEgo.Walk(theObject.X + 5, theObject.Y, eBlock, eWalkableAreas);
    cEgo.LockViewOffset(CLARAFIRSTAIDKIT_1005, 1, 0);
    cEgo.x += 5;
  }
  cEgo.Animate(1, 9, eOnce, eBlock, eForwards);
  oFirstAidKit.Visible = false;
  cEgo.Animate(2, 9, eOnce, eBlock, eForwards);
  
  oInsideFirstAidKit.Visible = true;
  oGreenThread.Visible = true;
  if (!cEgo.HasInventory(iClearTape))
  {
    oClearTape.Visible = true;
  }
}

function hCockpitChair_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("The seats in the cockpit are large and ergonomic, designed for long hours of precise control. Below the copilot's seat, you spot something attached to the underside.");
}

function hCockpitWindow_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("The windows offer a picturesque view of the sky, with soft clouds rolling past like a living painting. The sunlight streams through, giving the cabin a warm, serene glow. It's a peaceful moment, but Clara's investigative instincts remind her to stay alert.");
}

int pilotAnimationCycle = 0;
function handlePilot()
{
  if (crashing) return;
  switch(pilotAnimationCycle)
  {
    case 0:
    {
      if (!IsTimerExpired(1)) break;
      
      pilotAnimationCycle++;
    }
    case 1:
    {
      cPilot.LockView(COPILOTSEATED_1708);
      cPilot.Animate(1, 9, eOnce, eNoBlock, eForwards);
      pilotAnimationCycle++;
      
      break;
    }
    case 2:
    {
      if (!cPilot.Animating)
      {
        cPilot.UnlockView();
        pilotAnimationCycle++;
        SetTimer(1, 1000);
        pilotAnimationCycle = 0;
      }
      
      break;
    }
  }
}

bool copilotButtonLoop = 1;
function handleCopilot()
{
  if (crashing) return;
  if (!IsTimerExpired(2)) return;
  
  SetTimer(2, 1500);
  
  cCopilot.LockView(COPILOTSEATED_1708);
  cCopilot.Animate(copilotButtonLoop, 9, eOnce, eNoBlock, eForwards);
  
  if (copilotButtonLoop == 1)
    copilotButtonLoop = 3;
  else
    copilotButtonLoop = 1;
}

function initRadar()
{
  if (player.PreviousRoom != 301)
  {
    oRadar.SetView(AIRPLANEDETAILS_5001);
    oRadar.Animate(4, 18, eRepeat, eNoBlock, eForwards);
  }
}

function initCloud()
{
  if (player.PreviousRoom != 301)
  {
    oCockpitClouds.SetView(AIRPLANECLOUDS_5002);
    oCockpitClouds.Animate(1, 9, eRepeat, eNoBlock, eForwards);
  }
  else
  {
    oCloudsVertical.Visible = true;
    oCockpitClouds.Visible = false;
  }
}

function initClara()
{
  if (player.PreviousRoom != 301)
  {
    cEgo.x = 160;
    cEgo.y = 155;
    cEgo.Direction(eDirectionUp);
  }
  else
  {
    cEgo.LockViewFrame(CLARAFALL_1004, 4, 0);
    cEgo.LockViewOffset(CLARAFALL_1004, -2, -15);
  }
}

function initPilot()
{
  if (player.PreviousRoom != 301)
  {
    cPilot.SetIdleView(PILOTSEATED_1705, 5);
    cPilot.AnimationSpeed = 9;
    cPilot.Loop = 1;
    SetTimer(1, 1000);
  }
  else
  {
    cPilot.LockViewFrame(PILOTSEATED_1705, 4, 0);
  }
}

function initCopilot()
{
  if (player.PreviousRoom != 301)
  {
    cCopilot.SetIdleView(-1, 0);
    cCopilot.AnimationSpeed = 9;
    SetTimer(2, 1500);
  }
  else
  {
    cCopilot.LockViewFrame(PILOTSEATED_1705, 4, 0);
  }
}

function room_Load()
{
  cQueenie.ChangeRoom(3);
  cSteward.ChangeRoom(3);
  if (player.PreviousRoom == 301)
    globalChooseCursor = false;
  initClara();
  initPilot();
  initCopilot();
  //initRadar();
  initCloud();
}

function room_LeaveBottom()
{
  cEgo.ChangeRoom(3);
}

function on_mouse_click(MouseButton button)
{
  if (button == eMouseLeft && Mouse.Mode == eModeExit)
  {
    gIconbar.Visible = true;
    if (oInsideFirstAidKit.Visible)
    {
      cEgo.UnlockView();
      cEgo.LockViewOffset(CLARAFIRSTAIDKIT_1005, 1, 0);
      cEgo.Animate(2, 9, eOnce, eBlock, eBackwards);
      oFirstAidKit.Visible = true;
      cEgo.Animate(1, 9, eOnce, eBlock, eBackwards);
      cEgo.UnlockView();
      
      cEgo.Direction(eDirectionRight);
      oInsideFirstAidKit.Visible = false;
      oGreenThread.Visible = false;
      oClearTape.Visible = false;
      return;
    }
    
    if (oEngineSwitchPopout.Visible)
    {
      oEngineSwitchPopout.Visible = false;
      return;
    }
    
    if (oInstrumentPanelPopup.Visible)
    {
      turnOffInstrumentPanelPopup();
      return;
    }
  }
  
  if (oInsideFirstAidKit.Visible)
  {
    mouse.Mode = eModeExit;
    
    if (oInsideFirstAidKit.IsUnderMouse() || oGreenThread.IsUnderMouse() || oClearTape.IsUnderMouse())
    {
      mouse.Mode = eModeInteract;
    }
  }
  
  if (oEngineSwitchPopout.Visible)
  {
    mouse.Mode = eModeExit;
    
    if (oEngineSwitchPopout.IsUnderMouse())
    {
      mouse.Mode = eModeInteract;
    }
  }
  
  if (!IsGamePaused())
  {
    if (button == eMouseRight && invCursorToggle)
    {
      return;
    }
    
    if ((button == eMouseRight) && (Mouse.IsOverWalkableArea() || Mouse.IsOverNothing()) && !(Mouse.IsOverHotspot()))
    {
      Display("The cockpit is a well-organized maze of buttons, dials, and flickering lights. The controls are precise and intricate, each gauge serving a specific purpose in keeping the plane on course. The atmosphere is one of focused professionalism, with the hum of the engines and the steady movements of the pilot and copilot creating a rhythm of calm authority.");
      ClaimEvent();
    }
  }
}

function handlePopovers()
{
  if (oInsideFirstAidKit.Visible)
  {
    mouse.Mode = eModeExit;
    
    if (oInsideFirstAidKit.IsUnderMouse() || oGreenThread.IsUnderMouse() || oClearTape.IsUnderMouse())
    {
      mouse.Mode = eModeInteract;
    }
  }
  
  if (oEngineSwitch.Visible)
  {
    mouse.Mode = eModeExit;
    
    if (oEngineSwitch.IsUnderMouse())
    {
      mouse.Mode = eModeInteract;
    }
  }
  
  if (oInstrumentPanelPopup.Visible)
  {
    mouse.Mode = eModeExit;
    if (oInstrumentPanelPopup.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oToggle.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oLever.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oGas.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oEngineSwitch.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oDial.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oGrnBtn1.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oGrnBtn2.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oGrnBtn3.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oGrnBtn4.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oBlkBtn1.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oBlkBtn2.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oBlkBtn3.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oRedBtn1.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oRedBtn2.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oRedBtn3.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oRedBtn4.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oRdrBtn1.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oRdrBtn2.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oRdrBtn3.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oRdrBtn4.IsUnderMouse())
      mouse.Mode = eModeInteract;
    else if (oRadar.IsUnderMouse())
      mouse.Mode = eModeInteract;
  }
}

float objectSpeed = 3.0; // Initial speed
int cycleCounter = 0;    // Counter for cycles

function handleVerticalClouds()
{
  if (!crashing) return;

  cycleCounter++;

  // Update the object's position every 10 cycles
  if (cycleCounter % 10 == 0)
  {
    oCloudsVertical.Y -= FloatToInt(objectSpeed, eRoundNearest); // Move up by the current speed

    // Gradually increase the speed
    //objectSpeed += 0.2; // Add a small constant increment to speed
    // Alternatively, you can use multiplication for exponential increase:
    objectSpeed *= 1.09; // Increases speed by 10% each time
  }

  // Check if the object has reached the target position
  if (oCloudsVertical.Y <= 80) // Using '<=' to ensure we don't miss the transition
  {
    cEgo.ChangeRoom(302); // Change room
    return;
  }
}

function room_RepExec()
{
  handlePopovers();
  handleCopilot();
  handlePilot();
  
  handleVerticalClouds();
}

function loadInstrumentPanelPopup()
{
  oInstrumentPanelPopup.Baseline = 199;
  
  int startX = oInstrumentPanelPopup.X;
  int startY = oInstrumentPanelPopup.Y;
  
  oToggle.Baseline = 200;
  oToggle.SetPosition(startX + 118, startY - 15);
  
  oLever.Baseline = 200;
  oLever.SetPosition(startX + 78, startY - 16);
  
  oGas.Baseline = 200;
  oGas.SetPosition(startX + 163, startY - 28);
  
  oEngineSwitch.Baseline = 200;
  oEngineSwitch.SetPosition(startX + 95, startY - 13);
  
  oDial.Baseline = 200;
  oDial.SetPosition(startX + 36, startY - 27);
  
  oGrnBtn1.Baseline = 200;
  oGrnBtn1.SetPosition(startX + 149, startY - 20);
  
  oGrnBtn2.Baseline = 200;
  oGrnBtn2.SetPosition(oGrnBtn1.X, oGrnBtn1.Y);
  
  oGrnBtn3.Baseline = 200;
  oGrnBtn3.SetPosition(oGrnBtn1.X, oGrnBtn1.Y);
  
  oGrnBtn4.Baseline = 200;
  oGrnBtn4.SetPosition(oGrnBtn1.X, oGrnBtn1.Y);
  
  oBlkBtn1.Baseline = 200;
  oBlkBtn1.SetPosition(startX + 16, startY - 25);
  
  oBlkBtn2.Baseline = 200;
  oBlkBtn2.SetPosition(oBlkBtn1.X, oBlkBtn1.Y);
  
  oBlkBtn3.Baseline = 200;
  oBlkBtn3.SetPosition(oBlkBtn1.X, oBlkBtn1.Y);
  
  oRedBtn1.Baseline = 200;
  oRedBtn1.SetPosition(startX + 38, startY - 20);
  
  oRedBtn2.Baseline = 200;
  oRedBtn2.SetPosition(oRedBtn1.X, oRedBtn1.Y);
  
  oRedBtn3.Baseline = 200;
  oRedBtn3.SetPosition(oRedBtn1.X, oRedBtn1.Y);
  
  oRedBtn4.Baseline = 200;
  oRedBtn4.SetPosition(oRedBtn1.X, oRedBtn1.Y);
  
  oRdrBtn1.Baseline = 200;
  oRdrBtn1.SetPosition(startX + 96, startY - 34);
  
  oRdrBtn2.Baseline = 200;
  oRdrBtn2.SetPosition(oRdrBtn1.X, oRdrBtn1.Y);
  
  oRdrBtn3.Baseline = 200;
  oRdrBtn3.SetPosition(oRdrBtn1.X, oRdrBtn1.Y);
  
  oRdrBtn4.Baseline = 200;
  oRdrBtn4.SetPosition(oRdrBtn1.X, oRdrBtn1.Y);
  
  oRadar.Baseline = 200;
  oRadar.SetPosition(startX + 94, startY - 38);
}

function room_AfterFadeIn()
{
  loadInstrumentPanelPopup();
  if (player.PreviousRoom == 301)
  {
    crashing = true;
    //cPilot.LockView(PILOTSEATED_1705);
    cPilot.Animate(4, 4, eRepeat, eNoBlock, eForwards);
    //cCopilot.LockView(PILOTSEATED_1705);
    cCopilot.Animate(4, 4, eRepeat, eNoBlock, eForwards, 4);
    
    //cEgo.LockViewOffset(CLARAFALL_1004, -2, -15);
    cEgo.Animate(4, 4, eRepeat, eNoBlock, eForwards);
  }
}

function room_Unload()
{
}

function hInstrumentPanel_Interact(Hotspot *theHotspot, CursorMode mode)
{
  Point *hPoint = theHotspot.Position();
  if(!cEgo.CancelableWalk(hPoint.x, hPoint.y, eDirectionUp))
    return;
  
  SetRestartPoint();
  gIconbar.Visible = false;
  turnOnInstrumentPanelPopup();
}

function oRdrBtn_Interact(Object *theObject, CursorMode mode)
{
  for (int i = 0; i < (Game.GetFrameCountForLoop(RADAR, 5) - 1); i++)
  {
    ViewFrame *buttonOnFrame = Game.GetViewFrame(RADAR, 5, i+1);
    // return early if clicked button is on
    if (theObject.Graphic == buttonOnFrame.Graphic)
      return;
  }
  
  // turn off all buttons
  ViewFrame *button1Off = Game.GetViewFrame(RADAR, 4, 1);
  oRdrBtn1.Graphic = button1Off.Graphic;
  ViewFrame *button2Off = Game.GetViewFrame(RADAR, 4, 2);
  oRdrBtn2.Graphic = button2Off.Graphic;
  ViewFrame *button3Off = Game.GetViewFrame(RADAR, 4, 3);
  oRdrBtn3.Graphic = button3Off.Graphic;
  ViewFrame *button4Off = Game.GetViewFrame(RADAR, 4, 4);
  oRdrBtn4.Graphic = button4Off.Graphic;
  
  ViewFrame *buttonOn;
  int radarLoopToAnimate;
  // turn on pressed button
  String btnName = theObject.ScriptName;
  if (btnName.MyContains("1"))
  {
    if (rickRollChannel != null && rickRollChannel.IsPlaying)
    {
      rickRollChannel.Stop();
      a6001_Airplane_cropped.Play(eAudioPriorityHigh, eRepeat);
      aAirplane_hum.Play(eAudioPriorityHigh, eRepeat);
    }
    buttonOn = Game.GetViewFrame(RADAR, 5, 1);
    radarLoopToAnimate = 0;
  }
  else if (btnName.MyContains("2"))
  {
    if (rickRollChannel != null && rickRollChannel.IsPlaying)
    {
      rickRollChannel.Stop();
      a6001_Airplane_cropped.Play(eAudioPriorityHigh, eRepeat);
      aAirplane_hum.Play(eAudioPriorityHigh, eRepeat);
    }
    buttonOn = Game.GetViewFrame(RADAR, 5, 2);
    radarLoopToAnimate = 1;
  }
  else if (btnName.MyContains("3"))
  {
    if (rickRollChannel != null && rickRollChannel.IsPlaying)
    {
      rickRollChannel.Stop();
      a6001_Airplane_cropped.Play(eAudioPriorityHigh, eRepeat);
      aAirplane_hum.Play(eAudioPriorityHigh, eRepeat);
    }
    buttonOn = Game.GetViewFrame(RADAR, 5, 3);
    radarLoopToAnimate = 2;
  }
  else
  {
    if (rickRollChannel == null || !rickRollChannel.IsPlaying)
    {
      rickRollChannel = aRick_Roll_Sound_Effect.Play(eAudioPriorityHigh, eRepeat);
      a6001_Airplane_cropped.Stop();
      aAirplane_hum.Stop();
    }
      
    buttonOn = Game.GetViewFrame(RADAR, 5, 4);
    radarLoopToAnimate = 3;
  }
  
  oRadar.Animate(radarLoopToAnimate, 7, eRepeat, eNoBlock, eForwards);
  theObject.Graphic = buttonOn.Graphic;
}

function room_FirstLoad()
{
  ViewFrame *radarButton1On = Game.GetViewFrame(RADAR, 5, 1);
  oRdrBtn1.Graphic = radarButton1On.Graphic;
  oRadar.SetView(RADAR);
  oRadar.Animate(0, 7, eRepeat, eNoBlock, eForwards);
}

function oRedBtn_Interact(Object *theObject, CursorMode mode)
{
  bool buttonIsOn = true;
  for (int i = 0; i < (Game.GetFrameCountForLoop(BUTTONS, 0) - 1); i++)
  {
    ViewFrame *buttonOffFrame = Game.GetViewFrame(BUTTONS, 0, i+1);
    // return early if clicked button is on
    if (theObject.Graphic == buttonOffFrame.Graphic)
    {
      buttonIsOn = false;
      break;
    }
  }
  
  String btnName = theObject.ScriptName;
  int btnNumber;
  if (btnName.MyContains("1"))
    btnNumber = 1;
  else if (btnName.MyContains("2"))
    btnNumber = 2;
  else if (btnName.MyContains("3"))
    btnNumber = 3;
  else
    btnNumber = 4;
    
  ViewFrame *newButtonFrame;
  if (buttonIsOn)
    newButtonFrame = Game.GetViewFrame(BUTTONS, 0, btnNumber);
  else
    newButtonFrame = Game.GetViewFrame(BUTTONS, 1, btnNumber);
    
  theObject.Graphic = newButtonFrame.Graphic;
}

function oBlkBtn_Interact(Object *theObject, CursorMode mode)
{
  bool buttonIsOn = true;
  for (int i = 0; i < (Game.GetFrameCountForLoop(BUTTONS, 3) - 1); i++)
  {
    ViewFrame *buttonOffFrame = Game.GetViewFrame(BUTTONS, 3, i+1);
    // return early if clicked button is on
    if (theObject.Graphic == buttonOffFrame.Graphic)
    {
      buttonIsOn = false;
      break;
    }
  }
  
  String btnName = theObject.ScriptName;
  int btnNumber;
  if (btnName.MyContains("1"))
    btnNumber = 1;
  else if (btnName.MyContains("2"))
    btnNumber = 2;
  else
    btnNumber = 3;
    
  ViewFrame *newButtonFrame;
  if (buttonIsOn)
    newButtonFrame = Game.GetViewFrame(BUTTONS, 3, btnNumber);
  else
    newButtonFrame = Game.GetViewFrame(BUTTONS, 2, btnNumber);
    
  theObject.Graphic = newButtonFrame.Graphic;
}

function oGrnBtn_Interact(Object *theObject, CursorMode mode)
{
  bool buttonIsOn = true;
  for (int i = 0; i < (Game.GetFrameCountForLoop(BUTTONS, 4) - 1); i++)
  {
    ViewFrame *buttonOffFrame = Game.GetViewFrame(BUTTONS, 4, i+1);
    // return early if clicked button is on
    if (theObject.Graphic == buttonOffFrame.Graphic)
    {
      buttonIsOn = false;
      break;
    }
  }
  
  String btnName = theObject.ScriptName;
  int btnNumber;
  if (btnName.MyContains("1"))
    btnNumber = 1;
  else if (btnName.MyContains("2"))
    btnNumber = 2;
  else if (btnName.MyContains("3"))
    btnNumber = 3;
  else
    btnNumber = 4;
    
  ViewFrame *newButtonFrame;
  if (buttonIsOn)
    newButtonFrame = Game.GetViewFrame(BUTTONS, 4, btnNumber);
  else
    newButtonFrame = Game.GetViewFrame(BUTTONS, 5, btnNumber);
    
  theObject.Graphic = newButtonFrame.Graphic;
}

function oInstrumentPanelPopup_Interact(Object *theObject, CursorMode mode)
{

}

function oEngineSwitch_Interact(Object *theObject, CursorMode mode)
{
  SetRestartPoint();
  doPlaneDeathSequence();
}

function oGas_Interact(Object *theObject, CursorMode mode)
{
  if (oGas.View == 0)
    oGas.SetView(RANDOM);
  
  ViewFrame *gasOpenFrame = Game.GetViewFrame(RANDOM, 3, 0);
  if (theObject.Graphic == gasOpenFrame.Graphic)
    theObject.Animate(3, 4, eOnce, eNoBlock, eForwards);
  else
    theObject.Animate(2, 4, eOnce, eNoBlock, eForwards);
}

function oLever_Interact(Object *theObject, CursorMode mode)
{
  Display("Punch it!");
  int currentGraphic = theObject.Graphic;
  if (theObject.View == 0)
    theObject.SetView(RANDOM);
  
  ViewFrame *leverDownFrame = Game.GetViewFrame(RANDOM, 4, 0);
  if (currentGraphic == leverDownFrame.Graphic)
    theObject.Animate(4, 3, eOnce, eBlock, eForwards);
  else
    theObject.Animate(4, 3, eOnce, eBlock, eBackwards);
    
  doHyperspaceDeathSequene();
}

function oToggle_Interact(Object *theObject, CursorMode mode)
{
  int currentGraphic = theObject.Graphic;
  if (theObject.View == 0)
    theObject.SetView(RANDOM);
  
  ViewFrame *toggleLeftFrame = Game.GetViewFrame(RANDOM, 5, 0);
  if (currentGraphic == toggleLeftFrame.Graphic)
    theObject.Animate(5, 4, eOnce, eNoBlock, eForwards);
  else
    theObject.Animate(5, 4, eOnce, eNoBlock, eBackwards);
}
