function hEngineSwitch_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("the engine switch");
}

function hEngineSwitch_Interact(Hotspot *theHotspot, CursorMode mode)
{
  oEngineSwitch.Visible = true;
}

function oEngineSwitch_Interact(Object *theObject, CursorMode mode)
{
  if (theObject.Graphic == 967)
  {
    theObject.Graphic = 968;
  }
  else
  {
    theObject.Graphic = 967;
    Wait(40);
    oEngineSwitch.Visible = false;
  }
}

function oEngineSwitch_Look(Object *theObject, CursorMode mode)
{
  Display("the engine switch");
}

function oInsideFirstAidKit_Look(Object *theObject, CursorMode mode)
{
  Display("Inside of first aid kit");
}

function oInsideFirstAidKit_Interact(Object *theObject, CursorMode mode)
{
  if (cEgo.HasInventory(iGreenThread))
  {
    Display("There is nothing more to get here.");
    
    cEgo.UnlockView();
    cEgo.LockView(CLARAFIRSTAIDKIT_1005);
    cEgo.Animate(2, 9, eOnce, eBlock, eBackwards);
    oFirstAidKit.Visible = true;
    cEgo.Animate(1, 9, eOnce, eBlock, eBackwards);
    cEgo.UnlockView();
    cEgo.FaceDirection(eDirectionRight);
    
    oInsideFirstAidKit.Visible = false;
    
    return;
  }
  
  Display("You pick it up and place it in your purse.");
  cEgo.UnlockView();
  cEgo.LockViewOffset(CLARAFIRSTAIDKIT_1005, 11, 0);
  cEgo.Animate(3, 9, eOnce, eBlock, eForwards);
  cEgo.AddInventory(iGreenThread);
}

function oFirstAidKit_Look(Object *theObject, CursorMode mode)
{
  Display("A first aid kit");
}

function oFirstAidKit_Interact(Object *theObject, CursorMode mode)
{
  cEgo.Walk(theObject.X + 11, theObject.Y, eBlock, eWalkableAreas);
  cEgo.LockView(CLARAFIRSTAIDKIT_1005);
  cEgo.Animate(1, 9, eOnce, eBlock, eForwards);
  oFirstAidKit.Visible = false;
  cEgo.Animate(2, 9, eOnce, eBlock, eForwards);
  
  oInsideFirstAidKit.Visible = true;
}

int pilotAnimationCycle = 0;
function handlePilot()
{ 
  switch(pilotAnimationCycle)
  {
    case 0:
    {
      if (!IsTimerExpired(1)) break;
      
      pilotAnimationCycle++;
    }
    case 1:
    {
      cPilot.LockView(COPILOTSEATED_1708);
      cPilot.Animate(1, 9, eOnce, eNoBlock, eForwards);
      pilotAnimationCycle++;
      
      break;
    }
    case 2:
    {
      if (!cPilot.Animating)
      {
        cPilot.UnlockView();
        pilotAnimationCycle++;
        SetTimer(1, 1000);
        pilotAnimationCycle = 0;
      }
      
      break;
    }
  }
}

bool copilotButtonLoop = 1;
function handleCopilot()
{
  if (!IsTimerExpired(2)) return;
  
  SetTimer(2, 1500);
  
  cCopilot.LockView(COPILOTSEATED_1708);
  cCopilot.Animate(copilotButtonLoop, 9, eOnce, eNoBlock, eForwards);
  
  if (copilotButtonLoop == 1)
    copilotButtonLoop = 3;
  else
    copilotButtonLoop = 1;
}

function initRadar()
{
  oRadar.SetView(AIRPLANEDETAILS_5001);
  oRadar.Animate(4, 18, eRepeat, eNoBlock, eForwards);
}

function initClara()
{
  cEgo.x = 160;
  cEgo.y = 155;
  cEgo.FaceDirection(eDirectionUp);
}

function initPilot()
{
  cPilot.SetIdleView(PILOTSEATED_1705, 5);
  cPilot.AnimationSpeed = 9;
  cPilot.Loop = 1;
  SetTimer(1, 1000);
}

function initCopilot()
{
  cCopilot.SetIdleView(-1, 0);
  cCopilot.AnimationSpeed = 9;
  SetTimer(2, 1500);
}

function room_Load()
{
  initClara();
  initPilot();
  initCopilot();
  initRadar();
}

function room_LeaveBottom()
{
  cEgo.ChangeRoom(3);
}

bool test = true;
Overlay *gameInfoOverlay;
function room_RepExec()
{
  if (test) {
    test = false;
  }
  if (gameInfoOverlay != null) {
    gameInfoOverlay.Remove();
  }
  gameInfoOverlay = Overlay.CreateTextual(50, 150, 120, Game.SpeechFont, 15, String.Format("speed: %d", GetGameSpeed()));
  
  if (oInsideFirstAidKit.Visible)
  {
    mouse.Mode = eModeEmpty;
    
    if (oInsideFirstAidKit.IsUnderMouse())
    {
      mouse.Mode = eModeInteract;
    }
  }
  
  if (oEngineSwitch.Visible)
  {
    mouse.Mode = eModeEmpty;
    
    if (oEngineSwitch.IsUnderMouse())
    {
      mouse.Mode = eModeInteract;
    }
  }
  
  handleCopilot();
  handlePilot();
}
