bool isLookingIntoWastebasket = false;
Door bathroomDoor;
Door cockpitDoor;

Character* activeCharacter;
Character* previousActiveCharacter;

int carlosAnimationCycle = 0;
int joannaAnimationCycle = 0;
int queenieAnimationCycle = 0;

function setActiveCharacter()
{
  
  if (!IsTimerExpired(1)) return;
  
  switch (previousActiveCharacter)
  {
    case cQueenie:
      activeCharacter = cJoanna;
      break;
    case cJoanna:
      activeCharacter = cCarlos;
      break;
    case cCarlos:
      activeCharacter = cQueenie;
      break;
    default:
      activeCharacter = cQueenie;
      break;
  }
  
  previousActiveCharacter = activeCharacter;
}

function removeActiveCharacter()
{
  activeCharacter = null;
}

/*
Carlos functions
*/

int moveCarlosToBackState = 0;
function moveCarlosToBack()
{
  switch (moveCarlosToBackState)
  {
    case 0:
    {
      cCarlos.LockView(CARLOS_IDLE_SIT);
      cCarlos.Animate(2, 9, eOnce, eNoBlock, eForwards);
      oGlass.Visible = true;
      
      moveCarlosToBackState++;
      
      break;
    }
    case 1:
    {
      if (!cCarlos.Animating)
      {
        cCarlos.UnlockView();
        cCarlos.SetIdleView(CARLOS_IDLE, 0);
        cCarlos.Walk(101, 132, eNoBlock, eWalkableAreas);
        moveCarlosToBackState++;
      }
      
      break;
    }
    case 2:
    {
      if (!cCarlos.Moving)
      {
        moveCarlosToBackState = 0;
        carlosAnimationCycle++;
        setActiveCharacter();
        removeActiveCharacter();
        SetTimer(3, 200);
        SetTimer(1, 500);
      }
      
      break;
    }
  }
}

int moveCarlosToTableState = 0;
function moveCarlosToTable()
{
  switch (moveCarlosToTableState)
  {
    case 0:
    {
      cCarlos.Walk(433, 134, eNoBlock, eWalkableAreas);
      moveCarlosToTableState++;
      
      break;
    }
    case 1:
    {
      if (!cCarlos.Moving)
      {
        cCarlos.LockView(CARLOS_IDLE_SIT);
        cCarlos.Animate(2, 9, eOnce, eNoBlock, eBackwards);
        oGlass.Visible = false;
        moveCarlosToTableState++;
      }
      
      break;
    }
    case 2:
    {
      if (!cCarlos.Animating)
      {
        cCarlos.UnlockView();
        cCarlos.SetIdleView(CARLOS_IDLE_SIT, 0);
        cCarlos.Loop = 0;
        carlosAnimationCycle = 0;
        moveCarlosToTableState = 0;
        setActiveCharacter();
        removeActiveCharacter();
        SetTimer(3, 200);
        SetTimer(1, 500);
      }
      
      break;
    }
  }
}

int idleCarlosAtBackState = 101;
function idleCarlosAtBack()
{                                  
  switch (idleCarlosAtBackState)
  {
    case 0:
    { 
      if (!cCarlos.Animating)
      {
        cCarlos.UnlockView();
        
        SetTimer(3, 200);
        
        idleCarlosAtBackState = 101;
      }
      
      break;
    }
    case 1:
    {
      cCarlos.LockView(CARLOS_SMOKING_STANDING);
      cCarlos.Animate(2, 9, eOnce, eNoBlock, eForwards);
      
      idleCarlosAtBackState = 0;
      break;
    }
    default:
    {
      if (IsTimerExpired(3))
      {
        if (cCarlos == activeCharacter)
        {
          carlosAnimationCycle = 3;
        }
        
        idleCarlosAtBackState -= 100;
      }
      
      break;
    }
  }
}

int idleCarlosAtTableState = 101;
function idleCarlosAtTable()
{                                  
  switch (idleCarlosAtTableState)
  {
    default:
    {
      if (IsTimerExpired(3))
      {
        if (cCarlos == activeCharacter)
        {
          carlosAnimationCycle = 1;
        }
        else
        {
          SetTimer(3, 100);
        }
      }
      
      break;
    }
  }
}

Overlay *carlosInfoOverlay;
function handleCarlos()
{
  if (carlosInfoOverlay != null) {
    carlosInfoOverlay.Remove();
  }
  carlosInfoOverlay = Overlay.CreateTextual(200, 50, 120, Game.SpeechFont, 15, String.Format("Carlos view: %d, loop: %d, active: %d", cCarlos.View, cCarlos.Loop, activeCharacter == cCarlos));
  
  switch (carlosAnimationCycle)
  {
    case 0:
    {
      idleCarlosAtTable();
      break;
    }
    case 1:
    {
      moveCarlosToBack();
      break;
    }
    case 2:
    {
      idleCarlosAtBack();
      break;
    }
    case 3:
    {
      moveCarlosToTable();
      break;
    }
  }
}

/*
Joanna functions
*/

int moveJoannaToTableState = 0;
function moveJoannaToTable()
{
  switch (moveJoannaToTableState)
  {
    case 0:
    {
      cJoanna.ChangeView(JOANNA_NORMAL);
      cJoanna.LockView(JOANNA_SIT);
      cJoanna.Animate(5, 9, eOnce, eNoBlock, eForwards);
      
      moveJoannaToTableState++;
      
      break;
    }
    case 1:
    {
      if (!cJoanna.Animating)
      {
        cJoanna.UnlockView();
        cJoanna.SetIdleView(JOANNA_IDLE, 5);
        cJoanna.Walk(380, 130, eNoBlock, eWalkableAreas);
        
        moveJoannaToTableState++;
      }
      
      break;
    }
    case 2:
    {
      if (!cJoanna.Moving)
      {
        cJoanna.LockView(JOANNA_SIT);
        cJoanna.Animate(5, 9, eOnce, eNoBlock, eBackwards);
        
        moveJoannaToTableState++;
      }
      
      break;
    }
    case 3:
    {
      if (!cJoanna.Animating)
      {
        cJoanna.UnlockView();
        cJoanna.SetIdleView(JOANNA_SIT, 0);
        cJoanna.Loop = 0;
        moveJoannaToTableState = 0;
        joannaAnimationCycle++;
        setActiveCharacter();
        removeActiveCharacter();
        SetTimer(1, 500);
        SetTimer(2, 200);
      }
      
      break;
    }
  }
}

int moveJoannaToChairState = 0;
function moveJoannaToChair()
{
  switch (moveJoannaToChairState)
  {
    case 0:
    {
      cJoanna.LockView(JOANNA_SIT);
      cJoanna.Animate(5, 9, eOnce, eNoBlock, eForwards);
      
      moveJoannaToChairState++;
      
      break;
    }
    case 1:
    {
      if (!cJoanna.Animating)
      {
        cJoanna.UnlockView();
        cJoanna.SetIdleView(JOANNA_NORMAL, 5);
        cJoanna.Walk(hChair2.WalkToX, hChair2.WalkToY, eNoBlock, eWalkableAreas);
        moveJoannaToChairState++;
      }
      
      break;
    }
    case 2:
    {
      if (!cJoanna.Moving)
      {
        cJoanna.LockView(JOANNA_SIT);
        cJoanna.Animate(5, 9, eOnce, eNoBlock, eBackwards);
        
        moveJoannaToChairState++;
      }
      
      break;
    }
    case 3:
    {
      if (!cJoanna.Animating)
      {
        cJoanna.UnlockView();
        cJoanna.SetIdleView(JOANNA_SIT, 0);
        cJoanna.Loop = 0;
        moveJoannaToChairState = 0;
        joannaAnimationCycle = 0;
        setActiveCharacter();
        removeActiveCharacter();
        SetTimer(1, 500);
        SetTimer(2, 200);
      }
      
      break;
    }
  }
}

int idleJoannaAtChairState = 101;
function idleJoannaAtChair()
{                                  
  switch (idleJoannaAtChairState)
  {
    case 0:
    { 
      if (!cJoanna.Animating)
      {
        cJoanna.UnlockView();
        
        SetTimer(2, 80);
        
        if (cJoanna.Loop == 2)
        {
          idleJoannaAtChairState = 101;
        }
        else
        {
          idleJoannaAtChairState = (cJoanna.Loop + 101);
        }
        
        cJoanna.Loop = 0;
      }
      
      break;
    }
    case 1:
    case 2:
    {
      cJoanna.LockView(JOANNA_SIT);
      cJoanna.Animate(idleJoannaAtChairState, 9, eOnce, eNoBlock, eForwards);
      
      idleJoannaAtChairState = 0;
      break;
    }
    default:
    {
      if (IsTimerExpired(2))
      {
        if (cJoanna == activeCharacter)
        {
          joannaAnimationCycle++;
        }
        
        idleJoannaAtChairState -= 100;
      }
      
      break;
    }
  }
}

int idleJoannaAtTableState = 101;
function idleJoannaAtTable()
{                                  
  switch (idleJoannaAtTableState)
  {
    case 0:
    { 
      if (!cJoanna.Animating)
      {
        cJoanna.UnlockView();
        
        SetTimer(2, 500);
        
        if (cJoanna.Loop == 2)
        {
          idleJoannaAtTableState = 101;
        }
        else
        {
          idleJoannaAtTableState = (cJoanna.Loop + 101);
        }
        
        cJoanna.Loop = 0;
      }
      
      break;
    }
    case 1:
    case 2:
    {
      cJoanna.LockView(JOANNA_SIT);
      cJoanna.Animate(idleJoannaAtTableState, 9, eOnce, eNoBlock, eForwards);
      
      idleJoannaAtTableState = 0;
      break;
    }
    default:
    {
      if (IsTimerExpired(2))
      {
        if (cJoanna == activeCharacter)
        {
          joannaAnimationCycle++;
        }
        
        idleJoannaAtTableState -= 100;
      }
      
      break;
    }
  }
}

Overlay *joannaInfoOverlay;
function handleJoanna()
{
  if (joannaInfoOverlay != null) {
    joannaInfoOverlay.Remove();
  }
  joannaInfoOverlay = Overlay.CreateTextual(50, 50, 120, Game.SpeechFont, 15, String.Format("Joanna view: %d, loop: %d, active: %d", cJoanna.View, cJoanna.Loop, activeCharacter == cJoanna));
  
  switch (joannaAnimationCycle)
  {
    case 0:
    {
      idleJoannaAtChair();
      break;
    }
    case 1:
    {
      moveJoannaToTable();
      break;
    }
    case 2:
    {
      idleJoannaAtTable();
      break;
    }
    case 3:
    {
      moveJoannaToChair();
      break;
    }
  }
}

/*
Queenie functions
*/

int idleQueenieAtChairState = 101;
function idleQueenieAtChair()
{                                  
  switch (idleQueenieAtChairState)
  {
    default:
    {
      if (IsTimerExpired(4))
      {
        if (cQueenie == activeCharacter)
        {
          queenieAnimationCycle = 0;
        }
        else
        {
          setActiveCharacter();
          removeActiveCharacter();
          SetTimer(1, 500);
          SetTimer(4, 200);
        }
      }
      
      break;
    }
  }
}

Overlay *queenieInfoOverlay;
function handleQueenie()
{
  if (queenieInfoOverlay != null) {
    queenieInfoOverlay.Remove();
  }
  queenieInfoOverlay = Overlay.CreateTextual(200, 150, 120, Game.SpeechFont, 15, String.Format("Queenie view: %d, loop: %d, active: %d", cQueenie.View, cQueenie.Loop, activeCharacter == cQueenie));
  
  switch (queenieAnimationCycle)
  {
    case 0:
    {
      idleQueenieAtChair();
      break;
    }
  }
}

bool test = true;
Overlay *gameInfoOverlay;
function repeatedly_execute()
{
  if (test) {
    test = false;
    SetGameSpeed(80);
  }
  if (gameInfoOverlay != null) {
    gameInfoOverlay.Remove();
  }
  gameInfoOverlay = Overlay.CreateTextual(50, 150, 120, Game.SpeechFont, 15, String.Format("speed: %d", GetGameSpeed()));
  
  
  if (isLookingIntoWastebasket)
  {
    mouse.Mode = eModeEmpty;
    
    if (oInsideWastebasket.IsUnderMouse())
    {
      mouse.Mode = eModeInteract;
    }
  }
  
  if (activeCharacter == null) {
    setActiveCharacter();
  }
  
  handleCarlos();
  handleJoanna();
  handleQueenie();
  //handleActiveCharacter();
  /*
  if (anyNPCIsActive())
  {
    Character* activeCharacter = getActiveNPC();
    activateCharacter(activeCharacter);
  }
  
  if ((allNPCsAreInactive()) && IsTimerExpired(1))
  {
    Character* characterToActivate = pickCharacterToActivate();
    activateCharacter(characterToActivate);
  }
  */
}

/*
int objectSpeed = -1;
int cycleCounter = 0;

function handle_clouds()
{
  cycleCounter++; // Increment the counter on each game cycle

  if (cycleCounter % 25 == 0) // Check if the counter is a multiple of 10
  {
    // Move object to the right if it hasn't reached the edge of the screen
    if (oCloudsA.X > -218)  // Replace 320 with the screen boundary you want
    {
        oCloudsA.X += objectSpeed; // Moves the object to the right
    }
    else
    {
        oCloudsA.X = 127; // Resets the object to the starting point if it reaches the edge
    }  

    if (oCloudsA.X > -218)  // Replace 320 with the screen boundary you want
    {
        oCloudsB.X += objectSpeed; // Moves the object to the right
    }
    else
    {
        oCloudsB.X = 471; // Resets the object to the starting point if it reaches the edge
    }  
  }
}
*/

function on_mouse_click(MouseButton button)
{
  if (isLookingIntoWastebasket)
  {    
    if (oInsideWastebasket.IsUnderMouse())
    {
      Room.ProcessClick(mouse.x, mouse.y, mouse.Mode);
    }
    
    ClaimEvent();
    return;
  }
  if (!IsGamePaused())
  {
    if ((button == eMouseRight) && (Mouse.IsOverWalkableArea() || Mouse.IsOverNothing()) && !(Mouse.IsOverHotspot()))
    {
      mouse.Mode = eModeLookat;
      Display("There is a wastebasket in the left corner of the room. Other hints.");
      choose_cursor();
      ClaimEvent();
    }
  }
}
function room_LeaveRight()
{
  cEgo.ChangeRoom(5);
}

function room_LeaveLeft()
{
  cEgo.ChangeRoom(6);
}

function loadJoanna()
{
  cJoanna.ChangeView(JOANNA_NORMAL);
  cJoanna.SetIdleView(JOANNA_SIT, 0);
  cJoanna.x = hChair2.WalkToX;
  cJoanna.y = hChair2.WalkToY;
  
  SetTimer(2, 500);
  
  cJoanna.SetIsSitting(false);
}

function loadCarlos()
{
  oGlass.Visible = false;
  cCarlos.ChangeView(CARLOS_NORMAL);
  cCarlos.SetIdleView(CARLOS_IDLE_SIT, 0);
  cCarlos.x = 433;
  cCarlos.y = 134;
  
  SetTimer(3, 100);
  
  cCarlos.SetIsSitting(false);
}

function loadQueenie()
{
  cQueenie.ChangeView(4);
  cQueenie.SetIdleView(40, 0);
  cQueenie.x = hChair7.WalkToX - 15;
  cQueenie.y = hChair7.WalkToY + 2;
  
  SetTimer(4, 200);
  
  cQueenie.SetIsSitting(true);
}

function loadFlightAttendant()
{
  cFlAttend.x = hCabinet.WalkToX;
  cFlAttend.y = hCabinet.WalkToY;
  cFlAttend.FaceDirection(eDirectionLeft);
}

function initDoors()
{
  oBathroomDoor.SetView(AIRPLANE_DOORS);
  oCockpitDoor.SetView(AIRPLANE_DOORS);
  bathroomDoor.Init(oBathroomDoor, 35, 0, -20, -2);
  cockpitDoor.Init(oCockpitDoor, -25, 0, 30, -2);
}

function initTimers()
{
  SetTimer(1, 900);
}

function room_Load()
{
  aAirplane_hum.Play(eAudioPriorityNormal, eRepeat);
  a6001_AirplaneMusic.Play(eAudioPriorityNormal, eRepeat);
  
  initDoors();
  initTimers();
  
  int previousRoom = player.PreviousRoom;
  
  switch(player.PreviousRoom)
  {
    case 5:
      cEgo.x = cockpitDoor.doorObject.X - 5;
      cEgo.y = cockpitDoor.doorObject.Y;
      cEgo.FaceDirection(eDirectionLeft);
      break;
    case 6:
      cEgo.x = bathroomDoor.doorObject.X + 25;
      cEgo.y = bathroomDoor.doorObject.Y;
      cEgo.FaceDirection(eDirectionRight);
      break;
    default:
      break;
  }
  
  loadJoanna();
  loadQueenie();
  loadCarlos();
  //loadFlightAttendant();
}

function hWasteBasket_Look(Hotspot *theHotspot, CursorMode mode)
{
  if (cEgo.HasInventory(iMagazine))
    Display("Tbere is nothing more to get here");
  else
    Display("You see something inside the wastebasket.");
}

function hWasteBasket_Interact(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Walk(29, 157, eBlock);
  cEgo.LockView(CLARA_MAIN_CABIN);
  cEgo.Animate(0, 5, eOnce, eBlock);
  
  if (cEgo.HasInventory(iMagazine))
    oInsideWastebasket.Graphic = AIRPLANE_TRASH_EMPTY;
  else
    oInsideWastebasket.Graphic = AIRPLANE_TRASH_FULL;
  
  oInsideWastebasket.Visible = true;
  isLookingIntoWastebasket = true;
}

function oInsideWastebasket_Interact(Object *theObject, CursorMode mode)
{
  if (cEgo.HasInventory(iMagazine))
  {
    Display("Tbere is nothing more to get here");

    cEgo.Animate(0, 5, eOnce, eBlock, eBackwards);
    cEgo.UnlockView();
    cEgo.FaceDirection(eDirectionLeft);
    oInsideWastebasket.Visible = false;
    isLookingIntoWastebasket = false;
    
    return;
  }

  Display("You pick it up and place it in your purse.");
  cEgo.Animate(1, 5, eOnce, eBlock);
  cEgo.AddInventory(iMagazine);
  oInsideWastebasket.Graphic = AIRPLANE_TRASH_EMPTY;
}

function oSeatPocket_Interact(Object *theObject, CursorMode mode)
{
  Display("There is nothing more to get here");
}

function sitDown(Character *chara, Hotspot *hChair, int sitView, int sitLoop)
{
  chara.SetProperty("chairNumber", hChair1.ID);
  chara.SetProperty("isSitting", true);
  int currCharacterX = chara.x;
  
  chara.LockView(sitView);
  
  chara.x = (chara.x - 12);
  chara.Animate(sitLoop, 5, eOnce, eBlock, eBackwards);
}

function standUp(Character *chara, int standView, int standLoop)
{
  chara.SetProperty("isSitting", false);
  int currCharacterX = chara.x;
  
  chara.Animate(standLoop, 5, eOnce, eBlock, eForwards);
  cEgo.UnlockView(eStopMoving);
  cEgo.FaceDirection(eDirectionRight);
  chara.x = (currCharacterX + 12);
}

function hChair_Interact(Hotspot *theHotspot, CursorMode mode)
{
  Display(theHotspot.ScriptName);
  if (cEgo.GetProperty("isSitting") == true)
  {
    standUp(cEgo, CLARA_SIT, 3);
    
    oSeatPocket.Visible = false;
  }
  else
  {
    sitDown(cEgo, theHotspot, CLARA_SIT, 3);
    
    oSeatPocket.Visible = true;
  }
}

function hChair_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("You see a chair.");
}

function oInsideWastebasket_Look(Object *theObject, CursorMode mode)
{
  Display("You see the inside of the waste basket.");
}

function oDoor_interact(Object *theDoor, int walkToOffsetX,  int walkToOffsetY,  int walkOffOffsetX,  int walkOffOffsetY)
{
  int relativeX = theDoor.X + walkToOffsetX;
  int relativeY = theDoor.Y + walkToOffsetY;
  
  cEgo.Walk(relativeX, relativeY, eBlock);
  
  theDoor.SetView(AIRPLANE_DOORS);
  theDoor.Animate(1, 5, eOnce, eBlock, eForwards);
  
  cEgo.Walk(relativeX + walkOffOffsetX, relativeY + walkOffOffsetY, eBlock, eAnywhere);
}

function oCockpitDoor_Interact(Object *cockPitDoor, CursorMode mode)
{
  oDoor_interact(cockPitDoor, -5, 0, 65, -3);
}

function oBathroomDoor_Interact(Object *bathDoor, CursorMode mode)
{ 
  oDoor_interact(bathDoor, 25, 0, -45, -3);
}