Door bathroomDoor;
Door cockpitDoor;

#define SET_ACTIVE_CHARACTER_TIMER 1
#define CARLOS_ANIMATION_CYCLE_DELAY_TIMER 2
#define JOANNA_ANIMATION_CYCLE_DELAY_TIMER 3
#define QUEENIE_ANIMATION_CYCLE_DELAY_TIMER 4

Character* activeCharacter;
Character* previousActiveCharacter;

int carlosAnimationCycle = 0;
int joannaAnimationCycle = 0;
int queenieAnimationCycle = 0;

function setActiveCharacter()
{
  if (!IsTimerExpired(SET_ACTIVE_CHARACTER_TIMER))
  {
    activeCharacter = null;
    return;
  }
  
  switch (previousActiveCharacter)
  {
    case cQueenie:
      activeCharacter = cQueenie;
      break;
    case cJoanna:
      activeCharacter = cJoanna;
      break;
    case cCarlos:
      activeCharacter = cQueenie;
      break;
    default:
      activeCharacter = cJoanna;
      break;
  }
  
  previousActiveCharacter = activeCharacter;
}

function oDoor_interact(Object *theDoor, int loop, int walkToOffsetX,  int walkToOffsetY,  int walkOffOffsetX,  int walkOffOffsetY, Character* chara)
{
  int relativeX = theDoor.X + walkToOffsetX;
  int relativeY = theDoor.Y + walkToOffsetY;
  
  chara.Walk(relativeX, relativeY, eBlock);
  
  theDoor.SetView(AIRPLANEDOORS_5003);
  theDoor.Animate(loop, 9, eOnce, eBlock, eForwards);
  
  chara.Walk(relativeX + walkOffOffsetX, relativeY + walkOffOffsetY, eBlock, eAnywhere);
}

/*
Carlos functions
*/
int moveCarlosToBackState = 0;
function moveCarlosToBack()
{
  switch (moveCarlosToBackState)
  {
    case 0:
    {
      cCarlos.LockViewOffset(CARLOSIDLESIT_1205, 1, 0);
      cCarlos.Animate(2, 9, eOnce, eNoBlock, eForwards);
      oGlass.Visible = true;
      
      moveCarlosToBackState++;
      
      break;
    }
    case 1:
    {
      if (!cCarlos.Animating)
      {
        cCarlos.UnlockView();
        cCarlos.FaceDirection(eDirectionLeft);
        cCarlos.Walk(105, 135, eNoBlock, eWalkableAreas);
        cCarlos.SetIdleView(-1, 0);
        moveCarlosToBackState++;
      }
      
      break;
    }
    case 2:
    {
      if (!cCarlos.Moving)
      {
        cCarlos.FaceDirection(eDirectionUp);
        moveCarlosToBackState = 0;
        carlosAnimationCycle++;
        setActiveCharacter();
        StartTimerBySeconds(CARLOS_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
        StartTimerBySeconds(SET_ACTIVE_CHARACTER_TIMER, 6.0);
      }
      
      break;
    }
  }
}

int moveCarlosToTableState = 0;
function moveCarlosToTable()
{
  switch (moveCarlosToTableState)
  {
    case 0:
    {
      cCarlos.Walk(433, 134, eNoBlock, eWalkableAreas);
      moveCarlosToTableState++;
      
      break;
    }
    case 1:
    {
      if (!cCarlos.Moving)
      {
        cCarlos.LockViewOffset(CARLOSIDLESIT_1205, 1, 0);
        cCarlos.Animate(2, 9, eOnce, eNoBlock, eBackwards);
        moveCarlosToTableState++;
      }
      
      break;
    }
    case 2:
    {
      if (!cCarlos.Animating)
      {
        oGlass.Visible = false;
        cCarlos.UnlockView();
        cCarlos.SetIdleView(CARLOSIDLESIT_1205, 0);
        cCarlos.Loop = 0;
        carlosAnimationCycle = 0;
        moveCarlosToTableState = 0;
        setActiveCharacter();
        StartTimerBySeconds(CARLOS_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
        StartTimerBySeconds(SET_ACTIVE_CHARACTER_TIMER, 6.0);
      }
      
      break;
    }
  }
}

int idleCarlosAtBackState = 101;
function idleCarlosAtBack()
{                                  

  switch (idleCarlosAtBackState)
  {
    default:
    {
      if (cCarlos.Frame != 0) break;
      
      if (IsTimerExpired(CARLOS_ANIMATION_CYCLE_DELAY_TIMER))
      {
        if (cCarlos == activeCharacter)
        {
          carlosAnimationCycle = 3;
        }
        else
        {
          StartTimerBySeconds(CARLOS_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
        }
      }
      
      break;
    }
  }
}

int idleCarlosAtTableState = 101;
function idleCarlosAtTable()
{                                  
  switch (idleCarlosAtTableState)
  {
    default:
    {
      if (cCarlos.Frame != 0) break;
      
      if (IsTimerExpired(CARLOS_ANIMATION_CYCLE_DELAY_TIMER))
      {
        if (cCarlos == activeCharacter)
        {
          carlosAnimationCycle = 1;
        }
        else
        {
          StartTimerBySeconds(CARLOS_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
        }
      }
      
      break;
    }
  }
}

function handleCarlos()
{
  switch (carlosAnimationCycle)
  {
    case 0:
    {
      idleCarlosAtTable();
      break;
    }
    case 1:
    {
      moveCarlosToBack();
      break;
    }
    case 2:
    {
      idleCarlosAtBack();
      break;
    }
    case 3:
    {
      moveCarlosToTable();
      break;
    }
  }
}

/*
Joanna functions
*/
int moveJoannaToTableState = 0;
function moveJoannaToTable()
{
  switch (moveJoannaToTableState)
  {
    case 0:
    {
      cJoanna.ChangeView(JOANNANORMAL_1101);
      cJoanna.SetIdleView(JOANNAIDLE_1102, 5);
      cJoanna.LockViewOffset(JOANNASIT_1103, 2, 0);
      cJoanna.Animate(5, 9, eOnce, eNoBlock, eForwards);
      
      moveJoannaToTableState++;
      
      break;
    }
    case 1:
    {
      if (cJoanna.Animating) break;
      
      cJoanna.UnlockView();
      cJoanna.x += 10;
      cJoanna.y += 5;
      cJoanna.Walk(cJoanna.x, cJoanna.y + 7, eNoBlock, eAnywhere);
      
      moveJoannaToTableState++;
      
      break;
    }
    case 2:
    {
      if (cJoanna.Moving) break;
      
      cJoanna.Walk(383, 133, eNoBlock, eWalkableAreas);
      
      moveJoannaToTableState++;
      
      break;
    }
    case 3:
    {
      if (cJoanna.Moving) break;
      
      cJoanna.LockViewOffset(JOANNASIT_1103, 2, 0);
      cJoanna.Animate(5, 9, eOnce, eNoBlock, eBackwards);
      
      moveJoannaToTableState++;
      
      break;
    }
    case 4:
    {
      if (cJoanna.Animating) break;

      cJoanna.UnlockView();
      cJoanna.SetIdleView(JOANNASIT_1103, 0);
      cJoanna.Loop = 0;
      joannaAnimationCycle++;
      setActiveCharacter();
      StartTimerBySeconds(SET_ACTIVE_CHARACTER_TIMER, 6.0);
      StartTimerBySeconds(JOANNA_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
      
      moveJoannaToTableState = 0;
      break;
    }
  }
}

int moveJoannaToChairState = 0;
function moveJoannaToChair()
{
  switch (moveJoannaToChairState)
  {
    case 0:
    {
      cJoanna.LockViewOffset(JOANNASIT_1103, 2, 0);
      cJoanna.Animate(5, 9, eOnce, eNoBlock, eForwards);
      
      moveJoannaToChairState++;
      break;
    }
    case 1:
    {
      if (cJoanna.Animating) break;
      
      cJoanna.UnlockView();
      cJoanna.SetIdleView(JOANNANORMAL_1101, 5);
      cJoanna.Walk(234, 146, eNoBlock, eWalkableAreas);
      
      moveJoannaToChairState++;
      break;
    }
    case 2:
    {
      if (cJoanna.Moving) break;
      
      cJoanna.Walk(cJoanna.x, cJoanna.y - 12, eNoBlock, eAnywhere);
      
      moveJoannaToChairState++;
      break;
    }
    case 3:
    {
      if (cJoanna.Moving) break;
      
      cJoanna.x -= 10;
      cJoanna.LockViewOffset(JOANNASIT_1103, 2, 0);
      cJoanna.Animate(5, 9, eOnce, eNoBlock, eBackwards);
      
      moveJoannaToChairState++;
      break;
    }
    case 4:
    {
      if (cJoanna.Animating) break;
      
      cJoanna.UnlockView();
      cJoanna.SetIdleView(JOANNASIT_1103, 0);
      cJoanna.Loop = 0;
      joannaAnimationCycle = 0;
      setActiveCharacter();
      StartTimerBySeconds(SET_ACTIVE_CHARACTER_TIMER, 6.0);
      StartTimerBySeconds(JOANNA_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
      
      moveJoannaToChairState = 0;
      break;
    }
  }
}

int idleJoannaAtChairState = 101;
function idleJoannaAtChair()
{                                  
  switch (idleJoannaAtChairState)
  {
    case 0:
    { 
      if (!cJoanna.Animating)
      {
        cJoanna.UnlockView();
        
        if (cJoanna.Loop == 2)
        {
          idleJoannaAtChairState = 101;
        }
        else
        {
          idleJoannaAtChairState = (cJoanna.Loop + 101);
        }
        
        StartTimerBySeconds(JOANNA_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
        cJoanna.Loop = 0;
      }
      
      break;
    }
    case 1:
    {
      if (cJoanna.Frame != 0) break;
      
      cJoanna.LockView(JOANNASIT_1103);
      cJoanna.Animate(1, 9, eOnce, eNoBlock, eForwards);
      
      idleJoannaAtChairState = 0;
      break;
    }
    case 2:
    {
      if (cJoanna.Frame != 0) break;
      
      cJoanna.LockViewOffset(JOANNASIT_1103, 0, 0);
      cJoanna.Animate(2, 9, eOnce, eNoBlock, eForwards);
      
      idleJoannaAtChairState = 0;
      break;
    }
    default:
    {
      if (IsTimerExpired(JOANNA_ANIMATION_CYCLE_DELAY_TIMER))
      {
        if (cJoanna == activeCharacter)
        {
          joannaAnimationCycle++;
          idleJoannaAtChairState = 101;
          break;
        }
        
        idleJoannaAtChairState -= 100;
      }
      
      break;
    }
  }
}

int idleJoannaAtTableState = 101;
function idleJoannaAtTable()
{                                  
  switch (idleJoannaAtTableState)
  {
    case 0:
    { 
      if (!cJoanna.Animating)
      {
        cJoanna.UnlockView();
        
        if (cJoanna.Loop == 2)
        {
          idleJoannaAtTableState = 101;
        }
        else
        {
          idleJoannaAtTableState = (cJoanna.Loop + 101);
        }
        
        StartTimerBySeconds(JOANNA_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
        cJoanna.Loop = 0;
      }
      
      break;
    }
    case 1:
    {
      if (cJoanna.Frame != 0) break;
      
      cJoanna.LockView(JOANNASIT_1103);
      cJoanna.Animate(1, 9, eOnce, eNoBlock, eForwards);
      
      idleJoannaAtTableState = 0;
      break;
    }
    case 2:
    {
      if (cJoanna.Frame != 0) break;
      
      cJoanna.LockViewOffset(JOANNASIT_1103, -1, 0);
      cJoanna.Animate(2, 9, eOnce, eNoBlock, eForwards);
      
      idleJoannaAtTableState = 0;
      break;
    }
    default:
    {
      if (IsTimerExpired(JOANNA_ANIMATION_CYCLE_DELAY_TIMER))
      {
        if (cJoanna == activeCharacter)
        {
          joannaAnimationCycle++;
          idleJoannaAtTableState = 101;
          break;
        }
        
        idleJoannaAtTableState -= 100;
      }
      
      break;
    }
  }
}

function handleJoanna()
{
  switch (joannaAnimationCycle)
  {
    case 0:
    {
      idleJoannaAtChair();
      break;
    }
    case 1:
    {
      moveJoannaToTable();
      break;
    }
    case 2:
    {
      idleJoannaAtTable();
      break;
    }
    case 3:
    {
      moveJoannaToChair();
      break;
    }
  }
}

/*
Queenie functions
*/
int moveQueenieToBathroomState = 0;
function moveQueenieToBathroom()
{
  switch (moveQueenieToBathroomState)
  {
    case 0:
    {
      if (cQueenie.Frame != 0 && oQueenieSmoke.Frame != 13) break;
      
      oQueenieSmoke.Animate(0, 9, eOnce, eNoBlock, eForwards);
      
      moveQueenieToBathroomState++;
      break;
    }
    case 1:
    {
      if (oQueenieSmoke.Animating) break;
      
      oQueenieSmoke.Visible = false;
      cQueenie.LockViewAligned(QUEENIEIDLESIT_1153, 2, eAlignRight);
      cQueenie.Animate(2, 9, eOnce, eNoBlock, eForwards);
      
      moveQueenieToBathroomState++;
      break;
    }
    case 2:
    {
      if (cQueenie.Animating) break;
      
      cQueenie.x += 10;
      cQueenie.UnlockView();
      cQueenie.SetIdleView(QUEENIENORMAL_1151, 0);
      cQueenie.Walk(cQueenie.x + 7, cQueenie.y, eNoBlock, eAnywhere);
     
      moveQueenieToBathroomState++;
      break;
    }
    case 3:
    {
      if (cQueenie.Moving) break;
      
      cQueenie.Walk(79, 143, eNoBlock, eWalkableAreas);
      
      moveQueenieToBathroomState++;
      break;
    }
    case 4:
    {
      if (cQueenie.Moving) break;
      
      cQueenie.FaceDirection(eDirectionLeft);
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eNoBlock, eForwards);
      
      moveQueenieToBathroomState++;
      break;
    }
    case 5:
    {
      if (bathroomDoor.doorObject.Animating) break;
      
      cQueenie.Walk(cQueenie.x - 65, cQueenie.y - 3, eNoBlock, eAnywhere);
      
      moveQueenieToBathroomState++;
      break;
      
    }
    case 6:
    {
      if (cQueenie.Moving) break;
      
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eBlock, eBackwards);
      
      moveQueenieToBathroomState++;
      break;
    }
    case 7:
    {
      if (bathroomDoor.doorObject.Animating) break;
      
      cQueenie.UnlockView();
      cQueenie.SetIdleView(QUEENIEIDLESIT_1153, 0);
      cQueenie.ChangeRoom(6, 85, 138);
      //cQueenie.Loop = 0;
      moveQueenieToBathroomState = 0;
      queenieAnimationCycle++;
      //setActiveCharacter();
      //StartTimerBySeconds(SET_ACTIVE_CHARACTER_TIMER, 6.0);
      StartTimerBySeconds(QUEENIE_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
      
      break;
    }
  }
}

int moveQueenieToChairState = 0;
function moveQueenieToChair()
{
  switch (moveQueenieToChairState)
  {
    case 0:
    {
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eNoBlock, eForwards);
      
      moveQueenieToChairState++;
      break;
    }
    case 1:
    {
      if (bathroomDoor.doorObject.Animating) break;
      
      cQueenie.Walk(cQueenie.x + 60, cQueenie.y, eNoBlock, eAnywhere);
      
      moveQueenieToChairState++;
      break;
    }
    case 2:
    {
      if (cQueenie.Moving) break;
      
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eNoBlock, eBackwards);
      
      moveQueenieToChairState++;
      break;
    }
    case 3:
    {
      if (bathroomDoor.doorObject.Animating) break;
      //356 147
      
      //cQueenie.Walk(hChair7.WalkToX - 7, hChair7.WalkToY - 8, eNoBlock, eWalkableAreas);
      cQueenie.Walk(hChair7.WalkToX, hChair7.WalkToY, eNoBlock, eWalkableAreas);
      
      moveQueenieToChairState++;
      break;
    }
    case 4:
    {
      if (cQueenie.Moving) break;
      
      //cQueenie.Walk(cQueenie.x, hChair7.WalkToY + 2, eNoBlock, eAnywhere);
      cQueenie.Walk(cQueenie.x - 7, hChair7.WalkToY, eNoBlock, eAnywhere);
      
      moveQueenieToChairState++;
      break;
    }
    case 5:
    {
      if (cQueenie.Moving) break;
      
      cQueenie.SetIdleView(-1, 0);
      cQueenie.x -= 10;
      //cQueenie.LockViewOffset(QUEENIEIDLESIT_1153, -10, -2);
      cQueenie.LockView(QUEENIEIDLESIT_1153);
      cQueenie.Animate(2, 9, eOnce, eNoBlock, eBackwards);
      
      moveQueenieToChairState++;
      break;
    }
    case 6:
    {
      if (cQueenie.Animating) break;

      Display("hello");
      cQueenie.UnlockView();
      cQueenie.SetIdleView(QUEENIEIDLESIT_1153, 0);
      cQueenie.Loop = 3;
      moveQueenieToChairState = 0;
      queenieAnimationCycle = 0;
      setActiveCharacter();
      oQueenieSmoke.Visible = true;
      StartTimerBySeconds(SET_ACTIVE_CHARACTER_TIMER, 6.0);
      StartTimerBySeconds(QUEENIE_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
      
      break;
    }
  }
}

int idleQueenieAtChairState = 101;
function idleQueenieAtChair()
{                                  
  switch (idleQueenieAtChairState)
  {
    case 0:
    {
      if (!cQueenie.Animating)
      {
        cQueenie.UnlockView();
        oQueenieSmoke.Visible = true;
        
        idleQueenieAtChairState = 101;
        
        StartTimerBySeconds(QUEENIE_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
        cQueenie.Loop = 3;
      }
      
      break;
    }
    case 1:
    {
      if (cQueenie.Frame != 0 && oQueenieSmoke.Frame != 0) break;
      
      cQueenie.LockView(QUEENIEIDLESIT_1153);
      oQueenieSmoke.Visible = false;
      cQueenie.Animate(4, 9, eOnce, eNoBlock, eForwards);
      
      idleQueenieAtChairState = 0;
      break;
    }
    /*
    default:
    {
      if (IsTimerExpired(QUEENIE_ANIMATION_CYCLE_DELAY_TIMER))
      {
        if (cQueenie == activeCharacter)
        {
          //queenieAnimationCycle++; uncomment when need to move queenie
          idleQueenieAtChairState = 101;
          setActiveCharacter();
          StartTimerBySeconds(SET_ACTIVE_CHARACTER_TIMER, 6.0);
          StartTimerBySeconds(QUEENIE_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
          break;
        }
        
        StartTimerBySeconds(QUEENIE_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
        idleQueenieAtChairState -= 100;
      }
      
      break;
    }
    */
    default:
    {
      if (IsTimerExpired(QUEENIE_ANIMATION_CYCLE_DELAY_TIMER))
      {
        if (cQueenie == activeCharacter)
        {
          queenieAnimationCycle++;
          idleQueenieAtChairState = 101;
          break;
        }
        
        idleQueenieAtChairState -= 100;
      }
      
      break;
    }
  }
}

function idleQueenieAtBathroom()
{
  if (IsTimerExpired(QUEENIE_ANIMATION_CYCLE_DELAY_TIMER))
  {
    cQueenie.ChangeRoom(player.Room, 30, 142);
    cQueenie.SetIdleView(QUEENIENORMAL_1151, 5);
    queenieAnimationCycle++;
    StartTimerBySeconds(QUEENIE_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
  }
}

function handleQueenie()
{
  switch (queenieAnimationCycle)
  {
    case 0:
    {
      idleQueenieAtChair();
      break;
    }
    case 1:
    {
      moveQueenieToBathroom();
      break;
    }
    case 2:
    {
      idleQueenieAtBathroom();
      break;
    }
    case 3:
    {
      moveQueenieToChair();
      break;
    }
  }
}

int objectSpeed = -1;
int cycleCounter = 0;

function handleClouds()
{
  cycleCounter++; // Increment the counter on each game cycle

  if (cycleCounter % 25 == 0) // Check if the counter is a multiple of 10
  {
    // Move object to the right if it hasn't reached the edge of the screen
    if (oCloudsA.X > -332)  // Replace 320 with the screen boundary you want
    {
        oCloudsA.X += objectSpeed; // Moves the object to the right
    }
    else
    {
        oCloudsA.X = 89; // Resets the object to the starting point if it reaches the edge
    }  
    
    if (oCloudsA.X > -332)  // Replace 320 with the screen boundary you want
    {
      oCloudsB.X += objectSpeed; // Moves the object to the right
    }
    else
    {
      oCloudsB.X = 509; // Resets the object to the starting point if it reaches the edge
    }  
  }
}

function on_mouse_click(MouseButton button)
{
  if (button == eMouseLeft && Mouse.Mode == eModeExit)
  {
    if (oInsideCabinet.Visible)
    {
      cEgo.Animate(2, 9, eOnce, eBlock, eBackwards);
      cEgo.UnlockView();
      cEgo.FaceDirection(eDirectionRight);
      oInsideCabinet.Visible = false;
      
      return;
    }
    
    if (oInsideWastebasket.Visible)
    {
      cEgo.Animate(0, 9, eOnce, eBlock, eBackwards);
      cEgo.UnlockView();
      cEgo.FaceDirection(eDirectionLeft);
      oInsideWastebasket.Visible = false;
      
      return;
    }
  }

  if (!IsGamePaused())
  {
    if (button == eMouseRight && invCursorToggle)
    {
      return;
    }
    
    if ((button == eMouseRight) && (Mouse.IsOverWalkableArea() || Mouse.IsOverNothing()) && !(Mouse.IsOverHotspot()))
    {
      mouse.Mode = eModeLookat;
      Display("There is a wastebasket in the left corner of the room. Other hints.");
      choose_cursor();
      ClaimEvent();
    }
  }
}
function room_LeaveRight()
{
  cEgo.ChangeRoom(5);
}

function room_LeaveLeft()
{
  cEgo.ChangeRoom(6);
}

function loadClara()
{
  switch(player.PreviousRoom)
  {
    case 5:
      cEgo.x = cockpitDoor.doorObject.X + 60;
      cEgo.y = cockpitDoor.doorObject.Y - 2;
      break;
    case 6:
      cEgo.x = bathroomDoor.doorObject.X - 20;
      cEgo.y = bathroomDoor.doorObject.Y - 2;
      break;
    default:
      break;
  }
}

function loadJoanna()
{
  cJoanna.ChangeView(JOANNANORMAL_1101);
  cJoanna.SetIdleView(JOANNASIT_1103, 0);
  cJoanna.Loop = 0;
  cJoanna.ChangeRoom(player.Room, 224, 132);
  
  joannaAnimationCycle = 0;
  
  idleJoannaAtChairState = 101;
  moveJoannaToChairState = 0;
  
  idleJoannaAtTableState = 101;
  moveJoannaToTableState = 0;
  
  StartTimerBySeconds(JOANNA_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
}

function loadCarlos()
{
  oGlass.Visible = false;
  oGlass.Baseline = 128;
  oGlass.X = 417;
  oGlass.Y = 110;
  
  cCarlos.ChangeView(CARLOSNORMAL_1201);
  cCarlos.SetIdleView(CARLOSIDLESIT_1205, 0);
  cCarlos.Loop = 0;
  cCarlos.ChangeRoom(player.Room, 433, 134);
  
  carlosAnimationCycle = 0;
  
  idleCarlosAtTableState = 101;
  moveCarlosToTableState = 0;
  
  idleCarlosAtBackState = 101;
  moveCarlosToBackState = 0;
  
  StartTimerBySeconds(CARLOS_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
}

function loadQueenie()
{
  cQueenie.ChangeView(QUEENIENORMAL_1151);
  cQueenie.SetIdleView(QUEENIEIDLESIT_1153, 0);
  cQueenie.Loop = 3;
  cQueenie.ChangeRoom(player.Room, hChair7.WalkToX - 17, hChair7.WalkToY);
  
  oQueenieSmoke.X = cQueenie.x - 17;
  oQueenieSmoke.Y = cQueenie.y;
  oQueenieSmoke.Visible = true;
  oQueenieSmoke.SetView(QUEENIEIDLESIT_1153, 0, 0);
  oQueenieSmoke.Animate(0, 9, eRepeat, eNoBlock, eForwards);
  
  StartTimerBySeconds(QUEENIE_ANIMATION_CYCLE_DELAY_TIMER, 2.0);
}

function loadFlightAttendant()
{
  cFlAttend.x = 490;
  cFlAttend.y = 155;
  cFlAttend.FaceDirection(eDirectionLeft);
}

function initDoors()
{
  oBathroomDoor.SetView(AIRPLANEDOORS_5003);
  oCockpitDoor.SetView(AIRPLANEDOORS_5003);
  oCockpitDoor.Graphic = 1069;
  bathroomDoor.Init(oBathroomDoor, 35, 0, -20, -2);
  cockpitDoor.Init(oCockpitDoor, -25, 0, 30, -2);
}

function initTimers()
{
  StartTimerBySeconds(SET_ACTIVE_CHARACTER_TIMER, 6.0);
  activeCharacter = null;
}

function room_Load()
{
  aAirplane_hum.Play(eAudioPriorityNormal, eRepeat);
  a6001_AirplaneMusic.Play(eAudioPriorityNormal, eRepeat);
  
  initDoors();
  //initTimers();
  
  loadClara();
  loadJoanna();
  loadQueenie();
  loadCarlos();
  loadFlightAttendant();
}

function room_AfterFadeIn()
{
  initTimers();
  switch(player.PreviousRoom)
  {
    case 5:
      cockpitDoor.doorObject.SetView(AIRPLANEDOORS_5003);
      cockpitDoor.doorObject.Animate(1, 9, eOnce, eBlock, eForwards);
      cEgo.Walk(cEgo.x - 80, cEgo.y + 5, eBlock, eAnywhere);
      cockpitDoor.doorObject.Animate(1, 9, eOnce, eBlock, eBackwards);
      break;
    case 6:
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eBlock, eForwards);
      cEgo.Walk(cEgo.x + 65, cEgo.y + 5, eBlock, eAnywhere);
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eBlock, eBackwards);
      break;
    default:
      break;
  }
}

function hWasteBasket_Look(Hotspot *theHotspot, CursorMode mode)
{
  if (cEgo.HasInventory(iMagazine))
    Display("Tbere is nothing more to get here");
  else
    Display("You see something inside the wastebasket.");
}

function hWasteBasket_Interact(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.FaceDirection(eDirectionLeft);
  cEgo.LockViewAligned(CLARAMAINCABIN_1009, 0, eAlignRight);
  cEgo.Animate(0, 9, eOnce, eBlock);
  
  if (cEgo.HasInventory(iMagazine))
    oInsideWastebasket.Graphic = AIRPLANE_TRASH_EMPTY;
  else
    oInsideWastebasket.Graphic = AIRPLANE_TRASH_FULL;
  
  oInsideWastebasket.Baseline = 200;
  oInsideWastebasket.Visible = true;
}

function oInsideWastebasket_Interact(Object *theObject, CursorMode mode)
{
  if (cEgo.HasInventory(iMagazine))
  {
    Display("Tbere is nothing more to get here");

    cEgo.Animate(0, 9, eOnce, eBlock, eBackwards);
    cEgo.UnlockView();
    cEgo.FaceDirection(eDirectionLeft);
    oInsideWastebasket.Visible = false;
    
    return;
  }

  cEgo.Animate(1, 9, eOnce, eBlock);
  Display("You pick it up and place it in your purse.");
  cEgo.AddInventory(iMagazine);
  oInsideWastebasket.Graphic = AIRPLANE_TRASH_EMPTY;
  cEgo.Animate(1, 9, eOnce, eBlock, eBackwards);
}

function oSeatPocket_Interact(Object *theObject, CursorMode mode)
{
  Display("There is nothing more to get here.");

  cEgo.Animate(3, 9, eOnce, eBlock, eForwards);
  oSeatPocket.Visible = false;
  cEgo.UnlockView();
  cEgo.FaceDirection(eDirectionRight);
}

function sitDown(Character *chara, Hotspot *hChair, int sitView, int sitLoop)
{
  chara.SetProperty("chairNumber", hChair1.ID);
  chara.SetProperty("isSitting", true);
  int currCharacterX = chara.x;
  
  chara.LockView(sitView);
  
  chara.x = (chara.x - 12);
  chara.Animate(sitLoop, 5, eOnce, eBlock, eBackwards);
}

function standUp(Character *chara, int standView, int standLoop)
{
  chara.SetProperty("isSitting", false);
  int currCharacterX = chara.x;
  
  chara.Animate(standLoop, 5, eOnce, eBlock, eForwards);
  cEgo.UnlockView(eStopMoving);
  cEgo.FaceDirection(eDirectionRight);
  chara.x = (currCharacterX + 12);
}

function hChair_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("You see a chair.");
}

function oInsideWastebasket_Look(Object *theObject, CursorMode mode)
{
  Display("You see the inside of the waste basket.");
}

function oCockpitDoor_Interact(Object *cockPitDoor, CursorMode mode)
{
  oDoor_interact(cockPitDoor, 1, -5, 0, 100, -3, cEgo);
}

function oBathroomDoor_Interact(Object *bathDoor, CursorMode mode)
{ 
  oDoor_interact(bathDoor, 0, 25, 0, -85, -3, cEgo);
}

function handlePopovers()
{
  if (oInsideWastebasket.Visible)
  {
    mouse.Mode = eModeExit;
    
    if (oInsideWastebasket.IsUnderMouse())
      mouse.Mode = eModeInteract;
  }
  
  if (oSeatPocket.Visible)
  {
    mouse.Mode = eModeExit;
    
    if (oSeatPocket.IsUnderMouse())
      mouse.Mode = eModeInteract;
  }
  
  if (oInsideCabinet.Visible)
  {
    mouse.Mode = eModeExit;
    
    if (oInsideCabinet.IsUnderMouse())
      mouse.Mode = eModeInteract;
  }
}

Overlay *gameInfoOverlay;
function repeatedly_execute()
{
  if (gameInfoOverlay != null)
    gameInfoOverlay.Remove();
  /*
  gameInfoOverlay = Overlay.CreateTextual(50, 150, 120, Game.SpeechFont, 15, String.Format("speed: %d, \nactive timer: %.2f, queenie timer: %.2f, carlos timer: %.2f, joaana timer: %.2f",
    GetGameSpeed(), GetTimeRemainingInSeconds(SET_ACTIVE_CHARACTER_TIMER), GetTimeRemainingInSeconds(QUEENIE_ANIMATION_CYCLE_DELAY_TIMER), GetTimeRemainingInSeconds(CARLOS_ANIMATION_CYCLE_DELAY_TIMER), GetTimeRemainingInSeconds(JOANNA_ANIMATION_CYCLE_DELAY_TIMER)));
  */
  
  if (activeCharacter == null)
  {
    setActiveCharacter();
  }
  
  handlePopovers();
  
  handleCarlos();
  handleJoanna();
  handleQueenie();
  
  handleClouds();
}

function sitClaraInChair()
{
  cEgo.LockViewAligned(CLARASIT_1003, 3, eAlignRight);
  cEgo.Animate(3, 9, eOnce, eBlock, eBackwards);
  oSeatPocket.X = cEgo.x + 30;
  oSeatPocket.Y = cEgo.y - 20;
  oSeatPocket.Baseline = 200;
  oSeatPocket.Visible = true;
}

function hChair1_Interact(Hotspot *theHotspot, CursorMode mode)
{
  sitClaraInChair();
}

function hChair2_Interact(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Walk(hChair2.WalkToX, hChair2.WalkToY, eBlock, eWalkableAreas);
  sitClaraInChair();
}

function hChair3_Interact(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Walk(hChair3.WalkToX, hChair3.WalkToY, eBlock, eWalkableAreas);
  sitClaraInChair();
}

function hChair4_Interact(Hotspot *theHotspot, CursorMode mode)
{
  Display("You cannot sit in Joanna's chair");
}

function hChair5_Interact(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Walk(hChair5.WalkToX, hChair5.WalkToY, eBlock, eWalkableAreas);
  sitClaraInChair();
}

function hChair6_Interact(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Walk(hChair6.WalkToX, hChair6.WalkToY, eBlock, eWalkableAreas);
  sitClaraInChair();
}

function hChair7_Interact(Hotspot *theHotspot, CursorMode mode)
{
  Display("You cannot sit in Queenie's chair");
}

function hChair8_Interact(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Walk(hChair8.WalkToX, hChair8.WalkToY, eBlock, eWalkableAreas);
  sitClaraInChair();
}

function hCabinet_Interact(Hotspot *theHotspot, CursorMode mode)
{
  if (cFlAttend.Room == player.Room)
  {
    Display("Flight attendant is in the way");
    return;
  }
  cEgo.Walk(490, 155, eBlock, eWalkableAreas);
  cEgo.FaceDirection(eDirectionRight);
  cEgo.LockViewAligned(CLARAMAINCABIN_1009, 2, eAlignLeft);
  cEgo.Animate(2, 9, eOnce, eBlock);
  
  if (cEgo.HasInventory(iCigar))
    oInsideCabinet.Graphic = AIRPLANE_CABINET_EMPTY;
  else
    oInsideCabinet.Graphic = AIRPLANE_CABINET_FULL;
  
  oInsideCabinet.Baseline = 200;
  oInsideCabinet.X = 304;
  oInsideCabinet.Y = 138;
  oInsideCabinet.Visible = true;
}

function hCabinet_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("This is a cabinet.");
}

function oInsideCabinet_Interact(Object *theObject, CursorMode mode)
{
  if (cEgo.HasInventory(iCigar))
  {
    Display("Tbere is nothing more to get here");

    cEgo.Animate(2, 9, eOnce, eBlock, eBackwards);
    cEgo.UnlockView();
    cEgo.FaceDirection(eDirectionRight);
    oInsideCabinet.Visible = false;
    
    return;
  }

  Display("You pick it up and place it in your purse.");
  cEgo.AddInventory(iCigar);
  oInsideCabinet.Graphic = AIRPLANE_CABINET_EMPTY;
}

function oInsideCabinet_Look(Object *theObject, CursorMode mode)
{
  Display("Hmmmm.... fresh.");
}
