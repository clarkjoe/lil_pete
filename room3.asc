Door bathroomDoor;
Door cockpitDoor;

#define MIN_CARLOS_IDLE_ANIMATIONS 4
#define MAX_CARLOS_IDLE_ANIMATIONS 6
#define MIN_CARLOS_IDLE_DELAY_SEC 20
#define MAX_CARLOS_IDLE_DELAY_SEC 30

#define MIN_JOANNA_IDLE_ANIMATIONS 6
#define MAX_JOANNA_IDLE_ANIMATIONS 10
#define MIN_JOANNA_IDLE_DELAY_SEC 0
#define MAX_JOANNA_IDLE_DELAY_SEC 0

#define MIN_QUEENIE_IDLE_ANIMATIONS 4
#define MAX_QUEENIE_IDLE_ANIMATIONS 7
#define MIN_QUEENIE_IDLE_DELAY_SEC 5
#define MAX_QUEENIE_IDLE_DELAY_SEC 7
#define MIN_QUEENIE_SECS_IN_BATHROOM 15
#define MAX_QUEENIE_SECS_IN_BATHROOM 40

Character* activeCharacter;
Character* previousActiveCharacter;

int carlosAnimationCycle;
int carlosIdleAnimationsRemaining;
int moveCarlosToTableState;
int moveCarlosToBackState;
int idleCarlosAtBackState;
int idleCarlosAtTableState;

int joannaAnimationCycle;
int joannaIdleAnimationsRemaining;
int moveJoannaToTableState;
int moveJoannaToChairState;
int idleJoannaAtChairState;
int idleJoannaAtTableState;

int queenieAnimationCycle;
int queenieLoopsRemainingInBathroom;
int queenieIdleAnimationsRemaining;
int idleQueenieAtChairState;
int moveQueenieToBathroomState;

function sitClaraInChair(Object *theChair)
{
  if  (theChair.Y >= 140)
    cEgo.Walk(theChair.X+37, theChair.Y, eBlock, eWalkableAreas);
  else
    cEgo.Walk(theChair.X+37, theChair.Y-1, eBlock, eWalkableAreas);
    
  cEgo.LockViewAligned(CLARASIT_1003, 3, eAlignRight);
  cEgo.Animate(3, 9, eOnce, eBlock, eBackwards);
  oSeatPocket.X = cEgo.x + 30;
  oSeatPocket.Y = cEgo.y - 20;
  oMagazine.X = cEgo.x + 63;
  oMagazine.Y = cEgo.y - 83;
  oPukeBag.X = cEgo.x + 87;
  oPukeBag.Y = cEgo.y - 84;
  oSeatPocket.Baseline = 199;
  oSeatPocket.Visible = true;
  oMagazine.Visible = true;
  oPukeBag.Visible = true;
}

function oDoor_interact(Object *theDoor, int loop, int walkToOffsetX,  int walkToOffsetY,  int walkOffOffsetX,  int walkOffOffsetY, Character* chara)
{
  int relativeX = theDoor.X + walkToOffsetX;
  int relativeY = theDoor.Y + walkToOffsetY;
  
  chara.Walk(relativeX, relativeY, eBlock);
  
  if (theDoor == oBathroomDoor && cSteward.Room != player.Room && cQueenie.Room == player.Room)
  {
    Display("The steward is distracted. Take your chance with the minibar!");
    return;
  }
  
  theDoor.SetView(AIRPLANEDOORS_5003);
  theDoor.Animate(loop, 9, eOnce, eBlock, eForwards);
  
  chara.Walk(relativeX + walkOffOffsetX, relativeY + walkOffOffsetY, eBlock, eAnywhere);
}

/*
Carlos functions
*/
function moveCarlosToBack()
{
  switch (moveCarlosToBackState)
  {
    case 0:
    {
      cCarlos.Clickable = false;
      cCarlos.LockViewOffset(CARLOSIDLESIT_1205, 1, 0);
      cCarlos.Animate(1, 9, eOnce, eNoBlock, eForwards);
      moveCarlosToBackState++;
      break;
    }
    case 1:
    {
      cCarlos.Animate(2, 9, eOnce, eNoBlock, eForwards);
      oCarlosGlass.Visible = true;
      moveCarlosToBackState++;
      break;
    }
    case 2:
    {
      if (cCarlos.Animating)
        break;
      
      cCarlos.ChangeView(CARLOSNORMAL_1201);
      cCarlos.SetIdleView(-1, 0);
      cCarlos.Clickable = true;
      cCarlos.UnlockView();
      cCarlos.FaceDirection(eDirectionLeft);
      cCarlos.MyWalk(105, 135, eNoBlock, eWalkableAreas);
      
      moveCarlosToBackState++;
      break;
    }
    case 3:
    {
      if (cCarlos.Moving)
        break;
      
      if (!cCarlos.HasReachedDestination())
      {
        Point* destination = cCarlos.GetDestination();
        cCarlos.MyWalk(destination.x, destination.y, eNoBlock, eWalkableAreas);
        break;
      }
        
      cCarlos.ChangeView(CARLOSNORMALSTANDING);
      moveCarlosToBackState++;
      break;
    }
    case 4:
    {
      if (cCarlos.HasInventory(iCigar))
      {
        cCarlos.SetIdleView(CARLOSSMOKINGSTANDING_1203, RandomBetween(MIN_CARLOS_IDLE_DELAY_SEC, MAX_CARLOS_IDLE_DELAY_SEC));
        cCarlos.Loop = 2;
      }
      else
        cCarlos.FaceDirection(eDirectionUp);
        
      moveCarlosToBackState = 0;
      carlosAnimationCycle++;
      break;
    }
  }
}

function moveCarlosToTable()
{
  switch (moveCarlosToTableState)
  {
    case 0:
    {
      cCarlos.ChangeView(CARLOSNORMAL_1201);
      cCarlos.SetIdleView(-1, 0);
      cCarlos.MyWalk(oChair8.X + 130, oChair8.Y, eNoBlock, eWalkableAreas);
      moveCarlosToTableState++;
      break;
    }
    case 1:
    {
      if (cCarlos.Moving)
        break;
      
      if (!cCarlos.HasReachedDestination())
      {
        Point* destination = cCarlos.GetDestination();
        cCarlos.MyWalk(destination.x, destination.y, eNoBlock, eWalkableAreas);
        break;
      }
        
      moveCarlosToTableState++;
      break;
    }
    case 2:
    {
      cCarlos.Clickable = false;
      cCarlos.LockViewOffset(CARLOSIDLESIT_1205, 1, 0);
      cCarlos.Animate(2, 9, eOnce, eNoBlock, eBackwards);
      moveCarlosToTableState++;
      break;
    }
    case 3:
    {
      if (cCarlos.Animating)
        break;
      
      cCarlos.Clickable = true;
      cCarlos.ChangeView(CARLOSNORMALSIT);
      oCarlosGlass.Visible = false;
      cCarlos.UnlockView();
      cCarlos.SetIdleView(CARLOSIDLESIT_1205, 0);
      cCarlos.Loop = 0;
      carlosAnimationCycle = 0;
      moveCarlosToTableState = 0;
      break;
    }
  }
}

function idleCarlosAtBack()
{
  switch (idleCarlosAtBackState)
  {
    default:
    {
      int lastIdleFrame = Game.GetFrameCountForLoop(cCarlos.IdleView, 0) - 1;
      if (cCarlos.Frame != lastIdleFrame)
        break;

      carlosIdleAnimationsRemaining -= 1;
      if (carlosIdleAnimationsRemaining > 0)
      {
        break;
      }
      
      carlosIdleAnimationsRemaining = RandomBetween(MIN_CARLOS_IDLE_ANIMATIONS, MAX_CARLOS_IDLE_ANIMATIONS);
      if (!cCarlos.GetProperty("haveSpokenTo"))
      {
        break;
      }
      
      carlosAnimationCycle++;
      break;
    }
  }
}

function idleCarlosAtTable()
{                                  
  switch (idleCarlosAtTableState)
  {
    default:
    {
      int lastIdleFrame = Game.GetFrameCountForLoop(cCarlos.IdleView, 0) - 1;
      if (cCarlos.Frame != lastIdleFrame)
        break;

      carlosIdleAnimationsRemaining -= 1;
      if (carlosIdleAnimationsRemaining > 0)
      {
        break;
      }
      
      carlosIdleAnimationsRemaining = RandomBetween(MIN_CARLOS_IDLE_ANIMATIONS, MAX_CARLOS_IDLE_ANIMATIONS);
      if (!cCarlos.GetProperty("haveSpokenTo"))
      {
        break;
      }
      
      carlosAnimationCycle++;
      break;
    }
  }
}

function handleCarlos()
{
  switch (carlosAnimationCycle)
  {
    case 0:
    {
      idleCarlosAtTable();
      break;
    }
    case 1:
    {
      moveCarlosToBack();
      break;
    }
    case 2:
    {
      idleCarlosAtBack();
      break;
    }
    case 3:
    {
      moveCarlosToTable();
      break;
    }
  }
}

/*
Joanna functions
*/
function moveJoannaToTable()
{
  switch (moveJoannaToTableState)
  {
    // stand up
    case 0:
    {
      cJoanna.ChangeView(JOANNANORMAL_1101);
      cJoanna.SetIdleView(-1, 0);
      cJoanna.Clickable = false;
      cJoanna.LockViewOffset(JOANNASIT_1103, 2, 0);
      oJoannaMagazine.Visible = true;
      cJoanna.Animate(11, 9, eOnce, eNoBlock, eForwards);
      moveJoannaToTableState++;
      break;
    }
    // walk to aisle
    case 1:
    {
      if (cJoanna.Animating)
        break;
      
      cJoanna.Clickable = true;
      cJoanna.UnlockView();
      cJoanna.x += 10;
      cJoanna.y += 5;
      cJoanna.MyWalk(cJoanna.x, cJoanna.y + 8, eNoBlock, eAnywhere);
      moveJoannaToTableState++;
      break;
    }
    case 2:
    {
      if (cJoanna.Moving)
        break;
      
      if (!cJoanna.HasReachedDestination())
      {
        Point* destination = cJoanna.GetDestination();
        cJoanna.MyWalk(destination.x, destination.y, eNoBlock, eAnywhere);
        break;
      }
        
      moveJoannaToTableState++;
      break;
    }
    // walk to table
    case 3:
    {
      cJoanna.MyWalk(oChair8.X + 85, oChair8.Y - 5, eNoBlock, eWalkableAreas);
      moveJoannaToTableState++;
      break;
    }
    case 4:
    {
      if (cJoanna.Moving)
        break;
      
      if (!cJoanna.HasReachedDestination())
      {
        Point* destination = cJoanna.GetDestination();
        cJoanna.MyWalk(destination.x, destination.y, eNoBlock, eWalkableAreas);
        break;
      }
        
      moveJoannaToTableState++;
      break;
    }
    // sit down
    case 5:
    {
      cJoanna.LockViewAligned(JOANNASIT_1103, 2, eAlignRight);
      cJoanna.Clickable = true;
      cJoanna.Animate(10, 9, eOnce, eNoBlock, eBackwards);
      moveJoannaToTableState++;
      break;
    }
    case 6:
    {
      if (cJoanna.Animating)
        break;

      cJoanna.x -= 6;
      cJoanna.UnlockView();
      cJoanna.Clickable = true;
      cJoanna.ChangeView(JOANNANORMALSITNOMAG);
      cJoanna.SetIdleView(JOANNAIDLESITNOMAG, RandomBetween(MIN_JOANNA_IDLE_DELAY_SEC, MAX_JOANNA_IDLE_DELAY_SEC));
      joannaAnimationCycle++;
      moveJoannaToTableState = 0;
      break;
    }
  }
}

function moveJoannaToChair()
{
  switch (moveJoannaToChairState)
  {
    // stand up
    case 0:
    {
      cJoanna.ChangeView(JOANNANORMAL_1101);
      cJoanna.Clickable = false;
      cJoanna.LockViewOffset(JOANNASIT_1103, 1, 0);
      cJoanna.Animate(10, 9, eOnce, eNoBlock, eForwards);
      moveJoannaToChairState++;
      break;
    }
    // walk to aisle
    case 1:
    {
      if (cJoanna.Animating)
        break;
      
      cJoanna.x += 6;
      cJoanna.Clickable = true;
      cJoanna.UnlockView();
      cJoanna.SetIdleView(-1, 0);
      cJoanna.MyWalk(oChair4.X + 35, oChair4.Y + 12, eNoBlock, eWalkableAreas);
      moveJoannaToChairState++;
      break;
    }
    case 2:
    {
      if (cJoanna.Moving)
        break;
      
      if (!cJoanna.HasReachedDestination())
      {
        Point* destination = cJoanna.GetDestination();
        cJoanna.MyWalk(destination.x, destination.y, eNoBlock, eWalkableAreas);
        break;
      }
        
      moveJoannaToChairState++;      
      break;
    }
    // walk to chair
    case 3:
    {
      cJoanna.MyWalk(cJoanna.x, oChair4.Y - 2, eNoBlock, eAnywhere);
      moveJoannaToChairState++;
      break;
    }
    case 4:
    {
      if (cJoanna.Moving)
        break;
      
      if (!cJoanna.HasReachedDestination())
      {
        Point* destination = cJoanna.GetDestination();
        cJoanna.MyWalk(destination.x, destination.y, eNoBlock, eWalkableAreas);
        break;
      }
        
      moveJoannaToChairState++;
      break;
    }
    // sit down
    case 5:
    {
      cJoanna.Clickable = false;
      cJoanna.x -= 10;
      cJoanna.LockViewOffset(JOANNASIT_1103, 2, 0);
      cJoanna.Animate(11, 9, eOnce, eNoBlock, eBackwards);
      
      moveJoannaToChairState++;
      break;
    }
    case 6:
    {
      if (cJoanna.Animating)
        break;
      
      oJoannaMagazine.Visible = false;
      cJoanna.Clickable = true;
      cJoanna.UnlockView();
      cJoanna.SetIdleView(JOANNASIT_1103, RandomBetween(MIN_JOANNA_IDLE_DELAY_SEC, MAX_JOANNA_IDLE_DELAY_SEC));
      cJoanna.ChangeView(JOANNANORMALSIT);
      cJoanna.Loop = 0;
      joannaAnimationCycle = 0;
      moveJoannaToChairState = 0;
      break;
    }
  }
}

function idleJoannaAtChair()
{
  switch (idleJoannaAtChairState)
  {
    case 0:
    {
      if (cJoanna.Animating)
        break;
      
      cJoanna.UnlockView();
      idleJoannaAtChairState = -1;
      cJoanna.Loop = 0;
      break;
    }
    case 1:
    {
      if (cJoanna.Frame != 0)
        break;
        
      cJoanna.LockView(JOANNASIT_1103);
      cJoanna.Animate(1, 9, eOnce, eNoBlock, eForwards);
      idleJoannaAtChairState = 0;
      break;
    }
    case 2:
    {
      if (cJoanna.Frame != 0)
        break;
        
      cJoanna.LockViewOffset(JOANNASIT_1103, -1, 0);
      cJoanna.Animate(2, 9, eOnce, eNoBlock, eForwards);
      idleJoannaAtChairState = 0;
      break;
    }
    default:
    {
      int lastIdleFrame = Game.GetFrameCountForLoop(cJoanna.IdleView, 0) - 1;
      if (cJoanna.Frame != lastIdleFrame)
        break;

      joannaIdleAnimationsRemaining -= 1;
      if (joannaIdleAnimationsRemaining > 0)
      {
        idleJoannaAtChairState = RandomBetween(1, 2);
        break;
      }
      
      joannaIdleAnimationsRemaining = RandomBetween(MIN_JOANNA_IDLE_ANIMATIONS, MAX_JOANNA_IDLE_ANIMATIONS);
      if (!cJoanna.GetProperty("haveSpokenTo"))
      {
        idleJoannaAtChairState = RandomBetween(1, 2);
        break;
      }
      
      idleJoannaAtChairState = -1;
      joannaAnimationCycle++;
      break;
    }
  }
}

function idleJoannaAtTable()
{
  switch (idleJoannaAtTableState)
  {
    case 0:
    {
      if (cJoanna.Animating)
        break;
        
      cJoanna.UnlockView();
      idleJoannaAtTableState = -1;
      cJoanna.Loop = 0;
      break;
    }
    default:
    {
      int lastIdleFrame = Game.GetFrameCountForLoop(cJoanna.IdleView, 0) - 1;
      if (cJoanna.Frame != lastIdleFrame)
        break;

      joannaIdleAnimationsRemaining -= 1;
      if (joannaIdleAnimationsRemaining > 0)
      {
        idleJoannaAtTableState = RandomBetween(1, 2);
        break;
      }
      
      joannaIdleAnimationsRemaining = RandomBetween(MIN_JOANNA_IDLE_ANIMATIONS, MAX_JOANNA_IDLE_ANIMATIONS);
      idleJoannaAtTableState = -1;
      joannaAnimationCycle++;
      break;
    }
  }
}

function handleJoanna()
{
  switch (joannaAnimationCycle)
  {
    case 0:
    {
      idleJoannaAtChair();
      break;
    }
    case 1:
    {
      moveJoannaToTable();
      break;
    }
    case 2:
    {
      idleJoannaAtTable();
      break;
    }
    case 3:
    {
      moveJoannaToChair();
      break;
    }
  }
}

/*
Queenie functions
*/
function moveQueenieToBathroom()
{
  switch (moveQueenieToBathroomState)
  {
    // smoke ends
    case 0:
    {
      int lastSmokeFrame = Game.GetFrameCountForLoop(oQueenieSmoke.View, oQueenieSmoke.Loop) - 1;
      if (oQueenieSmoke.Frame != lastSmokeFrame)
        break;
      
      oQueenieSmoke.Animate(4, 9, eOnce, eNoBlock, eForwards);
      moveQueenieToBathroomState++;
      break;
    }
    // standup
    case 1:
    {
      if (oQueenieSmoke.Animating)
        break;
      
      oQueenieSmoke.Visible = false;
      cQueenie.ChangeView(QUEENIENORMAL_1151);
      cQueenie.LockViewAligned(QUEENIEIDLESIT_1153, 2, eAlignCenter);
      cQueenie.Animate(2, 9, eOnce, eNoBlock, eForwards);
      cQueenie.Clickable = false;
      moveQueenieToBathroomState++;
      break;
    }
    case 2:
    {
      // walk to aisle
      if (cQueenie.Animating)
        break;
      
      cQueenie.SetIdleView(-1, 0);
      cQueenie.Clickable = true;
      cQueenie.x += 10;
      cQueenie.UnlockView();
      cQueenie.MyWalk(cQueenie.x + 7, cQueenie.y, eNoBlock, eAnywhere);
      moveQueenieToBathroomState++;
      break;
    }
    case 3:
    {
      if (cQueenie.Moving)
        break;
      
      if (!cQueenie.HasReachedDestination())
      {
        Point* destination = cQueenie.GetDestination();
        cQueenie.MyWalk(destination.x, destination.y, eNoBlock, eWalkableAreas);
        break;
      }
        
      moveQueenieToBathroomState++;
      break;
    }
    // walk to bathroom door
    case 4:
    {
      if (cQueenie.Moving)
        break;
      
      cQueenie.MyWalk(bathroomDoor.approachX, bathroomDoor.approachY, eNoBlock, eWalkableAreas);
      moveQueenieToBathroomState++;
      break;
    }
    case 5:
    {
      if (cQueenie.Moving)
        break;
      
      if (!cQueenie.HasReachedDestination())
      {
        Point* destination = cQueenie.GetDestination();
        cQueenie.MyWalk(destination.x, destination.y, eNoBlock, eWalkableAreas);
        break;
      }
        
      moveQueenieToBathroomState++;
      break;
    }
    // walk through bathroom door
    case 6:
    {
      if (cQueenie.Moving)
        break;
      
      cQueenie.Clickable = false;
      cQueenie.FaceDirection(eDirectionLeft);
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eNoBlock, eForwards);
      bathroomDoor.doorObject.Clickable = false;
      moveQueenieToBathroomState++;
      break;
    }
    case 7:
    {
      if (bathroomDoor.doorObject.Animating)
        break;
      
      cQueenie.Walk(bathroomDoor.exitX, bathroomDoor.exitY, eNoBlock, eAnywhere);
      moveQueenieToBathroomState++;
      break;
      
    }
    case 8:
    {
      if (cQueenie.Moving)
        break;
      
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eNoBlock, eBackwards);
      moveQueenieToBathroomState++;
      break;
    }
    case 9:
    {
      if (bathroomDoor.doorObject.Animating)
        break;
      
      cQueenie.UnlockView();
      cQueenie.ChangeRoom(6, 85, 138);
      cQueenie.Clickable = true;
      bathroomDoor.doorObject.Clickable = true;
      moveQueenieToBathroomState = 0;
      queenieAnimationCycle++;
      queenieLoopsRemainingInBathroom = GetGameSpeed() * RandomBetween(MIN_QUEENIE_SECS_IN_BATHROOM, MAX_QUEENIE_SECS_IN_BATHROOM);
      break;
    }
  }
}

int moveQueenieToChairState = 0;
function moveQueenieToChair()
{
  switch (moveQueenieToChairState)
  {
    case 0:
    {
      bathroomDoor.doorObject.Clickable = false;
      cQueenie.Clickable = false;
      cQueenie.ChangeView(QUEENIENORMAL_1151);
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eNoBlock, eForwards);
      moveQueenieToChairState++;
      break;
    }
    case 1:
    {
      if (bathroomDoor.doorObject.Animating)
        break;
      
      cQueenie.Walk(bathroomDoor.approachX, bathroomDoor.approachY, eNoBlock, eAnywhere);
      moveQueenieToChairState++;
      break;
    }
    case 2:
    {
      if (cQueenie.Moving)
        break;
      
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eNoBlock, eBackwards);
      moveQueenieToChairState++;
      break;
    }
    case 3:
    {
      if (bathroomDoor.doorObject.Animating)
        break;
      
      bathroomDoor.doorObject.Clickable = true;
      cQueenie.Clickable = true;
      cQueenie.SetIdleView(-1, 0);
      cQueenie.MyWalk(oChair7.X + 35, oChair7.Y - 7, eNoBlock, eWalkableAreas);
      moveQueenieToChairState++;
      break;
    }
    case 4:
    {
      if (cQueenie.Moving)
        break;
      
      if (!cQueenie.HasReachedDestination())
      {
        Point* destination = cQueenie.GetDestination();
        cQueenie.MyWalk(destination.x, destination.y, eNoBlock, eWalkableAreas);
        break;
      }
        
      moveQueenieToChairState++;
      break;
    }
    case 5:
    {
      if (cQueenie.Moving)
        break;
 
      cQueenie.MyWalk(cQueenie.x, oChair7.Y - 2, eNoBlock, eAnywhere);
      moveQueenieToChairState++;
      break;
    }
    case 6:
    {
      if (cQueenie.Moving)
        break;
      
      if (!cQueenie.HasReachedDestination())
      {
        Point* destination = cQueenie.GetDestination();
        cQueenie.MyWalk(destination.x, destination.y, eNoBlock, eWalkableAreas);
        break;
      }
        
      moveQueenieToChairState++;
      break;
    }
    case 7:
    {
      if (cQueenie.Moving) 
        break;
      
      cQueenie.Clickable = false;
      cQueenie.x -= 10;
      cQueenie.LockView(QUEENIEIDLESIT_1153);
      cQueenie.Animate(2, 9, eOnce, eNoBlock, eBackwards);
      moveQueenieToChairState++;
      break;
    }
    case 8:
    {
      if (cQueenie.Animating)
        break;

      cQueenie.Clickable = true;
      cQueenie.UnlockView();
      cQueenie.ChangeView(QUEENIENORMALSIT);
      cQueenie.SetIdleView(QUEENIEIDLESIT_1153, RandomBetween(MIN_QUEENIE_IDLE_DELAY_SEC, MAX_QUEENIE_IDLE_DELAY_SEC));
      cQueenie.Loop = 0;
      moveQueenieToChairState = 0;
      queenieAnimationCycle = 0;
      oQueenieSmoke.Visible = true;
      oQueenieSmoke.Animate(3, 9, eRepeat, eNoBlock, eForwards);
      break;
    }
  }
}

function idleQueenieAtChair()
{                                  
  switch (idleQueenieAtChairState)
  {
    case 0:
    {
      if (cQueenie.Animating)
        break;
        
      idleQueenieAtChairState = -1;
        
      cQueenie.UnlockView();
      cQueenie.Loop = 0;
      
      oQueenieSmoke.Visible = true;
      oQueenieSmoke.Animate(3, 9, eRepeat, eNoBlock, eForwards);
      break;
    }
    case 1:
    {
      if (cQueenie.Frame != 0)
        break;
        
      if (oQueenieSmoke.Frame != 0)
        break;
      
      queenieIdleAnimationsRemaining -= 1;
      cQueenie.LockView(QUEENIEIDLESIT_1153);
      cQueenie.Animate(1, 9, eOnce, eNoBlock, eForwards);
      oQueenieSmoke.Visible = false;
      idleQueenieAtChairState = 0;
      break;
    }
    default:
    {
      if (cQueenie.IdleView != -1)
      {
        int lastIdleFrame = Game.GetFrameCountForLoop(cQueenie.IdleView, 0) - 1;
        if (cQueenie.Frame != lastIdleFrame)
          break;
        else
          cQueenie.SetIdleView(-1, 0);
      }
       
      if (oQueenieSmoke.Frame != 0)
        break;
      
      cQueenie.SetIdleView(QUEENIEIDLESIT_1153, RandomBetween(MIN_QUEENIE_IDLE_DELAY_SEC, MAX_QUEENIE_IDLE_DELAY_SEC));
      queenieIdleAnimationsRemaining -= 1;
      if (queenieIdleAnimationsRemaining > 0)
      {
        idleQueenieAtChairState = RandomBetween(1, 2);
        break;
      }
      
      queenieIdleAnimationsRemaining = RandomBetween(MIN_QUEENIE_IDLE_ANIMATIONS, MAX_QUEENIE_IDLE_ANIMATIONS);
      if (!cQueenie.GetProperty("haveSpokenTo"))
      {
        idleQueenieAtChairState = 1;
        break;
      }
      
      idleQueenieAtChairState = -1;
      queenieAnimationCycle++;
      break;
    }
  }
}

function idleQueenieAtBathroom()
{
  if (queenieLoopsRemainingInBathroom > 0)
  {
    queenieLoopsRemainingInBathroom -= 1;
    return;
  }

  cQueenie.ChangeRoom(player.Room, 30, 142);
  cQueenie.SetIdleView(QUEENIENORMAL_1151, RandomBetween(MIN_QUEENIE_IDLE_DELAY_SEC, MAX_QUEENIE_IDLE_DELAY_SEC));
  cQueenie.UnlockView();
  queenieAnimationCycle++;
}

function handleQueenie()
{
  switch (queenieAnimationCycle)
  {
    case 0:
    {
      idleQueenieAtChair();
      break;
    }
    case 1:
    {
      moveQueenieToBathroom();
      break;
    }
    case 2:
    {
      idleQueenieAtBathroom();
      break;
    }
    case 3:
    {
      moveQueenieToChair();
      break;
    }
  }
}

int objectSpeed = -1;
int cycleCounter = 0;

function handleClouds()
{
  cycleCounter++; // Increment the counter on each game cycle

  if (cycleCounter % 25 == 0) // Check if the counter is a multiple of 10
  {
    // Move object to the right if it hasn't reached the edge of the screen
    if (oCloudsA.X > -332)  // Replace 320 with the screen boundary you want
    {
        oCloudsA.X += objectSpeed; // Moves the object to the right
    }
    else
    {
        oCloudsA.X = 89; // Resets the object to the starting point if it reaches the edge
    }  
    
    if (oCloudsA.X > -332)  // Replace 320 with the screen boundary you want
    {
      oCloudsB.X += objectSpeed; // Moves the object to the right
    }
    else
    {
      oCloudsB.X = 509; // Resets the object to the starting point if it reaches the edge
    }  
  }
}

function hWasteBasket_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("A small metal waste basket sat on the cabin floor near the lavatory, its open top revealing crumpled papers within.");
}

function hWasteBasket_Interact(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.FaceDirection(eDirectionLeft);
  cEgo.LockViewAligned(CLARAMAINCABIN_1009, 0, eAlignRight);
  cEgo.Animate(0, 9, eOnce, eBlock);
  
  if (cEgo.HasInventory(iMagazine))
    oInsideWastebasket.Graphic = AIRPLANE_TRASH_EMPTY;
  else
    oInsideWastebasket.Graphic = AIRPLANE_TRASH_FULL;
  
  oInsideWastebasket.Baseline = 200;
  oInsideWastebasket.Visible = true;
}

function oInsideWastebasket_Handler()
{
  if (hasVisitedgMagazinePage2 == false)
  {
    Display("You have no reason to be digging through the trash.");
    return;
  }
  
  if (cEgo.HasInventory(iMagazine))
  {
    Display("Clara ruffles through the remaining wads of paper and realizes they are all identical.");
  }
  else
  {
    cEgo.Animate(1, 9, eOnce, eBlock);
    Display("Clara peers into the Waste Basket, spotting several crumpled wads of paper piled haphazardly inside. Curious, she reaches in and places it in her pocket.");
    cEgo.AddInventory(iCrumpledMagazine);
    oInsideWastebasket.Graphic = AIRPLANE_TRASH_EMPTY;
    cEgo.Animate(1, 9, eOnce, eBlock, eBackwards);
  }
}

function oInsideWastebasket_Interact(Object *theObject, CursorMode mode)
{
  oInsideWastebasket_Handler();
}

function oInsideWastebasket_Look(Object *theObject, CursorMode mode)
{
  oInsideWastebasket_Handler();
}

function oSeatPocket_Interact(Object *theObject, CursorMode mode)
{
  Display("The seatback pocket holds a glossy magazine and a neatly folded air sickness bag, both ready for use.");
}

function oSeatPocket_Look(Object *theObject, CursorMode mode)
{
  Display("The seatback pocket holds a glossy magazine and a neatly folded air sickness bag, both ready for use.");
}

function oPukeBag_Interact(Object *theObject, CursorMode mode)
{
  Display("You don’t need the air sickness bag right now.");
}

function oPukeBag_Look(Object *theObject, CursorMode mode)
{
  Display("A small, compact air sickness bag, neatly folded and ready to use.");
}

function oMagazine_Interact(Object *theObject, CursorMode mode)
{
  cEgo.LockViewOffset(CLARASIT_1003, -2, 0);
  cEgo.Animate(4, 9, eOnce, eBlock, eForwards);
  oMagazine.Visible = false;
  cEgo.Animate(5, 9, eOnce, eBlock, eForwards);
  cEgo.LockViewOffset(CLARASIT_1003, -2, 0);
  cEgo.Loop = 6;
  
  cEgo.ChangeRoom(4);
}

function oMagazine_Look(Object *theObject, CursorMode mode)
{
  Display("A glossy magazine filled with articles, advertisements, and pictures. Perfect for passing the time.");
}

function sitDown(Character *chara, Hotspot *hChair, int sitView, int sitLoop)
{
  chara.SetProperty("chairNumber", hChair1.ID);
  chara.SetProperty("isSitting", true);
  int currCharacterX = chara.x;
  
  chara.LockView(sitView);
  
  chara.x = (chara.x - 12);
  chara.Animate(sitLoop, 5, eOnce, eBlock, eBackwards);
}

function standUp(Character *chara, int standView, int standLoop)
{
  chara.SetProperty("isSitting", false);
  int currCharacterX = chara.x;
  
  chara.Animate(standLoop, 5, eOnce, eBlock, eForwards);
  cEgo.UnlockView(eStopMoving);
  cEgo.FaceDirection(eDirectionRight);
  chara.x = (currCharacterX + 12);
}

function oCockpitDoor_Interact(Object *cockPitDoor, CursorMode mode)
{
  oDoor_interact(cockPitDoor, 1, -20, -1, 100, -9, cEgo);
}

function oCockpitDoor_Look(Object *theObject, CursorMode mode)
{
  Display("The cockpit door is sturdy and unremarkable, its polished handle catching the light. Behind it, Clara can hear the faint murmur of activity as the pilot and copilot work to guide the plane safely toward Stonecliff Manor.");
}

function oBathroomDoor_Interact(Object *bathDoor, CursorMode mode)
{ 
  oDoor_interact(bathDoor, 0, 40, -1, -100, -9, cEgo);
}

function oBathroomDoor_Look(Object *theObject, CursorMode mode)
{
  Display("The bathroom door is simple with a small handle. It's clear where it leads—an essential space for passengers during long flights.");
}

function handlePopovers()
{
  if (oInsideWastebasket.Visible)
  {
    mouse.Mode = eModeExit;
    
    if (oInsideWastebasket.IsUnderMouse())
      mouse.Mode = eModeInteract;
      
    return;
  }
  
  if (oInsideCabinet.Visible)
  {
    mouse.Mode = eModeExit;
    
    if (oInsideCabinet.IsUnderMouse())
      mouse.Mode = eModeInteract;
      
    return;
  }
  
  if (oSeatPocket.Visible)
  {
    mouse.Mode = eModeExit;
    
    if (oSeatPocket.IsUnderMouse() || oMagazine.IsUnderMouse() || oPukeBag.IsUnderMouse())
      mouse.Mode = eModeInteract;
      
    return;
  }
}

/*
Overlay *gameInfoOverlay;
function repeatedly_execute_always()
{
  if (gameInfoOverlay != null)
    gameInfoOverlay.Remove();
  
  gameInfoOverlay = Overlay.CreateTextual(50, 150, 120, Game.SpeechFont, 15, String.Format("speed: %d, \nactive timer: %.2f, queenie timer: %.2f, carlos timer: %.2f, joaana timer: %.2f",
    GetGameSpeed(), GetTimeRemainingInSeconds(SET_ACTIVE_CHARACTER_TIMER), GetTimeRemainingInSeconds(cQueenie.ID), GetTimeRemainingInSeconds(cCarlos.ID), GetTimeRemainingInSeconds(cJoanna.ID)));
}
*/

function oChair1_Interact(Object *theObject, CursorMode mode)
{
  sitClaraInChair(theObject);
}

function oChair2_Interact(Object *theObject, CursorMode mode)
{
  sitClaraInChair(theObject);
}

function oChair3_Interact(Object *theObject, CursorMode mode)
{
  sitClaraInChair(theObject);
}

function oChair4_Interact(Object *theObject, CursorMode mode)
{
  Display("That’s Joanna's seat. It wouldn’t be proper to sit there.");
}

function oChair5_Interact(Object *theObject, CursorMode mode)
{
  sitClaraInChair(theObject);
}

function oChair6_Interact(Object *theObject, CursorMode mode)
{
  sitClaraInChair(theObject);
}

function oChair7_Interact(Object *theObject, CursorMode mode)
{
  Display("That’s Queenie's seat. It wouldn’t be proper to sit there.");
}

function oChair8_Interact(Object *theObject, CursorMode mode)
{
  Display("That’s Carlos's seat. It wouldn’t be proper to sit there.");
}

function oChair_Look(Object *theObject, CursorMode mode)
{
  Display("The airplane seats are neatly arranged in rows, each featuring adjustable armrests and immaculate cushions. A magazine rests in each seat pocket, its cover tempting Clara to take a closer look.");
}

function hCabinet_Interact(Hotspot *theHotspot, CursorMode mode)
{
  if (cSteward.Room == player.Room)
  {
    cEgo.Walk(470, 154, eBlock, eWalkableAreas);
    cEgo.FaceDirection(eDirectionRight);
    cSteward.Say("Do you need something, Ms. Bow?");
    Display("You cannot do anything with that while the steward is in the way");
    return;
  }
  cEgo.Walk(489, 154, eBlock, eWalkableAreas);
  cEgo.FaceDirection(eDirectionRight);
  cEgo.LockViewOffset(CLARAMAINCABIN_1009, 3, 1);
  cEgo.Animate(2, 9, eOnce, eBlock);
  
  if (cEgo.HasInventory(iCigar))
    oInsideCabinet.Graphic = AIRPLANE_CABINET_EMPTY;
  else
    oInsideCabinet.Graphic = AIRPLANE_CABINET_FULL;
  
  oInsideCabinet.Baseline = 200;
  oInsideCabinet.X = 304;
  oInsideCabinet.Y = 138;
  oInsideCabinet.Visible = true;
}

function hCabinet_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("The minibar is a compact, unassuming cabinet, seamlessly blending into the cabin's refined decor. Its contents are neatly tucked away behind closed doors.");
}

function oInsideCabinet_Interact(Object *theObject, CursorMode mode)
{
  if (cEgo.HasInventory(iCigar))
  {
    Display("There’s nothing else of interest left in the cabinet.");    
    return;
  }

  Display("You take the cigar.");
  Display("You snag a pack of matches, just to be safe.");
  cEgo.AddInventory(iCigar);
  cEgo.AddInventory(iMatchbox);
  oInsideCabinet.Graphic = AIRPLANE_CABINET_EMPTY;
}

function oInsideCabinet_Look(Object *theObject, CursorMode mode)
{
  if (!cEgo.HasInventory(iCigar))
  {
    Display("The cabinet is well-organized, holding a variety of items, but the cigar and matchbox catch your attention.");
    return RUN_DIALOG_STOP_DIALOG;
  }
  
  Display("The cabinet is well-organized, holding a variety of items, but there’s nothing else of interest left in the cabinet.");
}

function oCarlosGlass_Look(Object *theObject, CursorMode mode)
{
  cEgo.Walk(theObject.X, theObject.Y, eBlock, eWalkableAreas);
  cEgo.FaceObject(theObject);
  Display("Clara examines Carlos's glass and spots a faint fingerprint on the side, left behind in the condensation. It's an easy clue to overlook, but not for her sharp eye.");
}

function oCarlosGlass_Interact(Object *theObject, CursorMode mode)
{
  cEgo.Walk(theObject.X, theObject.Y, eBlock, eWalkableAreas);
  cEgo.FaceObject(theObject);
  Display("Clara examines Carlos's glass and spots a faint fingerprint on the side, left behind in the condensation. It's an easy clue to overlook, but not for her sharp eye.");
}

function hTable_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("The sleek, curved couch exudes luxury, a far cry from anything found on a typical passenger flight. Its pristine upholstery is unsurprising—nothing less would be expected from Xavier Montague Stone's private jet.");
}

function hCabinCouch_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("The sleek, curved couch exudes luxury, a far cry from anything found on a typical passenger flight. Its pristine upholstery is unsurprising—nothing less would be expected from Xavier Montague Stone's private jet.");
}

function oEmergencyDoor_Look(Object *theObject, CursorMode mode)
{
  Display("The emergency door looms as a stark reminder of the fragility of air travel. Clara has no intention of opening it… unless, of course, there's an emergency or perhaps curiosity to quench.");
}

function oEmergencyDoor_Interact(Object *theObject, CursorMode mode)
{
  cEgo.Walk(theObject.X + 20, theObject.Y, eBlock, eWalkableAreas);
  oEmergencyDoor.Visible = false;
  SetWalkBehindBase(8, 140);
  oChair2.Baseline = 140;
  cEgo.FaceDirection(eDirectionRight);
  cEgo.LockViewOffset(CLARAFALL_1004, 24, 0);
  Display("As Clara opens the emergency door, a deafening roar fills the cabin. The sudden rush of wind pulls her off balance, and before she can react, she's yanked out into the open sky, plummeting toward the ground below.");
  cEgo.Animate(5, 9, eOnce, eBlock, eForwards);
  cEgo.UnlockView();
  cEgo.ChangeRoom(301);
}

function hCabinWindows_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("The windows offer a picturesque view of the sky, with soft clouds rolling past like a living painting. The sunlight streams through, giving the cabin a warm, serene glow. It's a peaceful moment, but Clara's investigative instincts remind her to stay alert.");
}

function oCabinetBottlesGlasses_Interact(Object *theObject, CursorMode mode)
{
  if (cSteward.Room == player.Room)
  {
    cEgo.Walk(470, 154, eBlock, eWalkableAreas);
    cEgo.FaceDirection(eDirectionRight);
    cSteward.Say("Do you need something, Ms. Bow?");
    Display("You cannot do anything with that while the steward is in the way");
    return;
  }
 
  cEgo.Walk(490, 154, eBlock, eWalkableAreas);
  cEgo.FaceDirection(eDirectionRight);
  cEgo.LockViewOffset(CLARAMAINCABIN_1009, 4, 0);
  oCabinetBottles.Visible = false;
  oCabinetGlass1.Visible = false;
  oCabinetGlass2.Visible = false;
  cEgo.Animate(3, 9, eOnce, eBlock);
  Display("Ahhh....");
  oCabinetBottles.Visible = true;
  oCabinetGlass1.Visible = true;
  oCabinetGlass2.Visible = true;
  cEgo.UnlockView();
  cEgo.FaceDirection(eDirectionRight);
  numberDrinks++;
  
  if (numberDrinks > 1)
  {
    cEgo.Walk(cEgo.x - 15, cEgo.y - 8, eBlock, eWalkableAreas);
    cEgo.ChangeView(CLARADRUNKWALK);
    cEgo.Say("I dunn feeel sho well...");
    cEgo.Walk(185, 155, eBlock, eAnywhere);
    cEgo.LockView(CLARADRUNKMISC);
    cEgo.Animate(0, 9, eOnce, eBlock, eForwards);
    cSteward.Say("Ladies and gentlemen please return to your seats.");
    cSteward.Say("We are now approaching Stonecliff Manor.");
    Display("Good detective work, Clara");
    RestartGame();
  }
}

function oCabinetBottlesGlasses_Look(Object *theObject, CursorMode mode)
{
  Display("A row of neatly arranged liquor bottles and matching glasses sit on top of the minibar, ready for a quick pour.");
}

function room_LeaveRight()
{
  cEgo.ChangeRoom(5);
}

function room_LeaveLeft()
{
  cEgo.ChangeRoom(6);
}

function room_FirstLoad()
{
  /*
  Display("Welcome to this adventure!");
  Display("Before you get started, here are some important tips for you.");
  Display("Your mouse clicks will serve 2 main functions.");
  Display("The left click will interact with the world.");
  Display("The right click will look at items and give you helpful information.");
  Display("The FBI has given you a notebook that contains a list of guests.");
  Display("The notebook will also be used to track important information you discover.");
  Display("Don't worry, this information will be recorded automatically for you.");
  Display("Be on the look out for other useful detective tools.");
  //Display("Items in your notebook will change visually based on actions you need to take or have taken.");
  //Display("Simply put, colored items indicate information you have yet to discover, greyed items have not futher information to discover, and shadows items don't have enough know to gather information.");
  Display("Have fun on your adventure!");
  */
}

function loadClara()
{
  //cEgo.TurnOnDetails();
  switch(player.PreviousRoom)
  {
    case 5:
      cEgo.x = cockpitDoor.doorObject.X + 60;
      cEgo.y = cockpitDoor.doorObject.Y - 10;
      break;
    case 6:
      cEgo.x = bathroomDoor.doorObject.X - 60;
      cEgo.y = bathroomDoor.doorObject.Y - 10;
      break;
    default:
      break;
  }
}

function loadCarlos()
{
  if (player.PreviousRoom == 19)
    return;
    
  cCarlos.AddInventory(iCigar);
  cCarlos.TurnOnDetails();
  cCarlos.ChangeRoom(player.Room);
  
  idleCarlosAtBackState = -1;
  idleCarlosAtTableState = -1;
  moveCarlosToBackState = 0;
  moveCarlosToTableState = 0;
  
  carlosIdleAnimationsRemaining = RandomBetween(MIN_CARLOS_IDLE_ANIMATIONS, MAX_CARLOS_IDLE_ANIMATIONS);
  cCarlos.IdleAnimationDelay = GetGameSpeed() / 8;
  cCarlos.Clickable = true;
  cCarlos.UnlockView();
  
  oCarlosGlass.SetPosition(oChair8.X + 114, oChair8.Y - 24);
  oCarlosGlass.Baseline = 128;
  
  // place in chair
  if (carlosAnimationCycle == 0 || carlosAnimationCycle == 3)
  {
    cCarlos.x = oChair8.X + 130;
    cCarlos.y = oChair8.Y;
    carlosAnimationCycle = 0;
    
    cCarlos.SetIdleView(CARLOSIDLESIT_1205, RandomBetween(MIN_CARLOS_IDLE_DELAY_SEC, MAX_CARLOS_IDLE_DELAY_SEC));
    cCarlos.ChangeView(CARLOSNORMALSIT);
    cCarlos.Loop = 0;
    
    oCarlosGlass.Visible = false;
  }
  // place in back
  else
  {
    cCarlos.x = 105;
    cCarlos.y = 135;
    carlosAnimationCycle = 2;
    
    if (cCarlos.HasInventory(iCigar))
    {
      cCarlos.SetIdleView(CARLOSSMOKINGSTANDING_1203, RandomBetween(MIN_CARLOS_IDLE_DELAY_SEC, MAX_CARLOS_IDLE_DELAY_SEC));
      cCarlos.ChangeView(CARLOSNORMALSTANDING);
      cCarlos.Loop = 2;
    }
    else
    {
      cCarlos.SetIdleView(-1, 0);
      cCarlos.ChangeView(CARLOSNORMAL_1201);
      cCarlos.FaceDirection(eDirectionUp);
    }
    
    oCarlosGlass.Visible = true;
  }
}

function loadJoanna()
{
  if (player.PreviousRoom == 19)
    return;
    
  idleJoannaAtChairState = -1;
  idleJoannaAtTableState = -1;
  moveJoannaToChairState = 0;
  moveJoannaToTableState = 0;
  
  joannaIdleAnimationsRemaining = RandomBetween(MIN_JOANNA_IDLE_ANIMATIONS, MAX_JOANNA_IDLE_ANIMATIONS);
  cJoanna.Clickable = true;
  cJoanna.UnlockView();
  
  oJoannaMagazine.SetPosition(oChair4.X + 10, oChair4.Y - 2);
  cJoanna.IdleAnimationDelay = GetGameSpeed() / 2;
  cJoanna.ChangeRoom(player.Room);
  
  oJoannaMagazine.SetPosition(oChair4.X + 10, oChair4.Y - 2);
  
  // place in chair
  if (joannaAnimationCycle == 0 || joannaAnimationCycle == 3)
  {
    cJoanna.x = oChair4.X + 25;
    cJoanna.y = oChair4.Y - 2;
    joannaAnimationCycle = 0;
    
    cJoanna.SetIdleView(JOANNASIT_1103, RandomBetween(MIN_JOANNA_IDLE_DELAY_SEC, MAX_JOANNA_IDLE_DELAY_SEC));
    cJoanna.ChangeView(JOANNANORMALSIT);
    
    oJoannaMagazine.Visible = false;
  }
  // place at table
  else
  {
    cJoanna.x = oChair8.X + 85;
    cJoanna.y = oChair8.Y - 5;
    joannaAnimationCycle = 2;

    cJoanna.ChangeView(JOANNANORMALSITNOMAG);
    cJoanna.SetIdleView(JOANNAIDLESITNOMAG, RandomBetween(MIN_JOANNA_IDLE_DELAY_SEC, MAX_JOANNA_IDLE_DELAY_SEC));
    cJoanna.Loop = 2;
    
    oJoannaMagazine.Visible = true;
  }
}

function loadQueenie()
{
  switch(player.PreviousRoom)
  {
    case 19:
      return;
    case 5:
      idleQueenieAtChairState = -1;
      moveQueenieToChairState = 0;
      moveQueenieToBathroomState = 0;
      queenieIdleAnimationsRemaining = RandomBetween(MIN_QUEENIE_IDLE_ANIMATIONS, MAX_QUEENIE_IDLE_ANIMATIONS);
      queenieLoopsRemainingInBathroom = GetGameSpeed() * RandomBetween(MIN_QUEENIE_SECS_IN_BATHROOM, MAX_QUEENIE_SECS_IN_BATHROOM);
      
      // place in bathroom
      if (queenieAnimationCycle == 1 || queenieAnimationCycle == 2)
      {
        queenieAnimationCycle = 2;
        
        cQueenie.ChangeRoom(6);
        cQueenie.ChangeView(QUEENIENORMAL_1151);
        cQueenie.SetIdleView(-1, 0);
      }
      // place in chair
      else
      {
        cQueenie.x = oChair7.X + 25;
        cQueenie.y = oChair7.Y - 2;
        queenieAnimationCycle = 0;
        
        cQueenie.ChangeView(QUEENIENORMALSIT);
        cQueenie.SetIdleView(QUEENIEIDLESIT_1153, RandomBetween(MIN_QUEENIE_IDLE_DELAY_SEC, MAX_QUEENIE_IDLE_DELAY_SEC));
        cQueenie.Loop = 0;
      }
      
      break;
    case 6:
      idleQueenieAtChairState = -1;
      moveQueenieToChairState = 0;
      moveQueenieToBathroomState = 0;
      queenieIdleAnimationsRemaining = RandomBetween(MIN_QUEENIE_IDLE_ANIMATIONS, MAX_QUEENIE_IDLE_ANIMATIONS);
      queenieLoopsRemainingInBathroom = GetGameSpeed() * RandomBetween(MIN_QUEENIE_SECS_IN_BATHROOM, MAX_QUEENIE_SECS_IN_BATHROOM);
      
      // place in bathroom
      if (cQueenie.Room != player.Room)
      {
        queenieAnimationCycle = 2;
        
        cQueenie.ChangeRoom(6);
        cQueenie.ChangeView(QUEENIENORMAL_1151);
        cQueenie.SetIdleView(-1, 0);
        oQueenieSmoke.Visible = false;
      }
      // place in chair
      else
      {
        cQueenie.x = oChair7.X + 25;
        cQueenie.y = oChair7.Y - 2;
        queenieAnimationCycle = 0;
        
        cQueenie.ChangeView(QUEENIENORMALSIT);
        cQueenie.SetIdleView(QUEENIEIDLESIT_1153, RandomBetween(MIN_QUEENIE_IDLE_DELAY_SEC, MAX_QUEENIE_IDLE_DELAY_SEC));
        cQueenie.Loop = 0;
        oQueenieSmoke.Visible = true;
      }
      
      break;
    default:
      cQueenie.ChangeRoom(player.Room, oChair7.X + 25, oChair7.Y - 2);
      cQueenie.ChangeView(QUEENIENORMALSIT);
      cQueenie.SetIdleView(QUEENIEIDLESIT_1153, RandomBetween(MIN_QUEENIE_IDLE_DELAY_SEC, MAX_QUEENIE_IDLE_DELAY_SEC));
      cQueenie.IdleAnimationDelay = GetGameSpeed() / 2;
      
      queenieAnimationCycle = 0;
      queenieIdleAnimationsRemaining = RandomBetween(MIN_QUEENIE_IDLE_ANIMATIONS, MAX_QUEENIE_IDLE_ANIMATIONS);
      queenieLoopsRemainingInBathroom = GetGameSpeed() * RandomBetween(MIN_QUEENIE_SECS_IN_BATHROOM, MAX_QUEENIE_SECS_IN_BATHROOM);
      
      idleQueenieAtChairState = -1;
      moveQueenieToChairState = 0;
      
      moveQueenieToBathroomState = 0;
      
      oQueenieSmoke.X = cQueenie.x - 17;
      oQueenieSmoke.Y = cQueenie.y;
      oQueenieSmoke.SetView(QUEENIEIDLESIT_1153, 3, 0);
      oQueenieSmoke.Visible = true;
      break;
  }
}

function loadSteward()
{
  cSteward.x = 483;
  cSteward.y = 157;
  cSteward.FaceDirection(eDirectionLeft);
}

function loadDoors()
{
  oBathroomDoor.SetView(AIRPLANEDOORS_5003);
  oCockpitDoor.SetView(AIRPLANEDOORS_5003);
  oCockpitDoor.Graphic = 1069;
  bathroomDoor.Init(oBathroomDoor, 35, 0, -20, -2);
  cockpitDoor.Init(oCockpitDoor, -25, 0, 30, -2);
  
  bathroomDoor.doorObject.Clickable = true;
  cockpitDoor.doorObject.Clickable = true;
}

function room_Load()
{
  gIconbar.Visible = true;
  AudioChannel *musicChannel = System.AudioChannels[1];
  AudioChannel *backgroundChannel = System.AudioChannels[2];
  if (!musicChannel.IsPlaying)
  {
    a6001_AirplaneMusic.PlayOnChannel(1, eAudioPriorityHigh, eRepeat);
    aAirplane_hum.PlayOnChannel(2, eAudioPriorityNormal, eRepeat);
  }
  
  loadClara();
  loadCarlos();
  loadJoanna();
  loadQueenie();
  loadSteward();
  loadDoors();
}

function loadQueenieAfterFadeIn()
{
  if (!oQueenieSmoke.Animating)
    oQueenieSmoke.Animate(3, 9, eRepeat, eNoBlock, eForwards);
}

function room_AfterFadeIn()
{
  loadQueenieAfterFadeIn();

  switch(player.PreviousRoom)
  {
    case 4:
      cEgo.Animate(5, 8, eOnce, eBlock, eBackwards);
      oMagazine.Visible = true;
      cEgo.Animate(4, 8, eOnce, eBlock, eBackwards);
      break;
    case 5:
      cockpitDoor.doorObject.SetView(AIRPLANEDOORS_5003);
      cockpitDoor.doorObject.Animate(1, 9, eOnce, eBlock, eForwards);
      cEgo.Walk(cEgo.x - 80, cEgo.y + 9, eBlock, eAnywhere);
      cockpitDoor.doorObject.Animate(1, 9, eOnce, eBlock, eBackwards);
      break;
    case 6:
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eBlock, eForwards);
      cEgo.Walk(cEgo.x + 100, cEgo.y + 9, eBlock, eAnywhere);
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eBlock, eBackwards);
      if (threadTied)
      {
        cEgo.Walk(cEgo.x + 20, cEgo.y - 10, eBlock, eWalkableAreas);
        cEgo.FaceDirection(eDirectionUp);
        Display("*ding*");
        cSteward.x = 320;
        cSteward.y = 143;
        cSteward.Walk(bathroomDoor.approachX + 100, bathroomDoor.approachY, eBlock, eWalkableAreas);
        cSteward.Walk(bathroomDoor.approachX - 15, bathroomDoor.approachY, eBlock, eAnywhere);
        cSteward.LockView(STEWARDRANDOM);
        cSteward.Animate(0, 9, eOnce, eBlock, eForwards);
        bathroomDoor.doorObject.Animate(0, 9, eOnce, eBlock, eForwards);
        cSteward.UnlockView();
        cSteward.Walk(cSteward.x - 50, cSteward.y - 2, eBlock, eAnywhere);
        bathroomDoor.doorObject.Animate(0, 9, eOnce, eBlock, eBackwards);
        cSteward.ChangeRoom(6, 0, 0);
        threadTied = false;
      }
      break;
    default:
      break;
  }
  
  if (activeDialog != null)
  {
    activeDialog.Start();
  }
}

function on_mouse_click(MouseButton button)
{
  if (button == eMouseLeft && Mouse.Mode == eModeExit)
  {
    if (oInsideCabinet.Visible)
    {
      oInsideCabinet.Visible = false;
      cEgo.Animate(2, 9, eOnce, eBlock, eBackwards);
      cEgo.UnlockView();
      cEgo.FaceDirection(eDirectionRight);
      
      return;
    }
    
    if (oInsideWastebasket.Visible)
    {
      cEgo.Animate(0, 9, eOnce, eBlock, eBackwards);
      cEgo.UnlockView();
      cEgo.FaceDirection(eDirectionLeft);
      oInsideWastebasket.Visible = false;
      
      return;
    }
    
    if (oSeatPocket.Visible)
    {
      cEgo.Animate(3, 9, eOnce, eBlock, eForwards);
      cEgo.UnlockView();
      cEgo.FaceDirection(eDirectionRight);
      oSeatPocket.Visible = false;
      oMagazine.Visible = false;
      oPukeBag.Visible = false;
      
      return;
    }
  }

  if (!IsGamePaused())
  {
    if (button == eMouseRight && invCursorToggle)
    {
      return;
    }
    
    if ((button == eMouseRight) && (Mouse.IsOverWalkableArea() || Mouse.IsOverNothing()) && !(Mouse.IsOverHotspot()))
    {
      mouse.Mode = eModeLookat;
      Display("The cabin radiates understated luxury, with polished wood accents and the gentle hum of the engines setting a tranquil tone. Rows of seats align with windows that reveal a pastel sky, while a lounge area, complete with a plush couch and sleek table, makes it clear this is no ordinary flight.");
      choose_cursor();
      ClaimEvent();
    }
  }
}

function repeatedly_execute()
{
  Object *currentMouseOverObject = Mouse.GetOverObject();
  if (currentMouseOverObject == oEmergencyDoor)
    SetRestartPoint();
  Hotspot *currentMouseOverHotspot = Mouse.GetOverHotspot();
  if (currentMouseOverHotspot == hCabinet)
    SetRestartPoint();
  
  handlePopovers();
  
  handleCarlos();
  handleJoanna();
  handleQueenie();
}

function repeatedly_execute_always()
{
  handleClouds();
}
