Door bathroomDoor;
Door cockpitDoor;

#define MIN_CARLOS_IDLE_ANIMATIONS 1
#define MAX_CARLOS_IDLE_ANIMATIONS 1
#define MIN_CARLOS_IDLE_DELAY_SEC 0
#define MAX_CARLOS_IDLE_DELAY_SEC 0

#define MIN_JOANNA_IDLE_ANIMATIONS 1
#define MAX_JOANNA_IDLE_ANIMATIONS 1
#define MIN_JOANNA_IDLE_DELAY_SEC 0
#define MAX_JOANNA_IDLE_DELAY_SEC 0

#define MIN_QUEENIE_IDLE_ANIMATIONS 1
#define MAX_QUEENIE_IDLE_ANIMATIONS 1
#define MIN_QUEENIE_IDLE_DELAY_SEC 0
#define MAX_QUEENIE_IDLE_DELAY_SEC 0
#define MIN_QUEENIE_SECS_IN_BATHROOM 1
#define MAX_QUEENIE_SECS_IN_BATHROOM 1

Character* activeCharacter;
Character* previousActiveCharacter;

int carlosAnimationCycle;
int carlosIdleAnimationsRemaining;
int moveCarlosToTableState;
int moveCarlosToBackState;
int idleCarlosAtBackState;
int idleCarlosAtTableState;

int joannaAnimationCycle;
int joannaIdleAnimationsRemaining;
int moveJoannaToTableState;
int moveJoannaToChairState;
int idleJoannaAtChairState;
int idleJoannaAtTableState;

int queenieAnimationCycle;
int queenieLoopsRemainingInBathroom;
int queenieIdleAnimationsRemaining;
int idleQueenieAtChairState;
int moveQueenieToBathroomState;

function oDoor_interact(Object *theDoor, int loop, int walkToOffsetX,  int walkToOffsetY,  int walkOffOffsetX,  int walkOffOffsetY, Character* chara)
{
  int relativeX = theDoor.X + walkToOffsetX;
  int relativeY = theDoor.Y + walkToOffsetY;
  
  if (theDoor == oBathroomDoor)
  {
    if (!chara.CancelableWalk(relativeX, relativeY, eDirectionLeft))
      return;
  }
  else
  {
    if (!chara.CancelableWalk(relativeX, relativeY, eDirectionRight))
      return;
  }

  if (theDoor == oBathroomDoor && cSteward.Room != player.Room && cQueenie.Room == player.Room)
  {
    Display("The steward is distracted. Take your chance with the minibar!");
    return;
  }
  
  theDoor.SetView(AIRPLANEDOORS_5003);
  theDoor.Animate(loop, 9, eOnce, eBlock, eForwards);
  
  chara.Walk(relativeX + walkOffOffsetX, relativeY + walkOffOffsetY, eBlock, eAnywhere);
}

/*
Carlos functions
*/
function moveCarlosToBack()
{
  switch (moveCarlosToBackState)
  {
    // release glass
    case 0:
    {
      cCarlos.Clickable = false;
      cCarlos.LockView(CARLOSRANDOMSIT);
      cCarlos.Animate(2, 14, eOnce, eNoBlock, eForwards);
      moveCarlosToBackState++;
      break;
    }
    // stand up
    case 1:
    {
      if (cCarlos.Animating)
        break;
        
      cCarlos.Sitting(false);
      cCarlos.LockViewOffset(CARLOSRANDOMSIT, 1, 0);
      cCarlos.Animate(4, 9, eOnce, eNoBlock, eForwards);
      oCarlosGlass.Visible = true;
      moveCarlosToBackState++;
      break;
    }
    // setup
    case 2:
    {
      if (cCarlos.Animating)
        break;
      
      cCarlos.Sitting(false);
      cCarlos.ChangeView(CARLOSNORMAL_1201);
      cCarlos.SetIdleView(-1, 0);
      cCarlos.Clickable = true;
      cCarlos.UnlockView();
      cCarlos.Direction(eDirectionLeft);
      
      moveCarlosToBackState++;
      break;
    }
    case 3:
    {
      // walk to back
      if (!cCarlos.ResumableWalk(105, 135))
      {
        break;
      }
      
      moveCarlosToBackState++;
      break;
    }
    case 4:
    {
      // setup
      if (cCarlos.HasInventory(iCigar))
      {
        cCarlos.SetIdleView(CARLOSSMOKINGSTANDING_1203, RandomBetween(MIN_CARLOS_IDLE_DELAY_SEC, MAX_CARLOS_IDLE_DELAY_SEC));
        cCarlos.Direction(eDirectionDown);
      }
      else
      {
        cCarlos.Direction(eDirectionUp);
      }
        
      moveCarlosToBackState = 0;
      carlosAnimationCycle++;
      break;
    }
  }
}

function moveCarlosToTable()
{
  switch (moveCarlosToTableState)
  {
    // setup
    case 0:
    {
      cCarlos.ChangeView(CARLOSNORMAL_1201);
      cCarlos.SetIdleView(-1, 0);
      moveCarlosToTableState++;
      break;
    }
    // walk to table
    case 1:
    {
      if (!cCarlos.ResumableWalk(oChair8.X + 130, oChair8.Y))
      {
        break;
      }
      moveCarlosToTableState++;
      break;
    }
    // sit down
    case 2:
    {
      cCarlos.Clickable = false;
      cCarlos.LockViewOffset(CARLOSRANDOMSIT, 0, 0);
      cCarlos.Animate(4, 9, eOnce, eNoBlock, eBackwards);
      moveCarlosToTableState++;
      break;
    }
    // grab glass
    case 3:
    {
      if (cCarlos.Animating)
        break;
      cCarlos.Sitting(true);
      cCarlos.Animate(2, 14, eOnce, eNoBlock, eBackwards);
      oCarlosGlass.Visible = false;
      moveCarlosToTableState++;
      break;
    }
    // setup
    case 4:
    {
      if (cCarlos.Animating)
        break;
      
      cCarlos.Clickable = true;
      cCarlos.ChangeView(CARLOSNORMALSIT);
      cCarlos.SetIdleView(CARLOSIDLESIT, RandomBetween(MIN_CARLOS_IDLE_DELAY_SEC, MAX_CARLOS_IDLE_DELAY_SEC));
      cCarlos.Direction(eDirectionLeft);
      cCarlos.UnlockView();
      carlosAnimationCycle = 0;
      moveCarlosToTableState = 0;
      break;
    }
  }
}

function idleCarlosAtBack()
{
  switch (idleCarlosAtBackState)
  {
    default:
    {
      if (cCarlos.IdleView != -1)
      {
        int lastIdleFrame = Game.GetFrameCountForLoop(cCarlos.IdleView, cCarlos.Loop) - 1;
        if (cCarlos.Frame != lastIdleFrame)
          break;
      }

      carlosIdleAnimationsRemaining -= 1;
      if (carlosIdleAnimationsRemaining > 0)
        break;
      
      carlosIdleAnimationsRemaining = RandomBetween(MIN_CARLOS_IDLE_ANIMATIONS, MAX_CARLOS_IDLE_ANIMATIONS);
      carlosAnimationCycle++;
      break;
    }
  }
}

function idleCarlosAtTable()
{                                  
  switch (idleCarlosAtTableState)
  {
    default:
    {
      int lastIdleFrame = Game.GetFrameCountForLoop(cCarlos.IdleView, cCarlos.Loop) - 1;
      if (cCarlos.Frame != lastIdleFrame)
        break;

      carlosIdleAnimationsRemaining -= 1;
      if (carlosIdleAnimationsRemaining > 0)
      {
        break;
      }
      
      carlosIdleAnimationsRemaining = RandomBetween(MIN_CARLOS_IDLE_ANIMATIONS, MAX_CARLOS_IDLE_ANIMATIONS);
      if (!cCarlos.GetProperty("haveSpokenTo"))
      {
        break;
      }
      
      carlosAnimationCycle++;
      break;
    }
  }
}

function handleCarlos()
{
  switch (carlosAnimationCycle)
  {
    case 0:
    {
      idleCarlosAtTable();
      break;
    }
    case 1:
    {
      moveCarlosToBack();
      break;
    }
    case 2:
    {
      idleCarlosAtBack();
      break;
    }
    case 3:
    {
      moveCarlosToTable();
      break;
    }
  }
}

/*
Joanna functions
*/
function moveJoannaToTable()
{
  switch (moveJoannaToTableState)
  {
    // stand up
    case 0:
    {
      cJoanna.ChangeView(JOANNANORMAL_1101);
      cJoanna.Sitting(false);
      cJoanna.SetIdleView(-1, 0);
      cJoanna.Clickable = false;
      cJoanna.LockViewOffset(JOANNASIT_1103, 2, 0);
      oJoannaMagazine.Visible = true;
      cJoanna.Animate(11, 9, eOnce, eNoBlock, eForwards);
      moveJoannaToTableState++;
      break;
    }
    // setup
    case 1:
    {
      if (cJoanna.Animating)
        break;
      
      cJoanna.Clickable = true;
      cJoanna.UnlockView();
      cJoanna.x += 10;
      //cJoanna.y += 5;
      moveJoannaToTableState++;
      break;
    }
    // walk to aisle
    case 2:
    {
      if (!cJoanna.ResumableWalk(cJoanna.x, 144, eNoBlock, eAnywhere))
        break;
      moveJoannaToTableState++;
      break;
    }
    // walk to table
    case 3:
    {
      if (!cJoanna.ResumableWalk(381, 129))
      {
        break;
      }
      moveJoannaToTableState++;
      break;
    }
    // sit down
    case 4:
    {
      cJoanna.LockViewFrame(JOANNASIT_1103, 10, 13);
      cJoanna.LockViewAligned(JOANNASIT_1103, 10, eAlignRight);
      cJoanna.Clickable = false;
      cJoanna.Animate(10, 9, eOnce, eNoBlock, eBackwards);
      moveJoannaToTableState++;
      break;
    }
    // setup
    case 5:
    {
      if (cJoanna.Animating)
        break;
      cJoanna.Sitting(true);
      cJoanna.x -= 1;
      cJoanna.UnlockView();
      cJoanna.Clickable = true;
      cJoanna.ChangeView(JOANNANORMALSITSOLO);
      cJoanna.SetIdleView(JOANNAIDLESITSOLO, RandomBetween(MIN_JOANNA_IDLE_DELAY_SEC, MAX_JOANNA_IDLE_DELAY_SEC));
      cJoanna.Direction(eDirectionRight);
      joannaAnimationCycle++;
      moveJoannaToTableState = 0;
      break;
    }
  }
}

function moveJoannaToChair()
{
  switch (moveJoannaToChairState)
  {
    // stand up
    case 0:
    {
      cJoanna.ChangeView(JOANNANORMAL_1101);
      cJoanna.Clickable = false;
      cJoanna.Sitting(false);
      cJoanna.LockViewOffset(JOANNASIT_1103, 1, 0);
      cJoanna.Animate(10, 9, eOnce, eNoBlock, eForwards);
      moveJoannaToChairState++;
      break;
    }
    // walk to aisle
    case 1:
    {
      if (cJoanna.Animating)
        break;
      
      cJoanna.x += 6;
      cJoanna.Clickable = true;
      cJoanna.UnlockView();
      cJoanna.SetIdleView(-1, 0);
      moveJoannaToChairState++;
      break;
    }
    case 2:
    {
      if (!cJoanna.ResumableWalk(oChair4.X + 35, oChair4.Y + 12))
        break;
      moveJoannaToChairState++;      
      break;
    }
    // walk to chair
    case 3:
    {
      if (!cJoanna.ResumableWalk(cJoanna.x, oChair4.Y - 2, eNoBlock, eAnywhere))
        break;
      moveJoannaToChairState++;
      break;
    }
    // sit down
    case 4:
    {
      cJoanna.Clickable = false;
      cJoanna.x -= 10;
      cJoanna.Sitting(true);
      cJoanna.LockViewOffset(JOANNASIT_1103, 2, 0);
      cJoanna.Animate(11, 9, eOnce, eNoBlock, eBackwards);
      
      moveJoannaToChairState++;
      break;
    }
    case 5:
    {
      if (cJoanna.Animating)
        break;
      
      oJoannaMagazine.Visible = false;
      cJoanna.Clickable = true;
      cJoanna.UnlockView();
      cJoanna.SetIdleView(JOANNASIT_1103, RandomBetween(MIN_JOANNA_IDLE_DELAY_SEC, MAX_JOANNA_IDLE_DELAY_SEC));
      //cJoanna.ChangeView(JOANNANORMALSIT);
      cJoanna.ChangeView(JOANNANORMAL_1101);
      cJoanna.Loop = 0;
      joannaAnimationCycle = 0;
      moveJoannaToChairState = 0;
      break;
    }
  }
}

function idleJoannaAtChair()
{
  switch (idleJoannaAtChairState)
  {
    case 0:
    {
      if (cJoanna.Animating)
        break;
      
      cJoanna.UnlockView();
      cJoanna.Direction(eDirectionRight);
      idleJoannaAtChairState = -1;
      break;
    }
    case 1:
    {
      if (cJoanna.Frame != 0)
        break;
        
      cJoanna.LockView(JOANNASIT_1103);
      cJoanna.Animate(1, 9, eOnce, eNoBlock, eForwards);
      idleJoannaAtChairState = 0;
      break;
    }
    case 2:
    {
      if (cJoanna.Frame != 0)
        break;
        
      cJoanna.LockViewOffset(JOANNASIT_1103, -1, 0);
      cJoanna.Animate(2, 9, eOnce, eNoBlock, eForwards);
      idleJoannaAtChairState = 0;
      break;
    }
    default:
    {
      int lastIdleFrame = Game.GetFrameCountForLoop(cJoanna.IdleView, cJoanna.Loop) - 1;
      if (cJoanna.Frame != lastIdleFrame)
        break;

      joannaIdleAnimationsRemaining -= 1;
      if (joannaIdleAnimationsRemaining > 0)
      {
        idleJoannaAtChairState = RandomBetween(1, 2);
        break;
      }
      
      joannaIdleAnimationsRemaining = RandomBetween(MIN_JOANNA_IDLE_ANIMATIONS, MAX_JOANNA_IDLE_ANIMATIONS);
      if (!cJoanna.GetProperty("haveSpokenTo"))
      {
        idleJoannaAtChairState = RandomBetween(1, 2);
        break;
      }
      
      idleJoannaAtChairState = -1;
      joannaAnimationCycle++;
      break;
    }
  }
}

function idleJoannaAtTable()
{
  switch (idleJoannaAtTableState)
  {
    case 0:
    {
      if (cJoanna.Animating)
        break;
        
      cJoanna.UnlockView();
      idleJoannaAtTableState = -1;
      break;
    }
    default:
    {
      int lastIdleFrame = Game.GetFrameCountForLoop(cJoanna.IdleView, cJoanna.Loop) - 1;
      if (cJoanna.Frame != lastIdleFrame)
        break;

      joannaIdleAnimationsRemaining -= 1;
      if (joannaIdleAnimationsRemaining > 0)
      {
        idleJoannaAtTableState = -1;
        break;
      }
      
      joannaIdleAnimationsRemaining = RandomBetween(MIN_JOANNA_IDLE_ANIMATIONS, MAX_JOANNA_IDLE_ANIMATIONS);
      idleJoannaAtTableState = -1;
      joannaAnimationCycle++;
      break;
    }
  }
}

function handleJoanna()
{
  switch (joannaAnimationCycle)
  {
    case 0:
    {
      idleJoannaAtChair();
      break;
    }
    case 1:
    {
      moveJoannaToTable();
      break;
    }
    case 2:
    {
      idleJoannaAtTable();
      break;
    }
    case 3:
    {
      moveJoannaToChair();
      break;
    }
  }
}

/*
Queenie functions
*/
function moveQueenieToBathroom()
{
  switch (moveQueenieToBathroomState)
  {
    // smoke ends
    case 0:
    {
      int lastSmokeFrame = Game.GetFrameCountForLoop(oQueenieSmoke.View, oQueenieSmoke.Loop) - 1;
      if (oQueenieSmoke.Frame != lastSmokeFrame)
        break;
      
      cQueenie.Clickable = false;
      oQueenieSmoke.Animate(5, 9, eOnce, eNoBlock, eForwards);
      moveQueenieToBathroomState++;
      break;
    }
    // standup
    case 1:
    {
      if (oQueenieSmoke.Animating)
        break;
      
      oQueenieSmoke.Visible = false;
      cQueenie.ChangeView(QUEENIENORMAL_1151);
      cQueenie.LockViewAligned(QUEENIERANDOMSIT, 2, eAlignCenter);
      cQueenie.Animate(2, 9, eOnce, eNoBlock, eForwards);
      moveQueenieToBathroomState++;
      break;
    }
    case 2:
    {
      // walk to aisle
      if (cQueenie.Animating)
        break;
      
      cQueenie.SetIdleView(-1, 0);
      cQueenie.Clickable = true;
      cQueenie.Sitting(false);
      cQueenie.x += 10;
      cQueenie.UnlockView();
      moveQueenieToBathroomState++;
      break;
    }
    case 3:
    {
      if (!cQueenie.ResumableWalk(oChair7.X + 40, cQueenie.y, eNoBlock, eAnywhere))
        break;
      moveQueenieToBathroomState++;
      break;
    }
    // walk to bathroom door
    case 4:
    {
      if (!cQueenie.ResumableWalk(bathroomDoor.approachX, bathroomDoor.approachY))
        break;
      moveQueenieToBathroomState++;
      break;
    }
    // walk through bathroom door
    case 5:
    {
      if (cQueenie.Moving)
        break;
      
      cQueenie.Clickable = false;
      cQueenie.Direction(eDirectionLeft);
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eNoBlock, eForwards);
      bathroomDoor.doorObject.Clickable = false;
      moveQueenieToBathroomState++;
      break;
    }
    case 6:
    {
      if (bathroomDoor.doorObject.Animating)
        break;
      
      cQueenie.Walk(bathroomDoor.exitX, bathroomDoor.exitY, eNoBlock, eAnywhere);
      moveQueenieToBathroomState++;
      break;
      
    }
    case 7:
    {
      if (cQueenie.Moving)
        break;
      
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eNoBlock, eBackwards);
      moveQueenieToBathroomState++;
      break;
    }
    case 8:
    {
      if (bathroomDoor.doorObject.Animating)
        break;
      
      cQueenie.UnlockView();
      cQueenie.ChangeRoom(6, 85, 138);
      cQueenie.Clickable = true;
      bathroomDoor.doorObject.Clickable = true;
      moveQueenieToBathroomState = 0;
      queenieAnimationCycle++;
      queenieLoopsRemainingInBathroom = GetGameSpeed() * RandomBetween(MIN_QUEENIE_SECS_IN_BATHROOM, MAX_QUEENIE_SECS_IN_BATHROOM);
      break;
    }
  }
}

int moveQueenieToChairState = 0;
function moveQueenieToChair()
{
  switch (moveQueenieToChairState)
  {
    // open door
    case 0:
    {
      bathroomDoor.doorObject.Clickable = false;
      cQueenie.Clickable = false;
      cQueenie.ChangeView(QUEENIENORMAL_1151);
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eNoBlock, eForwards);
      moveQueenieToChairState++;
      break;
    }
    // walk out of door
    case 1:
    {
      if (bathroomDoor.doorObject.Animating)
        break;
      
      cQueenie.Walk(bathroomDoor.approachX, bathroomDoor.approachY, eNoBlock, eAnywhere);
      moveQueenieToChairState++;
      break;
    }
    // close door
    case 2:
    {
      if (cQueenie.Moving)
        break;
      
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eNoBlock, eBackwards);
      moveQueenieToChairState++;
      break;
    }
    // setup
    case 3:
    {
      if (bathroomDoor.doorObject.Animating)
        break;
      
      bathroomDoor.doorObject.Clickable = true;
      cQueenie.Clickable = true;
      cQueenie.SetIdleView(-1, 0);
      moveQueenieToChairState++;
      break;
    }
    // walk to front of chair
    case 4:
    {
      if (!cQueenie.ResumableWalk(oChair7.X + 35, oChair7.Y - 9))
        break;
      moveQueenieToChairState++;
      break;
    }
    // walk to chair
    case 5:
    {
      if (!cQueenie.ResumableWalk(cQueenie.x, oChair7.Y - 1, eNoBlock, eAnywhere))
        break;
      moveQueenieToChairState++;
      break;
    }
    // sit in chair
    case 6:
    {
      if (cQueenie.Moving) 
        break;
      
      cQueenie.Sitting(true);
      cQueenie.Clickable = false;
      cQueenie.x -= 10;
      cQueenie.LockView(QUEENIERANDOMSIT);
      cQueenie.Animate(2, 9, eOnce, eNoBlock, eBackwards);
      moveQueenieToChairState++;
      break;
    }
    // setup
    case 7:
    {
      if (cQueenie.Animating)
        break;

      cQueenie.Clickable = true;
      cQueenie.UnlockView();
      cQueenie.ChangeView(QUEENIENORMALSIT);
      cQueenie.SetIdleView(QUEENIEIDLESIT_1153, RandomBetween(MIN_QUEENIE_IDLE_DELAY_SEC, MAX_QUEENIE_IDLE_DELAY_SEC));
      cQueenie.Direction(eDirectionRight);
      moveQueenieToChairState = 0;
      queenieAnimationCycle = 0;
      oQueenieSmoke.Visible = true;
      oQueenieSmoke.Animate(4, 9, eRepeat, eNoBlock, eForwards);
      break;
    }
  }
}

function idleQueenieAtChair()
{                                  
  switch (idleQueenieAtChairState)
  {
    case 0:
    {
      if (cQueenie.Animating)
        break;
        
      idleQueenieAtChairState = -1;
      cQueenie.Direction(eDirectionRight);
      cQueenie.UnlockView();
      oQueenieSmoke.Visible = true;
      oQueenieSmoke.Animate(4, 9, eRepeat, eNoBlock, eForwards);
      break;
    }
    case 1:
    {
      if (cQueenie.Frame != 0)
        break;
        
      if (oQueenieSmoke.Frame != 0)
        break;
      
      queenieIdleAnimationsRemaining -= 1;
      cQueenie.LockView(QUEENIERANDOMSIT);
      cQueenie.Animate(0, 9, eOnce, eNoBlock, eForwards);
      oQueenieSmoke.Visible = false;
      idleQueenieAtChairState = 0;
      break;
    }
    default:
    {
      if (cQueenie.IdleView != -1)
      {
        int lastIdleFrame = Game.GetFrameCountForLoop(cQueenie.IdleView, cQueenie.Loop) - 1;
        if (cQueenie.Frame != lastIdleFrame)
          break;
        else
          cQueenie.SetIdleView(-1, 0);
      }
       
      if (oQueenieSmoke.Frame != 0)
        break;
      
      cQueenie.SetIdleView(QUEENIEIDLESIT_1153, RandomBetween(MIN_QUEENIE_IDLE_DELAY_SEC, MAX_QUEENIE_IDLE_DELAY_SEC));
      queenieIdleAnimationsRemaining -= 1;
      if (queenieIdleAnimationsRemaining > 0)
      {
        idleQueenieAtChairState = RandomBetween(0, 1);
        break;
      }
      
      queenieIdleAnimationsRemaining = RandomBetween(MIN_QUEENIE_IDLE_ANIMATIONS, MAX_QUEENIE_IDLE_ANIMATIONS);
      if (!cQueenie.GetProperty("haveSpokenTo"))
      {
        idleQueenieAtChairState = 1;
        break;
      }
      
      idleQueenieAtChairState = -1;
      queenieAnimationCycle++;
      break;
    }
  }
}

function idleQueenieAtBathroom()
{
  if (queenieLoopsRemainingInBathroom > 0)
  {
    queenieLoopsRemainingInBathroom -= 1;
    return;
  }

  cQueenie.ChangeRoom(player.Room, 30, 142);
  cQueenie.SetIdleView(QUEENIENORMAL_1151, RandomBetween(MIN_QUEENIE_IDLE_DELAY_SEC, MAX_QUEENIE_IDLE_DELAY_SEC));
  cQueenie.UnlockView();
  queenieAnimationCycle++;
}

function handleQueenie()
{
  switch (queenieAnimationCycle)
  {
    case 0:
    {
      idleQueenieAtChair();
      break;
    }
    case 1:
    {
      moveQueenieToBathroom();
      break;
    }
    case 2:
    {
      idleQueenieAtBathroom();
      break;
    }
    case 3:
    {
      moveQueenieToChair();
      break;
    }
  }
}

function hWasteBasket_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("A small metal waste basket sat on the cabin floor near the lavatory, its open top revealing crumpled papers within.");
}

function hWasteBasket_Interact(Hotspot *theHotspot, CursorMode mode)
{
  Point* hPoint = theHotspot.Position();
  if (cEgo.CancelableWalk(hPoint.x, hPoint.y, eDirectionLeft))
  {
    cEgo.Direction(eDirectionLeft);
    cEgo.LockViewAligned(CLARAMAINCABIN_1009, 0, eAlignRight);
    cEgo.Animate(0, 9, eOnce, eBlock);
    
    if (cEgo.HasInventory(iMagazine))
    {
      ViewFrame* airplaceTrashEmpty = Game.GetViewFrame(AIRPLANEDETAILS_5001, 1, 1);
      oInsideWastebasket.Graphic = airplaceTrashEmpty.Graphic;
    }
    else
    {
      ViewFrame* airplaceTrashFull = Game.GetViewFrame(AIRPLANEDETAILS_5001, 1, 0);
      oInsideWastebasket.Graphic = airplaceTrashFull.Graphic;
    }
    
    oInsideWastebasket.Baseline = 200;
    oInsideWastebasket.Visible = true;
  }
}

function hWasteBasket_UseInv(Hotspot *theHotspot, CursorMode mode)
{
  if (player.ActiveInventory == iMatchbox)
  {
    if (oWastebasketBurning.Animating)
      return;
    Point* hPoint = theHotspot.Position();
    if (!cEgo.CancelableWalk(hPoint.x + 30, hPoint.y, eDirectionDown))
      return;
    
    SetWalkBehindBase(3, 0);
    cEgo.LockViewFrame(CLARAMAINCABIN_1009, 4, 0);
    cEgo.LockViewOffset(CLARAMAINCABIN_1009, -23, 2);
    cEgo.Animate(4, 9, eOnce, eBlock, eForwards);
    cEgo.y -= 2;
    cEgo.x += 2;
    cEgo.UnlockView();
    theHotspot.Interactable(false);
    SetWalkBehindBase(3, 157);
    oWastebasketBurning.SetView(CLARAMAINCABIN_1009, 5, 0);
    oWastebasketBurning.Visible = true;
    oWastebasketBurning.Animate(5, 9, eRepeat, eNoBlock, eForwards);
    
    /*
    cSteward.x = oChair3.X;
    cSteward.y = oChair3.Y - 10;
    cSteward.Walk(cSteward.x - 40, cSteward.y, eBlock, eWalkableAreas);
    cSteward.FaceCharacter(cEgo, eBlock);
    cSteward.Say("Oh no. There is a fire. Everyone, please return to your seats for the rest of the flight");
    RestoreGameSlot(99);
    */
  }
}

function oInsideWastebasket_Handler()
{
  if (hasVisitedgMagazinePage2 == false)
  {
    Display("You have no reason to be digging through the trash.");
    return;
  }
  
  if (cEgo.HasInventory(iMagazine))
  {
    Display("Clara ruffles through the remaining wads of paper and realizes they are all identical.");
  }
  else
  {
    cEgo.Animate(1, 9, eOnce, eBlock);
    Display("Clara peers into the Waste Basket, spotting several crumpled wads of paper piled haphazardly inside. Curious, she reaches in and places it in her pocket.");
    cEgo.AddInventory(iCrumpledMagazine);
    ViewFrame* airplaceTrashEmpty = Game.GetViewFrame(AIRPLANEDETAILS_5001, 1, 1);
    oInsideWastebasket.Graphic = airplaceTrashEmpty.Graphic;
    cEgo.Animate(1, 9, eOnce, eBlock, eBackwards);
  }
}

function oInsideWastebasket_Interact(Object *theObject, CursorMode mode)
{
  oInsideWastebasket_Handler();
}

function oInsideWastebasket_Look(Object *theObject, CursorMode mode)
{
  oInsideWastebasket_Handler();
}

function oSeatPocket_Interact(Object *theObject, CursorMode mode)
{
  Display("The seatback pocket holds a glossy magazine and a neatly folded air sickness bag, both ready for use.");
}

function oSeatPocket_Look(Object *theObject, CursorMode mode)
{
  Display("The seatback pocket holds a glossy magazine and a neatly folded air sickness bag, both ready for use.");
}

function oPukeBag_Interact(Object *theObject, CursorMode mode)
{
  Display("You don’t need the air sickness bag right now.");
}

function oPukeBag_Look(Object *theObject, CursorMode mode)
{
  Display("A small, compact air sickness bag, neatly folded and ready to use.");
}

function oMagazine_Interact(Object *theObject, CursorMode mode)
{
  cEgo.LockViewOffset(CLARASIT_1003, -2, 0);
  cEgo.Animate(4, 9, eOnce, eBlock, eForwards);
  oMagazine.Visible = false;
  cEgo.Animate(5, 9, eOnce, eBlock, eForwards);
  cEgo.LockViewOffset(CLARASIT_1003, -2, 0);
  cEgo.Loop = 6;
  
  cEgo.ChangeRoom(4);
}

function oMagazine_Look(Object *theObject, CursorMode mode)
{
  Display("A glossy magazine filled with articles, advertisements, and pictures. Perfect for passing the time.");
}

function oCockpitDoor_Interact(Object *cockPitDoor, CursorMode mode)
{
  oDoor_interact(cockPitDoor, 1, -20, -1, 100, -9, cEgo);
}

function oCockpitDoor_Look(Object *theObject, CursorMode mode)
{
  Display("The cockpit door is sturdy and unremarkable, its polished handle catching the light. Behind it, Clara can hear the faint murmur of activity as the pilot and copilot work to guide the plane safely toward Stonecliff Manor.");
}

function oBathroomDoor_Interact(Object *bathDoor, CursorMode mode)
{ 
  oDoor_interact(bathDoor, 0, 40, -1, -100, -9, cEgo);
}

function oBathroomDoor_Look(Object *theObject, CursorMode mode)
{
  Display("The bathroom door is simple with a small handle. It's clear where it leads—an essential space for passengers during long flights.");
}

function handlePopovers()
{
  if (oInsideWastebasket.Visible)
  {
    mouse.Mode = eModeExit;
    
    if (oInsideWastebasket.IsUnderMouse())
      mouse.Mode = eModeInteract;
      
    return;
  }
  
  if (oInsideCabinet.Visible)
  {
    mouse.Mode = eModeExit;
    
    if (oInsideCabinet.IsUnderMouse())
      mouse.Mode = eModeInteract;
      
    return;
  }
  
  if (oSeatPocket.Visible)
  {
    mouse.Mode = eModeExit;
    
    if (oSeatPocket.IsUnderMouse() || oMagazine.IsUnderMouse() || oPukeBag.IsUnderMouse())
      mouse.Mode = eModeInteract;
      
    return;
  }
}

/*
Overlay *gameInfoOverlay;
function repeatedly_execute_always()
{
  if (gameInfoOverlay != null)
    gameInfoOverlay.Remove();
  
  gameInfoOverlay = Overlay.CreateTextual(50, 150, 120, Game.SpeechFont, 15, String.Format("speed: %d, \nactive timer: %.2f, queenie timer: %.2f, carlos timer: %.2f, joaana timer: %.2f",
    GetGameSpeed(), GetTimeRemainingInSeconds(SET_ACTIVE_CHARACTER_TIMER), GetTimeRemainingInSeconds(cQueenie.ID), GetTimeRemainingInSeconds(cCarlos.ID), GetTimeRemainingInSeconds(cJoanna.ID)));
}
*/

function oChair_Interact(Object *theObject, CursorMode mode)
{
  int xOffset = 37;
  int yOffset = 0;
  
  if (theObject.Y >= 140)
    yOffset--;
    
  if (cEgo.CancelableWalk(theObject.X + xOffset, theObject.Y + yOffset, eDirectionRight))
  {
    cEgo.LockViewAligned(CLARASIT_1003, 3, eAlignRight);
    cEgo.Animate(3, 9, eOnce, eBlock, eBackwards);
    oSeatPocket.X = cEgo.x + 30;
    oSeatPocket.Y = cEgo.y - 20;
    oMagazine.X = cEgo.x + 63;
    oMagazine.Y = cEgo.y - 83;
    oPukeBag.X = cEgo.x + 87;
    oPukeBag.Y = cEgo.y - 84;
    oSeatPocket.Baseline = 199;
    oSeatPocket.Visible = true;
    oMagazine.Visible = true;
    oPukeBag.Visible = true;
  }
}

function oChair_Look(Object *theObject, CursorMode mode)
{
  Display("The airplane seats are neatly arranged in rows, each featuring adjustable armrests and immaculate cushions. A magazine rests in each seat pocket, its cover tempting Clara to take a closer look.");
}

function hCabinet_Interact(Hotspot *theHotspot, CursorMode mode)
{
  if (cSteward.Room == player.Room)
  {
    cEgo.Walk(470, 154, eBlock, eWalkableAreas);
    cEgo.Direction(eDirectionRight);
    cSteward.Say("Do you need something, Ms. Bow?");
    Display("You cannot do anything with that while the steward is in the way");
    return;
  }
  cEgo.Walk(489, 154, eBlock, eWalkableAreas);
  cEgo.Direction(eDirectionRight);
  cEgo.LockViewOffset(CLARAMAINCABIN_1009, 3, 1);
  cEgo.Animate(2, 9, eOnce, eBlock);
  
  if (cEgo.HasInventory(iCigar))
  {
    ViewFrame* cabinetEmpty = Game.GetViewFrame(AIRPLANEDETAILS_5001, 3, 1);
    oInsideCabinet.Graphic = cabinetEmpty.Graphic;
  }
  else
  {
    ViewFrame* cabinetFull = Game.GetViewFrame(AIRPLANEDETAILS_5001, 3, 0);
    oInsideCabinet.Graphic = cabinetFull.Graphic;
  }
  
  oInsideCabinet.Baseline = 200;
  oInsideCabinet.X = 304;
  oInsideCabinet.Y = 138;
  oInsideCabinet.Visible = true;
}

function hCabinet_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("The minibar is a compact, unassuming cabinet, seamlessly blending into the cabin's refined decor. Its contents are neatly tucked away behind closed doors.");
}

function oInsideCabinet_Interact(Object *theObject, CursorMode mode)
{
  if (cEgo.HasInventory(iCigar))
  {
    Display("There’s nothing else of interest left in the cabinet.");    
    return;
  }

  Display("You take the cigar.");
  Display("You snag a pack of matches, just to be safe.");
  cEgo.AddInventory(iCigar);
  cEgo.AddInventory(iMatchbox);
  ViewFrame* cabinetEmpty = Game.GetViewFrame(AIRPLANEDETAILS_5001, 3, 1);
  oInsideCabinet.Graphic = cabinetEmpty.Graphic;
}

function oInsideCabinet_Look(Object *theObject, CursorMode mode)
{
  if (!cEgo.HasInventory(iCigar))
  {
    Display("The cabinet is well-organized, holding a variety of items, but the cigar and matchbox catch your attention.");
    return RUN_DIALOG_STOP_DIALOG;
  }
  
  Display("The cabinet is well-organized, holding a variety of items, but there’s nothing else of interest left in the cabinet.");
}

function oCarlosGlass_Look(Object *theObject, CursorMode mode)
{
  cEgo.Walk(theObject.X, theObject.Y, eBlock, eWalkableAreas);
  cEgo.FaceObject(theObject);
  Display("Clara examines Carlos's glass and spots a faint fingerprint on the side, left behind in the condensation. It's an easy clue to overlook, but not for her sharp eye.");
}

function oCarlosGlass_Interact(Object *theObject, CursorMode mode)
{
  cEgo.Walk(theObject.X, theObject.Y, eBlock, eWalkableAreas);
  cEgo.FaceObject(theObject);
  Display("Clara examines Carlos's glass and spots a faint fingerprint on the side, left behind in the condensation. It's an easy clue to overlook, but not for her sharp eye.");
}

function hTable_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("The sleek, curved couch exudes luxury, a far cry from anything found on a typical passenger flight. Its pristine upholstery is unsurprising—nothing less would be expected from Xavier Montague Stone's private jet.");
}

function hCabinCouch_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("The sleek, curved couch exudes luxury, a far cry from anything found on a typical passenger flight. Its pristine upholstery is unsurprising—nothing less would be expected from Xavier Montague Stone's private jet.");
}

function oEmergencyDoor_Look(Object *theObject, CursorMode mode)
{
  Display("The emergency door looms as a stark reminder of the fragility of air travel. Clara has no intention of opening it… unless, of course, there's an emergency or perhaps curiosity to quench.");
}

function oEmergencyDoor_Interact(Object *theObject, CursorMode mode)
{
  if (cEgo.CancelableWalk(theObject.X + 20, theObject.Y, eDirectionRight))
  {
    oEmergencyDoor.Visible = false;
    SetWalkBehindBase(8, 140);
    oChair2.Baseline = 140;
    cEgo.LockViewOffset(CLARAFALL_1004, 24, 0);
    Display("As Clara opens the emergency door, a deafening roar fills the cabin. The sudden rush of wind pulls her off balance, and before she can react, she's yanked out into the open sky, plummeting toward the ground below.");
    cEgo.Animate(5, 9, eOnce, eBlock, eForwards);
    cEgo.UnlockView();
    cEgo.ChangeRoom(301);
  }  
}

function hCabinWindows_Look(Hotspot *theHotspot, CursorMode mode)
{
  Display("The windows offer a picturesque view of the sky, with soft clouds rolling past like a living painting. The sunlight streams through, giving the cabin a warm, serene glow. It's a peaceful moment, but Clara's investigative instincts remind her to stay alert.");
}

function oCabinetBottlesGlasses_Interact(Object *theObject, CursorMode mode)
{
  if (cSteward.Room == player.Room)
  {
    cEgo.Walk(470, 154, eBlock, eWalkableAreas);
    cEgo.Direction(eDirectionRight);
    cSteward.Say("Do you need something, Ms. Bow?");
    Display("You cannot do anything with that while the steward is in the way");
    return;
  }
 
  cEgo.Walk(490, 154, eBlock, eWalkableAreas);
  cEgo.Direction(eDirectionRight);
  cEgo.LockViewOffset(CLARAMAINCABIN_1009, 4, 0);
  oCabinetBottles.Visible = false;
  oCabinetGlass1.Visible = false;
  oCabinetGlass2.Visible = false;
  cEgo.Animate(3, 9, eOnce, eBlock);
  Display("Ahhh....");
  oCabinetBottles.Visible = true;
  oCabinetGlass1.Visible = true;
  oCabinetGlass2.Visible = true;
  cEgo.UnlockView();
  cEgo.Direction(eDirectionRight);
  numberDrinks++;
  
  if (numberDrinks > 1)
  {
    cEgo.Walk(cEgo.x - 15, cEgo.y - 8, eBlock, eWalkableAreas);
    cEgo.ChangeView(CLARADRUNKWALK);
    cEgo.Say("I dunn feeel sho well...");
    cEgo.Walk(185, 155, eBlock, eAnywhere);
    cEgo.LockView(CLARADRUNKMISC);
    cEgo.Animate(0, 9, eOnce, eBlock, eForwards);
    cSteward.Say("Ladies and gentlemen please return to your seats.");
    cSteward.Say("We are now approaching Stonecliff Manor.");
    Display("Good detective work, Clara");
    RestartGame();
  }
}

function oCabinetBottlesGlasses_Look(Object *theObject, CursorMode mode)
{
  Display("A row of neatly arranged liquor bottles and matching glasses sit on top of the minibar, ready for a quick pour.");
}

function room_LeaveRight()
{
  cEgo.ChangeRoom(5);
}

function room_LeaveLeft()
{
  cEgo.ChangeRoom(6);
}

function room_FirstLoad()
{
  /*
  Display("Welcome to this adventure!");
  Display("Before you get started, here are some important tips for you.");
  Display("Your mouse clicks will serve 2 main functions.");
  Display("The left click will interact with the world.");
  Display("The right click will look at items and give you helpful information.");
  Display("The FBI has given you a notebook that contains a list of guests.");
  Display("The notebook will also be used to track important information you discover.");
  Display("Don't worry, this information will be recorded automatically for you.");
  Display("Be on the look out for other useful detective tools.");
  //Display("Items in your notebook will change visually based on actions you need to take or have taken.");
  //Display("Simply put, colored items indicate information you have yet to discover, greyed items have not futher information to discover, and shadows items don't have enough know to gather information.");
  Display("Have fun on your adventure!");
  */
}

function loadClara()
{
  //cEgo.TurnOnDetails();
  switch(player.PreviousRoom)
  {
    case 5:
      cEgo.x = cockpitDoor.doorObject.X + 60;
      cEgo.y = cockpitDoor.doorObject.Y - 10;
      break;
    case 6:
      cEgo.x = bathroomDoor.doorObject.X - 60;
      cEgo.y = bathroomDoor.doorObject.Y - 10;
      break;
    default:
      break;
  }
}

function loadCarlos()
{
  if (player.PreviousRoom == 19)
    return;
    
  cCarlos.ChangeRoom(player.Room);
  
  idleCarlosAtBackState = -1;
  idleCarlosAtTableState = -1;
  moveCarlosToBackState = 0;
  moveCarlosToTableState = 0;
  
  carlosIdleAnimationsRemaining = RandomBetween(MIN_CARLOS_IDLE_ANIMATIONS, MAX_CARLOS_IDLE_ANIMATIONS);
  cCarlos.IdleAnimationDelay = GetGameSpeed() / 8;
  cCarlos.Clickable = true;
  cCarlos.UnlockView();
  
  oCarlosGlass.SetPosition(oChair8.X + 114, oChair8.Y - 24);
  oCarlosGlass.Baseline = 128;
  
  // place in chair
  if (carlosAnimationCycle == 0 || carlosAnimationCycle == 3)
  {
    cCarlos.x = oChair8.X + 130;
    cCarlos.y = oChair8.Y;
    carlosAnimationCycle = 0;
    
    cCarlos.ChangeView(CARLOSNORMALSIT);
    cCarlos.SetIdleView(CARLOSIDLESIT, RandomBetween(MIN_CARLOS_IDLE_DELAY_SEC, MAX_CARLOS_IDLE_DELAY_SEC));
    cCarlos.Direction(eDirectionLeft);
    cCarlos.Sitting(true);
    
    oCarlosGlass.Visible = false;
  }
  // place in back
  else
  {
    cCarlos.x = 105;
    cCarlos.y = 135;
    carlosAnimationCycle = 2;
    cCarlos.Sitting(false);
    
    if (cCarlos.HasInventory(iCigar))
    {
      cCarlos.ChangeView(CARLOSNORMALSMOKING);
      cCarlos.SetIdleView(CARLOSIDLESMOKING, RandomBetween(MIN_CARLOS_IDLE_DELAY_SEC, MAX_CARLOS_IDLE_DELAY_SEC));
      cCarlos.Direction(eDirectionDown);
    }
    else
    {
      cCarlos.ChangeView(CARLOSNORMAL_1201);
      cCarlos.SetIdleView(-1, 0);
      cCarlos.Direction(eDirectionUp);
    }
    
    oCarlosGlass.Visible = true;
  }
}

function loadJoanna()
{
  if (player.PreviousRoom == 19)
    return;
    
  idleJoannaAtChairState = -1;
  idleJoannaAtTableState = -1;
  moveJoannaToChairState = 0;
  moveJoannaToTableState = 0;
  
  joannaIdleAnimationsRemaining = RandomBetween(MIN_JOANNA_IDLE_ANIMATIONS, MAX_JOANNA_IDLE_ANIMATIONS);
  cJoanna.Clickable = true;
  cJoanna.UnlockView();
  cJoanna.IdleAnimationDelay = GetGameSpeed() / 2;
  cJoanna.ChangeRoom(player.Room);
  cJoanna.Sitting(true);
  
  oJoannaMagazine.SetPosition(oChair4.X + 10, oChair4.Y - 2);
  
  // place in chair
  if (joannaAnimationCycle == 0 || joannaAnimationCycle == 3)
  {
    cJoanna.x = oChair4.X + 25;
    cJoanna.y = oChair4.Y - 2;
    joannaAnimationCycle = 0;
    
    cJoanna.ChangeView(JOANNANORMALSITMAGAZINE);
    cJoanna.SetIdleView(JOANNAIDLESITMAGAZINE, RandomBetween(MIN_JOANNA_IDLE_DELAY_SEC, MAX_JOANNA_IDLE_DELAY_SEC));
    cJoanna.Direction(eDirectionRight);
    
    oJoannaMagazine.Visible = false;
  }
  // place at table
  else
  {
    cJoanna.x = oChair8.X + 85;
    cJoanna.y = oChair8.Y - 5;
    joannaAnimationCycle = 2;

    cJoanna.ChangeView(JOANNANORMALSITSOLO);
    cJoanna.SetIdleView(JOANNAIDLESITSOLO, RandomBetween(MIN_JOANNA_IDLE_DELAY_SEC, MAX_JOANNA_IDLE_DELAY_SEC));
    cJoanna.Direction(eDirectionRight);
    
    oJoannaMagazine.Visible = true;
  }
}

function loadQueenie()
{
  switch(player.PreviousRoom)
  {
    case 19:
      return;
    case 5:
      idleQueenieAtChairState = -1;
      moveQueenieToChairState = 0;
      moveQueenieToBathroomState = 0;
      queenieIdleAnimationsRemaining = RandomBetween(MIN_QUEENIE_IDLE_ANIMATIONS, MAX_QUEENIE_IDLE_ANIMATIONS);
      queenieLoopsRemainingInBathroom = GetGameSpeed() * RandomBetween(MIN_QUEENIE_SECS_IN_BATHROOM, MAX_QUEENIE_SECS_IN_BATHROOM);
      
      cQueenie.Direction(eDirectionRight);
      
      // place in bathroom
      if (queenieAnimationCycle == 1 || queenieAnimationCycle == 2)
      {
        queenieAnimationCycle = 2;
        
        cQueenie.ChangeRoom(6);
        cQueenie.ChangeView(QUEENIENORMAL_1151);
        cQueenie.SetIdleView(-1, 0);
        cQueenie.Sitting(false);
      }
      // place in chair
      else
      {
        cQueenie.x = oChair7.X + 25;
        cQueenie.y = oChair7.Y - 1;
        queenieAnimationCycle = 0;
        
        cQueenie.ChangeView(QUEENIENORMALSIT);
        cQueenie.SetIdleView(QUEENIEIDLESIT_1153, RandomBetween(MIN_QUEENIE_IDLE_DELAY_SEC, MAX_QUEENIE_IDLE_DELAY_SEC));
        cQueenie.Sitting(true);
      }
      
      break;
    case 6:
      idleQueenieAtChairState = -1;
      moveQueenieToChairState = 0;
      moveQueenieToBathroomState = 0;
      queenieIdleAnimationsRemaining = RandomBetween(MIN_QUEENIE_IDLE_ANIMATIONS, MAX_QUEENIE_IDLE_ANIMATIONS);
      queenieLoopsRemainingInBathroom = GetGameSpeed() * RandomBetween(MIN_QUEENIE_SECS_IN_BATHROOM, MAX_QUEENIE_SECS_IN_BATHROOM);
      
      cQueenie.Direction(eDirectionRight);
      
      // place in bathroom
      if (cQueenie.Room != player.Room)
      {
        queenieAnimationCycle = 2;
        
        cQueenie.ChangeRoom(6);
        cQueenie.ChangeView(QUEENIENORMAL_1151);
        cQueenie.SetIdleView(-1, 0);
        cQueenie.Sitting(false);
        oQueenieSmoke.Visible = false;
      }
      // place in chair
      else
      {
        cQueenie.x = oChair7.X + 25;
        cQueenie.y = oChair7.Y - 1;
        queenieAnimationCycle = 0;
        
        cQueenie.ChangeView(QUEENIENORMALSIT);
        cQueenie.SetIdleView(QUEENIEIDLESIT_1153, RandomBetween(MIN_QUEENIE_IDLE_DELAY_SEC, MAX_QUEENIE_IDLE_DELAY_SEC));
        cQueenie.Sitting(true);
        oQueenieSmoke.Visible = true;
      }
      
      break;
    default:
      cQueenie.ChangeRoom(player.Room, oChair7.X + 25, oChair7.Y - 1);
      cQueenie.ChangeView(QUEENIENORMALSIT);
      cQueenie.SetIdleView(QUEENIEIDLESIT_1153, RandomBetween(MIN_QUEENIE_IDLE_DELAY_SEC, MAX_QUEENIE_IDLE_DELAY_SEC));
      cQueenie.Direction(eDirectionRight);
      cQueenie.IdleAnimationDelay = GetGameSpeed() / 2;
      cQueenie.Sitting(true);
      
      queenieAnimationCycle = 0;
      queenieIdleAnimationsRemaining = RandomBetween(MIN_QUEENIE_IDLE_ANIMATIONS, MAX_QUEENIE_IDLE_ANIMATIONS);
      queenieLoopsRemainingInBathroom = GetGameSpeed() * RandomBetween(MIN_QUEENIE_SECS_IN_BATHROOM, MAX_QUEENIE_SECS_IN_BATHROOM);
      
      idleQueenieAtChairState = -1;
      moveQueenieToChairState = 0;
      moveQueenieToBathroomState = 0;
      
      oQueenieSmoke.X = cQueenie.x - 17;
      oQueenieSmoke.Y = cQueenie.y;
      oQueenieSmoke.SetView(QUEENIERANDOMSIT, 4, 0);
      oQueenieSmoke.Visible = true;
      break;
  }
}

function loadSteward()
{
  cSteward.x = 483;
  cSteward.y = 157;
  cSteward.Direction(eDirectionLeft);
}

function loadDoors()
{
  oBathroomDoor.SetView(AIRPLANEDOORS_5003);
  oCockpitDoor.SetView(AIRPLANEDOORS_5003);
  oCockpitDoor.Graphic = 1069;
  bathroomDoor.Init(oBathroomDoor, 35, 0, -20, -2);
  cockpitDoor.Init(oCockpitDoor, -25, 0, 30, -2);
  
  bathroomDoor.doorObject.Clickable = true;
  cockpitDoor.doorObject.Clickable = true;
}

function loadBlinds()
{
  oBlinds0.SetView(AIRPLANEMAINCABINDETAILS, 2, 0);
  oBlinds1.SetView(AIRPLANEMAINCABINDETAILS, 2, 0);
  oBlinds2.SetView(AIRPLANEMAINCABINDETAILS, 2, 0);
  oBlinds3.SetView(AIRPLANEMAINCABINDETAILS, 2, 0);
  oBlinds4.SetView(AIRPLANEMAINCABINDETAILS, 2, 0);
  oBlinds5.SetView(AIRPLANEMAINCABINDETAILS, 2, 0);
  oBlinds6.SetView(AIRPLANEMAINCABINDETAILS, 2, 0);
  oBlinds7.SetView(AIRPLANEMAINCABINDETAILS, 2, 0);
  oBlinds8.SetView(AIRPLANEMAINCABINDETAILS, 2, 0);
  oBlinds9.SetView(AIRPLANEMAINCABINDETAILS, 2, 0);
}

function loadObjects()
{
  loadDoors();
  loadBlinds();
}

AudioChannel *musicChannel;
AudioChannel *ambientChannel;
function room_Load()
{
  gIconbar.Visible = true;
  /*
  if (musicChannel == null || !musicChannel.IsPlaying)
    musicChannel = a6001_AirplaneMusic.Play(eAudioPriorityHigh, eRepeat);
  if (ambientChannel == null || !ambientChannel.IsPlaying)
    ambientChannel = aAirplane_hum.Play(eAudioPriorityHigh, eRepeat);
  */
  
  loadObjects();
  loadClara();
  loadCarlos();
  loadJoanna();
  loadQueenie();
  loadSteward();
}

function loadQueenieAfterFadeIn()
{
  if (!oQueenieSmoke.Animating)
    oQueenieSmoke.Animate(4, 9, eRepeat, eNoBlock, eForwards);
}

function room_AfterFadeIn()
{
  //System.Log(eLogInfo, String.Format("room3 -- room_AfterFadeIn"));
  
  oClouds.SetView(AIRPLANECLOUDS_5002, 2, 0);
  oClouds.Animate(2, 13, eRepeat, eNoBlock, eForwards);
  loadQueenieAfterFadeIn();

  switch(player.PreviousRoom)
  {
    case 4:
      cEgo.Animate(5, 8, eOnce, eBlock, eBackwards);
      oMagazine.Visible = true;
      cEgo.Animate(4, 8, eOnce, eBlock, eBackwards);
      break;
    case 5:
      cockpitDoor.doorObject.SetView(AIRPLANEDOORS_5003);
      cockpitDoor.doorObject.Animate(1, 9, eOnce, eBlock, eForwards);
      cEgo.Walk(cEgo.x - 80, cEgo.y + 9, eBlock, eAnywhere);
      cockpitDoor.doorObject.Animate(1, 9, eOnce, eBlock, eBackwards);
      break;
    case 6:
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eBlock, eForwards);
      cEgo.Walk(cEgo.x + 100, cEgo.y + 9, eBlock, eAnywhere);
      bathroomDoor.doorObject.Animate(0, 9, eOnce, eBlock, eBackwards);
      if (threadTied)
      {
        cEgo.Walk(cEgo.x + 20, cEgo.y - 10, eBlock, eWalkableAreas);
        cEgo.Direction(eDirectionUp);
        Display("*ding*");
        cSteward.x = 320;
        cSteward.y = 143;
        cSteward.Walk(bathroomDoor.approachX + 100, bathroomDoor.approachY, eBlock, eWalkableAreas);
        cSteward.Walk(bathroomDoor.approachX - 15, bathroomDoor.approachY, eBlock, eAnywhere);
        cSteward.LockView(STEWARDRANDOM);
        cSteward.Animate(0, 9, eOnce, eBlock, eForwards);
        bathroomDoor.doorObject.Animate(0, 9, eOnce, eBlock, eForwards);
        cSteward.UnlockView();
        cSteward.Walk(cSteward.x - 50, cSteward.y - 2, eBlock, eAnywhere);
        bathroomDoor.doorObject.Animate(0, 9, eOnce, eBlock, eBackwards);
        cSteward.ChangeRoom(6, 0, 0);
        threadTied = false;
      }
      break;
    default:
      break;
  }
  
  if (activeDialog != null)
  {
    activeDialog.Start();
  }
}

function on_mouse_click(MouseButton button)
{
  if (button == eMouseLeft && Mouse.Mode == eModeExit)
  {
    if (oInsideCabinet.Visible)
    {
      oInsideCabinet.Visible = false;
      cEgo.Animate(2, 9, eOnce, eBlock, eBackwards);
      cEgo.UnlockView();
      cEgo.Direction(eDirectionRight);
      
      return;
    }
    
    if (oInsideWastebasket.Visible)
    {
      cEgo.Animate(0, 9, eOnce, eBlock, eBackwards);
      cEgo.UnlockView();
      cEgo.Direction(eDirectionLeft);
      oInsideWastebasket.Visible = false;
      
      return;
    }
    
    if (oSeatPocket.Visible)
    {
      cEgo.Animate(3, 9, eOnce, eBlock, eForwards);
      cEgo.UnlockView();
      cEgo.Direction(eDirectionRight);
      oSeatPocket.Visible = false;
      oMagazine.Visible = false;
      oPukeBag.Visible = false;
      
      return;
    }
  }

  if (!IsGamePaused())
  {
    if (button == eMouseRight && invCursorToggle)
    {
      return;
    }
    
    if ((button == eMouseRight) && (Mouse.IsOverWalkableArea() || Mouse.IsOverNothing()) && !(Mouse.IsOverHotspot()))
    {
      mouse.Mode = eModeLookat;
      Display("The cabin radiates understated luxury, with polished wood accents and the gentle hum of the engines setting a tranquil tone. Rows of seats align with windows that reveal a pastel sky, while a lounge area, complete with a plush couch and sleek table, makes it clear this is no ordinary flight.");
      choose_cursor();
      ClaimEvent();
    }
  }
}

int burningWastebasketAnimationsRemaining;
int burningWastebasketLastFrame;
bool decrementHandled; // Tracks if the decrement for this loop has been handled
function handleBurningWastebasket() {
  if (!oWastebasketBurning.Animating)
    return;

  if (burningWastebasketAnimationsRemaining == 0) {
    burningWastebasketLastFrame = Game.GetFrameCountForLoop(CLARAMAINCABIN_1009, 5) - 1;
    burningWastebasketAnimationsRemaining = 3;
    decrementHandled = false; // Reset the state tracker
    return;
  }

  // Check if we're on the last frame and decrement only once per loop
  if (oWastebasketBurning.Frame == burningWastebasketLastFrame) {
    if (!decrementHandled) { // Ensure we decrement only once
      burningWastebasketAnimationsRemaining--;
      decrementHandled = true; // Mark that we've handled this frame
    }
  } else {
    decrementHandled = false; // Reset the state when frame changes
  }

  if (burningWastebasketAnimationsRemaining == 0) {
    oWastebasketBurning.StopAnimating();
    oWastebasketBurning.Visible = false;
    hWasteBasket.Interactable(true);
    return;
  }
}

function repeatedly_execute_always()
{ 
  handleCarlos();
  handleJoanna();
  handleQueenie();
  handleBurningWastebasket();
}

function repeatedly_execute()
{
  Object *currentMouseOverObject = Mouse.GetOverObject();
  if (currentMouseOverObject == oEmergencyDoor)
  {
    SetRestartPoint();
  }
  Hotspot *currentMouseOverHotspot = Mouse.GetOverHotspot();
  if (currentMouseOverHotspot == hCabinet)
  {
    SetRestartPoint();
  }
  
  handlePopovers();
}

function closeBlinds(Object *theBlinds)
{
  theBlinds.Animate(1, 4, eOnce, eBlock, eForwards);
  theBlinds.SetView(AIRPLANEMAINCABINDETAILS, 2, 0);
}

function openBlinds(Object *theBlinds)
{
  theBlinds.Animate(2, 4, eOnce, eBlock, eForwards);
  theBlinds.SetView(AIRPLANEMAINCABINDETAILS, 1, 0);
}

function oBlinds_Interact(Object *theObject, CursorMode mode)
{
  ViewFrame* closedBlinds = Game.GetViewFrame(AIRPLANEMAINCABINDETAILS, 2, 0);
  ViewFrame* openedBlinds = Game.GetViewFrame(AIRPLANEMAINCABINDETAILS, 1, 0);
  
  if (theObject.Loop == closedBlinds.Loop)
  {
    openBlinds(theObject);
  }
  else
  {
    closeBlinds(theObject);
  }
}

function room_Unload()
{
  System.Log(eLogInfo, "2");
  doLog = true;
}

function room_Leave()
{
  System.Log(eLogInfo, "1");
}
