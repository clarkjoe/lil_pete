#define HANDLE_TIMER 5
#define HANDLE_W_THREAD_TIMER 6

function loadClara()
{
  cEgo.x = 163;
  cEgo.y = 165;
  cEgo.FaceDirection(eDirectionUp);
  //cEgo.AddInventory(iGreenThread);
}

function loadCarlos()
{
  //cCarlos.ChangeRoom(player.Room, 85, 138);
  //cCarlos.LockView(CARLOSBATHROOM_1204);
}

function loadJoanna()
{
  //cJoanna.ChangeRoom(player.Room, 123, 131);
  //cJoanna.LockView(JOANNABATHROOM_1104);
  //cJoanna.Animate(0, 49, eRepeat, eNoBlock, eForwards);
}

function loadQueenie()
{
  cQueenie.ChangeRoom(player.Room, 92, 124);
}

function loadObjects()
{
  oHandle.Graphic = BATHROOM_HANDLE_SIDE;
  oHandle.Visible = true;
  
  oCord.Graphic = BATHROOM_CORD;
  oCord.Visible = true;
  
  oCordWithThread.Graphic = BATHROOM_CORD_W_THREAD;
  oCordWithThread.Visible = false;
  
  oCordWithThreadLoose.Graphic = BATHROOM_CORD_W_THREAD_HANDLE_LOOSE;
  oCordWithThreadLoose.Visible = false;
  oCordWithThreadLoose.Baseline = 120;
  
  oCordWithThreadTaut.Graphic = BATHROOM_CORD_W_THREAD_HANDLE_TAUT;
  oCordWithThreadTaut.Visible = false;
  oCordWithThreadTaut.Baseline = 120;
  
  oSign.Graphic = BATHROOM_SIGN_OFF;
  oSign.Visible = true;
}

function room_Load()
{  
  loadClara();
  loadCarlos();
  loadJoanna();
  loadQueenie();
  
  loadObjects();
}

function room_AfterFadeIn()
{
  if (cCarlos.Room == player.Room)
  {
    cCarlos.Animate(1, 9, eOnce, eBlock, eForwards);
    cCarlos.Say("What are you doing in here, toots?");
    cEgo.Say("How ghastly!");
    cEgo.Walk(cEgo.x, cEgo.y + 100, eBlock, eAnywhere);
  }
  
  if (cJoanna.Room == player.Room)
  {
    Wait(100);
    cJoanna.StopMoving();
    cJoanna.Animate(1, 9, eOnce, eBlock, eForwards);
    cJoanna.Say("Please leave.");
    cEgo.Say("Okay.");
    cEgo.Walk(cEgo.x, cEgo.y + 100, eBlock, eAnywhere);
  }
}

function on_mouse_click(MouseButton button)
{
  if (!IsGamePaused())
  {
    if (button == eMouseRight && invCursorToggle)
    {
      return;
    }
    
    if ((button == eMouseRight) && (Mouse.IsOverWalkableArea() || Mouse.IsOverNothing()) && !(Mouse.IsOverHotspot()))
    {
      dPrologue_BathroomDesc.Start();
      ClaimEvent();
    }
  }
}

function room_Unload()
{
  cCarlos.UnlockView();
}

function room_LeaveBottom()
{
  cEgo.ChangeRoom(3);
}

function room_RepExec()
{
  if (IsTimerExpired(HANDLE_TIMER))
  {
    oHandle.SetView(AIRPLANEBATHROOMDETAILS, 0, 2);
    oHandle.Animate(0, 4, eOnce, eBlock, eBackwards);
    oHandle.Graphic = BATHROOM_HANDLE_SIDE;
  }
  
  if (IsTimerExpired(HANDLE_W_THREAD_TIMER))
  {
    oCordWithThreadTaut.SetView(CLARABATHROOMTHREADB_1008);
    oCordWithThreadTaut.Animate(2, 4, eOnce, eBlock, eForwards);
    oCordWithThreadTaut.Graphic = BATHROOM_CORD_W_THREAD_HANDLE_TAUT;
    oCordWithThreadTaut.Visible = false;
    
    oHandle.Visible = true;
    oCord.Visible = true;
    
    oSign.Graphic = BATHROOM_SIGN_ON;
    Wait(100);
    cFlAttend.Say("Everything alright in there?");
    cEgo.Say("Everything is alright.");
    cFlAttend.Say("Okay. I will leave.");
    Wait(100);
    oSign.Graphic = BATHROOM_SIGN_OFF;
    cEgo.UnlockView();
  }
}

int cordOffsetX = 17;
int cordOffsetY = 0;
function oCord_Interact(Object *theObject, CursorMode mode)
{
  cEgo.Walk(oCord.X + 17, oCord.Y, eBlock, eWalkableAreas);
  cEgo.FaceDirection(eDirectionLeft);
  cEgo.LockViewOffset(CLARABATHROOM_1006, -8, 1);
  theObject.Visible = false;
  cEgo.Animate(1, 4, eOnce, eBlock, eForwards);
  oSign.Graphic = BATHROOM_SIGN_ON;
  Wait(100);
  cFlAttend.Say("Everything alright in there?");
  cEgo.Say("Everything is alright.");
  cFlAttend.Say("Okay. I will leave.");
  Wait(100);
  oSign.Graphic = BATHROOM_SIGN_OFF;
  cEgo.UnlockView();
  cEgo.x -= 6;
  cEgo.y += 1;
  theObject.Visible = true;
}

function oHandle_Interact(Object *theObject, CursorMode mode)
{
  if (oHandle.Graphic == BATHROOM_HANDLE_UP)
  {
    Display("Not able to flush your poop at this time");
    return;
  }
  cEgo.Walk(oHandle.X + 12, oHandle.Y, eBlock, eWalkableAreas);
  cEgo.FaceDirection(eDirectionLeft);
  theObject.Visible = false;
  cEgo.LockViewAligned(CLARABATHROOM_1006, 4, eAlignRight);
  cEgo.Animate(4, 6, eOnce, eBlock, eForwards);
  Display("Now flushing your poop");
  SetTimer(HANDLE_TIMER, 100);
  cEgo.UnlockView();
  cEgo.FaceDirection(eDirectionLeft);
  theObject.Graphic = BATHROOM_HANDLE_UP;
  theObject.Visible = true;
}

function oObject0_UseInv(Object *theObject, CursorMode mode)
{
  if (theObject.ID == iGreenThread.ID)
  {
    Display("put it on.");
  }
}

function oCord_UseInv(Object *theObject, CursorMode mode)
{
  if (cEgo.ActiveInventory == iGreenThread)
  {
    cEgo.Walk(oCord.X + cordOffsetX, oCord.Y + cordOffsetY, eBlock, eWalkableAreas);
    cEgo.FaceDirection(eDirectionLeft);
    cEgo.LockViewOffset(CLARABATHROOMTHREADA_1007, cordOffsetX - 31, cordOffsetY + 1);
    theObject.Visible = false;
    cEgo.Animate(0, 4, eOnce, eBlock, eForwards);
    
    Wait(10);
    
    oCordWithThread.Visible = true;
    cEgo.UnlockView();
    cEgo.FaceDirection(eDirectionLeft);
    cEgo.Walk(cEgo.x - 40, cEgo.y, eBlock, eWalkableAreas);
    cEgo.FaceDirection(eDirectionUp);
    cEgo.LockViewOffset(CLARABATHROOMTHREADA_1007, cordOffsetX - 20, cordOffsetY + 3);
    oCordWithThread.Visible = false;
    oHandle.Visible = false;
    cEgo.Animate(2, 4, eOnce, eBlock, eForwards);
    cEgo.Animate(3, 4, eOnce, eBlock, eForwards);
    cEgo.UnlockView();
    cEgo.FaceDirection(eDirectionUp);
    oCordWithThreadLoose.Visible = true;
    
    cEgo.Walk(cEgo.x - 15, cEgo.y - 2, eBlock, eWalkableAreas);
    cEgo.FaceDirection(eDirectionLeft);

    oCordWithThreadLoose.Visible = false;
    cEgo.LockViewOffset(CLARABATHROOMTHREADB_1008, 12, 0);
    cEgo.Animate(0, 4, eOnce, eBlock, eForwards);
    cEgo.UnlockView();
    oCordWithThreadTaut.Visible = true;
    SetTimer(HANDLE_W_THREAD_TIMER, 100);
    cEgo.FaceDirection(eDirectionLeft);
    cEgo.x -= 2;
    
    //cEgo.LoseInventory(iGreenThread);
  }
}

function oCord_Look(Object *theObject, CursorMode mode)
{
  dPrologue_ChainDesc.Start();
}

function oHandle_Look(Object *theObject, CursorMode mode)
{
  dPrologue_ToiletHandleDesc.Start();
}

function oCordWithThread_Look(Object *theObject, CursorMode mode)
{
  dPrologue_ChainThreadDesc.Start();
}

function hMakeupStand_Look(Hotspot *theHotspot, CursorMode mode)
{
  dPrologue_MakeupStand.Start();
}

function hMirror_Look(Hotspot *theHotspot, CursorMode mode)
{
  dPrologue_MirrorDesc.Start();
}

function hBathroomCouch_Look(Hotspot *theHotspot, CursorMode mode)
{
  dPrologue_BathroomCouchDesc.Start();
}

function oSign_Look(Object *theObject, CursorMode mode)
{
  dPrologue_ServiceSignDesc.Start();
}

function hSink_Look(Hotspot *theHotspot, CursorMode mode)
{
  dPrologue_SinkDesc.Start();
}

function hBathroomWindows_Look(Hotspot *theHotspot, CursorMode mode)
{
  dPrologue_BathroomWindowsDesc.Start();
}

function hToilet_Look(Hotspot *theHotspot, CursorMode mode)
{
  dPrologue_ToiletDesc.Start();
}
